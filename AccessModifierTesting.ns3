Newspeak3
'Tessi-Tests'
class AccessModifierTesting usingPlatform: platform  <Platform> testFramework: utf <Minitest> = (|
    private TestContext = utf TestContext.

    private ClassMirror = platform mirrors ClassMirror.
    private MessageNotUnderstood = platform blackMarket Exceptions MessageNotUnderstood.
|)
(
class ClassAccessingTests = TestContext ((*	Try to access classes with different access modifiers, but only via public send.
	 Class access is implemented via accessor methods - so fetching a class falls back to finding the appropriate method.
	All kind of different method sends are already tested in the MethodAcessingTests. *)|
	testSubjects = TestSubjects new.
|)
('as yet unclassified'
public testPrivateClassAccess = (
	should: [testSubjects APrivateClass] signal: MessageNotUnderstood.
)
public testProtectedClassAccess = (
	should: [testSubjects AClass] signal: MessageNotUnderstood.
)
public testPublicClassAccess = (
	assert: testSubjects APublicClass new ping = 42.
)) : ('as yet unclassified'
TEST_CONTEXT = ())
class MethodAccessingTests = TestContext ((* Test access to methods with different access modifiers *)|
	testSubjects = TestSubjects new.
|)
('implicit receiver send testing'
public testDefaultImplicitReceiverSend = (
	(* send a message via an implicitReceiverSend which goes to a method with default access modifier *)
	assert: testSubjects ASubClass new ASubInnerClass new defaultImplicitReceiverSend = 42.
)
public testDefaultImplicitReceiverSendWithInheritance = (
	(* send a message via implicitReceiverSend which goes to a method with default access modifier with inheritance *)
	assert: testSubjects ASubClass new defaultImplicitReceiverSendWithInheritance = 42.
)
public testPrivateImplicitReceiverSend = (
	(* send a message via implicitReceiverSend which goes to a private method *)
	assert: testSubjects ASubClass new ASubInnerClass new privateImplicitReceiverSend = 42.
)
public testPrivateImplicitReceiverSendWithInheritance = (
	(* send a message via implicitReceiverSend which goes to a private method via inheritance *)
	should: [testSubjects ASubClass new privateImplicitReceiverSendWithInheritance] signal: MessageNotUnderstood.
)
public testProtectedImplicitReceiverSend = (
	(* send a message via implicitReceiverSend which goes to a protected method *)
	assert: testSubjects ASubClass new ASubInnerClass new protectedImplicitReceiverSend = 42.
)
public testProtectedImplicitReceiverSendWithInheritance = (
	(* send a message via implicitReceiverSend which goes to a protected method via inheritance *)
	assert: testSubjects ASubClass new protectedImplicitReceiverSendWithInheritance = 42.
)
public testPublicImplicitReceiverSend = (
	(* send a message via implicitReceiverSend which goes to a public method *)
	assert: testSubjects ASubClass new ASubInnerClass new publicImplicitReceiverSend = 42.
)
public testPublicImplicitReceiverSendWithInheritance = (
	(* send a message via implicitReceiverSend which goes to a public method via inheritance *)
	assert: testSubjects ASubClass new publicImplicitReceiverSendWithInheritance = 42.
)'method override testing'
public testOverriddenMethodInInnerClassPublicSend = (
	should: [testSubjects ASubClass new overriddenMethod] signal: MessageNotUnderstood.
)
public testOverriddenMethodInASubInnerClassClassViaImplicitReceiverSend = (
	assert: testSubjects ASubClass new callOverriddenMethod = 42.
)
public testOverriddenMethodInSuperClassViaImplicitReceiverSend = (
	assert: testSubjects AClass new AInnerClass new callOverriddenMethod = 42.
)
public testOverriddenMethodInSuperClassViaPublicSend = (
	should: [testSubjects AClass new AInnerClass new overriddenMethod] signal: MessageNotUnderstood.
)'outer send testing'
public testDefaultOuterSend = (
	(* send a message via an outerSend which goes to a method with default access modifier *)
	assert: testSubjects AClass new AInnerClass new outerDefaultMethod = 42.
)
public testDefaultOuterSendWithInheritance = (
	(* send a message via an outerSend which goes to a method with default access modifier via inheritance *)
	assert: testSubjects ASubClass new ASubInnerClass new defaultOuterSendWithInheritance = 42.
)
public testPrivateOuterSend = (
	(* send a message via outerSend which goes to a private method *)
	assert: testSubjects AClass new AInnerClass new outerPrivateMethod = 42.
)
public testPrivateOuterSendWithInheritance = (
	(* send a message via outerSend which goes to a private method via inheritance *)
	should: [testSubjects ASubClass new ASubInnerClass new privateOuterSendWithInheritance] signal: MessageNotUnderstood.
)
public testProtectedOuterSend = (
	(* send a message via outerSend which goes to a protected method *)
	assert: testSubjects AClass new AInnerClass new outerProtectedMethod = 42.
)
public testProtectedOuterSendWithInheritance = (
	(* send a message via outerSend which goes to a protected method via inheritance *)
	assert: testSubjects ASubClass new ASubInnerClass new protectedOuterSendWithInheritance = 42.
)
public testPublicOuterSend = (
	(* send a message via outerSend which goes to a public method *)
	assert: testSubjects AClass new AInnerClass new outerPublicMethod = 42.
)
public testPublicOuterSendWithInheritance = (
	(* send a message via outerSend which goes to a public method via inheritance *)
	assert: testSubjects ASubClass new ASubInnerClass new publicOuterSendWithInheritance = 42.
)'public send testing'
public testDefaultPublicSend = (
	(* send a message via publicSend which goes to a method with default access modifier *)
	should: [testSubjects AClass new defaultMethod] signal: MessageNotUnderstood.
)
public testDefaultPublicSendWithInheritance = (
	(* send a message via publicSend which goes to a method with default access modifier in the super chain *)
	should: [testSubjects ASubClass new defaultMethod] signal: MessageNotUnderstood.
)
public testPrivatePublicSend = (
	(* send a message via publicSend which goes to a private method *)
	should: [testSubjects AClass new privateMethod] signal: MessageNotUnderstood.
)
public testPrivatePublicSendWithInheritance = (
	(* send a message via publicSend which goes to a private method in the super chain *)
	should: [testSubjects ASubClass new privateMethod] signal: MessageNotUnderstood.
)
public testProtectedPublicSend = (
	(* send a message via publicSend which goes to a protected method *)
	should: [testSubjects AClass new protectedMethod] signal: MessageNotUnderstood.
)
public testProtectedPublicSendWithInheritance = (
	(* send a message via publicSend which goes to a protected method in the super chain *)
	should: [testSubjects ASubClass new protectedMethod] signal: MessageNotUnderstood.
)
public testPublicPublicSend = (
	(* send a message via publicSend which goes to a public method *)
	assert: testSubjects AClass new publicMethod = 42.
)
public testPublicPublicSendWithInheritance = (
	(* send a message via publicSend which goes to a public method in the super chain *)
	assert: testSubjects ASubClass new publicMethod = 42.
)'self send testing'
public testDefaultSelfSend = (
	(* send a message via selfSend which goes to a method with default access modifier *)
	assert: testSubjects AClass new selfDefaultMethod = 42.
)
public testDefaultSelfSendWithInheritance = (
	(* send a message via selfSend which goes to a method with default access modifier in the super chain *)
	assert: testSubjects ASubClass new selfDefaultMethod = 42.
)
public testPrivateSelfSend = (
	(* send a message via selfSend which goes to a private method*)
	assert: testSubjects AClass new selfPrivateMethod = 42.
)
public testPrivateSelfSendwithInheritance = (
	(* send a message via selfSend which goes to a private method in the super chain *)
	should: [ testSubjects ASubClass new selfPrivateMethod ] signal: MessageNotUnderstood.
)
public testProtectedSelfSend = (
	(* send a message via selfSend which goes to a protected method*)
	assert: testSubjects AClass new selfProtectedMethod = 42.
)
public testProtectedSelfSendWithInheritance = (
	(* send a message via selfSend which goes to a protected method in the super chain *)
	assert: testSubjects ASubClass new selfProtectedMethod = 42.
)
public testPublicSelfSend = (
	(* send a message via selfSend which goes to a public method*)
	assert: testSubjects ASubClass new selfPublicMethod = 42.
)
public testPublicSelfSendWithInheritance = (
	(* send a message via selfSend which goes to a public method in the super chain *)
	assert: testSubjects ASubClass new selfPublicMethod = 42.
)'super send testing'
public testDefaultSuperSend = (
	(* send a message via superSend which goes to a method with default access modifier *)
	assert: testSubjects ASubClass new superDefaultMethod = 42.
)
public testPrivateSuperSend = (
	(* send a message via superSend which goes to a private method *)
	should: [ testSubjects ASubClass new superPrivateMethod ] signal: MessageNotUnderstood.
)
public testProtectedSuperSend = (
	(* send a message via superSend which goes to a protected method *)
	assert: testSubjects ASubClass new superProtectedMethod = 42.
)
public testPublicSuperSend = (
	(* send a message via superSend which goes to a public method *)
	assert: testSubjects ASubClass new superPublicMethod = 42.
)) : ('as yet unclassified'
TEST_CONTEXT = ())
class SlotAccessingTests = TestContext ((*	Try to access slots with different access modifiers, but only via public send.
	 Slot access is implemented via accessor methods - so fetching a slot falls back to finding the appropriate method.
	All kind of different method sends are already tested in the MethodAcessingTests. *)|
	testSubjects = TestSubjects new.
|)
('as yet unclassified'
public testDefaultSlotAccess = (
	should: [testSubjects AClass new privateSlot] signal: MessageNotUnderstood.
)
public testPrivateSlotAccess = (
	should: [testSubjects AClass new privateSlot] signal: MessageNotUnderstood.
)
public testProtectedSlotAccess = (
	should: [testSubjects AClass new protectedSlot] signal: MessageNotUnderstood.
)
public testPublicSlotAccess = (
	assert: testSubjects AClass new publicSlot = 42.
)) : ('as yet unclassified'
TEST_CONTEXT = ())
class TestSubjects = ((* Holds a bunch of classes which we test against *)||)
(
class ASubClass = AClass ((* A class to test access to inherited slots/methods *)||)
(
class ASubInnerClass = ((* A Class to test outerSends in combination with inheritance *)|
				defaultImplicitReceiverTarget		= 42.
	public		publicImplicitReceiverTarget		= 42.
	protected	protectedImplicitReceiverTarget	= 42.
	private		privateImplicitReceiverTarget		= 42.
|)
('as yet unclassified'
public defaultImplicitReceiverSend = (
	^ defaultImplicitReceiverTarget
)
public defaultOuterSendWithInheritance = (
	^ outer ASubClass defaultMethod
)
public privateImplicitReceiverSend = (
	^ privateImplicitReceiverTarget
)
public privateOuterSendWithInheritance = (
	^ outer ASubClass privateMethod
)
public protectedImplicitReceiverSend = (
	^ protectedImplicitReceiverTarget
)
public protectedOuterSendWithInheritance = (
	^ outer ASubClass protectedMethod
)
public publicImplicitReceiverSend = (
	^ publicImplicitReceiverTarget
)
public publicOuterSendWithInheritance = (
	^ outer ASubClass publicMethod
)) : ()'as yet unclassified'
public callOverriddenMethod = (
	^ overriddenMethod
)
public defaultImplicitReceiverSendWithInheritance = (
	^ defaultMethod
)
private overriddenMethod = (
	(*	This is a private method which overrides a a method with the same signature from a super class.
		The super method should never be executed when called from an instance of this class via implicit receiver send. *)
	^ 42
)
public privateImplicitReceiverSendWithInheritance = (
	^ privateMethod
)
public protectedImplicitReceiverSendWithInheritance = (
	^ protectedMethod
)
public publicImplicitReceiverSendWithInheritance = (
	^ publicMethod
)
public selfDefaultMethod = (
	^ self defaultMethod
)
public selfPrivateMethod = (
	^ self privateMethod
)
public selfProtectedMethod = (
	^ self protectedMethod
)
public selfPublicMethod = (
	^ self publicMethod
)
public superDefaultMethod = (
	^ super defaultMethod
)
public superPrivateMethod = (
	^ super privateMethod
)
public superProtectedMethod = (
	^ super protectedMethod
)
public superPublicMethod = (
	^ super publicMethod
)) : ()
class AClass = ((* A class with a default access modifier. Slots and methods of all sort to be tested against *)|
	public		publicSlot		= 42.
	protected	protectedSlot	= 42.
	private		privateSlot		= 42.
				defaultSlot		= 42.
|)
(
class AInnerClass = ((* A class to test outerSends with different accessModifiers.
    We implement all methods/slots to return `nil` so that we can be certain that an outerSend was used.*)|
	public		publicSlot
	protected	protectedSlot
	private		privateSlot	
				defaultSlot
|)
('as yet unclassified'
public callOverriddenMethod = (
	^ overriddenMethod
)
defaultMethod = (
	^ nil
)
public outerDefaultMethod = (
	^ outer AClass defaultMethod
)
public outerPrivateMethod = (
	^ outer AClass privateMethod
)
public outerProtectedMethod = (
	^ outer AClass protectedMethod
)
public outerPublicMethod = (
	^ outer AClass publicMethod
)
private overriddenMethod = (
	(*	This is a private method which overrides a a method with the same signature from an outer class.
		The outer method should never be executed when called from an instance of this class via implicit receiver send. *)
	^ 42
)
private privateMethod = (
	^ nil
)
protected protectedMethod = (
	^ nil
)
public publicMethod = (
	^ nil
)) : ()'as yet unclassified'
defaultMethod = (
	^ 42
)
public overriddenMethod = (
	(*	This is a public method which is overridden by a subclass and an inner class.
		This should never be executed when called from the sub-/inner class via implicit receiver send. *)
	^ 0
)
private privateMethod = (
	^ 42
)
protected protectedMethod = (
	^ 42
)
public publicMethod = (
	^ 42
)
public selfDefaultMethod = (
	^ self defaultMethod
)
public selfPrivateMethod = (
	^ self privateMethod
)
public selfProtectedMethod = (
	^ self protectedMethod
)) : ()
class APrivateClass = ()
() : ()
class APublicClass = ()
('as yet unclassified'
ping = (
	^ 42
)) : ()) : ()) : ()