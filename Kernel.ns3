Newspeak3
'NSKernel'
class Kernel usingPlatform: p vmMirror: vmm = (
"N.B.: Although Newspeak classes and mixins are represented by the Behavior and Mixin classes of this module, not all code here is live in the system.  In particular, true, false and nil are not instances of the True, False and UndefinedObject defined here but still instances of the corresponding Squeak classes.

This code was derived in part by converting the Strongtalk Magnitude, Boolean and  UndefinedObject classes to Newspeak. For those classes, the Sun Microsystems copyright and BSD license below applies.

Copyright 2008 Cadence Design Systems, Inc.
   
Licensed under the Apache License, Version 2.0 (the ''License''); you may not use this file except in compliance with the License.  You may obtain a copy of the License at  http://www.apache.org/licenses/LICENSE-2.0

Copyright (c) 1995-2006 Sun Microsystems, Inc. ALL RIGHTS RESERVED.
Copyright 2008-2009 David Pennell, Stephen Pair, Gilad Bracha and other contributors.
Copyright 2011 Ryan Macnak

Redistribution and use in source and binary forms, with or without modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, 
 this  list of conditions and the following disclaimer.

* Redistribution in binary form must reproduce the above copyright notice, 
this list of conditions and the following disclaimer in the documentation and/o other materials provided with the distribution.

Neither the name of Sun Microsystems or the names of contributors may 
be used to endorse or promote products derived from this software without 
specific prior written permission.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS ''AS IS'' AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, 
THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS 
BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE 
GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT 
LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH 
DAMAGE."|
	private Map = p blackMarket Dictionary.
	private WeakSet = p blackMarket WeakSet.
	private IdentitySet = p blackMarket IdentitySet.
	private MethodDictionary = p blackMarket MethodDictionary.
	"Note we use Squeak's collection classes for serializing/bootstrapping purposes."
	private Language = p blackMarket Language. 
	private Smalltalk = p blackMarket Smalltalk.
	
	private vmmirror = vmm.
	private GetterPool = p blackMarket Mixin getterPool.
	private SetterPool = p blackMarket Mixin setterPool.
	"N.B.: These pools contain anonymous methods: methods whose selector doesn't match the selectors which are their keys in method dictionaries. It is important that Cog can identify these methods as anonymous because it effects the mechanics of unlinking send sites. Anonymous methods are identified by having a nil method class association. So do not change the method class association of these methods!"
	
	private ImplementationBase = p blackMarket ImplementationBase.
	private ArraySlot = Array.
	private ByteArraySlot = ByteArray.
	private CharacterSlot = Character.
	private DelaySlot = Delay.
	private ErrorSlot = Error.
	private FloatSlot = Float.
	private IntegerSlot = Integer.
	private ObjectSlot = Object.
	private StringSlot = String.
	private SymbolSlot = Symbol.
|)
(
class AbstractMixin = (
"A mixin is the difference between a class and its superclass: a set of additional methods, slots and nested class declarations.  Newspeak class declarations implicitly define instance-side and class-side pairs of mixins, and Newspeak classes (other than Top) are all mixin applications.

Most of the methods of this class are to allow mixins to behave like Smalltalk classes when we must deal with Squeak, since Newspeak classes do reside in Squeak's system dictionary, mostly source control (the changes file) and IDE utilities like senders/implementors. These methods are not part of a mature Newspeak system.

In principle, slots common to both instance- and class-side mixins should be defined here. However, for historic reasons the offsets at which the VM expects these slots are not at the beginning of the object, so they are duplicated in InstanceMixin and ClassMixin."|
|)
('as yet unclassified'
acceptsLoggingOfCompilation = (
	^false
)
allSubclassesDo: blk = (
	"IDE crap"
)
allSuperclasses = (
	^{}
)
canUnderstand: sel = (
	^self methodDictionary includesKey: sel
)
classSide = (
	^self theMetaClass
)
compiledMethodAt: selector <Symbol> ^<CompiledMethod> = (
	^ self methodDictionary at: selector
)
definingClass = (
	^self
)
enclosingClass = (
	#BOGUS. "Used by syntax highlighter, at least"
	^self enclosingMixin
)
fileOutChangedMessages: aSet on: aFileStream moveSource: moveSource toFile: fileIndex = (
	"File a description of the messages of this class that have been 
	changed (i.e., are entered into the argument, aSet) onto aFileStream.  If 
	moveSource, is true, then set the method source pointer to the new file position.
	Note when this method is called with moveSource=true, it is condensing the
	.changes file, and should only write a preamble for every method."
	| org |
	(org:: self organization) categories do: 
		[:cat | 
		| sels |
		sels:: (org listAtCategoryNamed: cat) select: [:sel | aSet includes: sel].
		((cat beginsWith: '*') and: [cat endsWith: '-override'])
			ifTrue: [
				sels do:
					[:sel |  self printMethodChunkHistorically: sel on: aFileStream
						moveSource: moveSource toFile: fileIndex]]
			ifFalse: [
				sels do:
					[:sel |  self printMethodChunk: sel withPreamble: true on: aFileStream
						moveSource: moveSource toFile: fileIndex]]]
)
includesSelector: sel <Symbol> ^<Boolean> = (
	"Answer whether the message whose selector is the argument is in the 
	method dictionary of the receiver's class."

	^ self methodDict includesKey: sel 
)
isMixin ^<Boolean> = (
	^true
)
language = (
	^Language newspeak3
)
methodDict = (
	^self methodDictionary
)
mixin = (
	^self
)
moveChangesTo: newFile = (
	"Used in the process of condensing changes, this message requests that 
	the source code of all methods of the receiver that have been changed 
	should be moved to newFile."

	| changes |
	changes:: self methodDict keys select: [:sel |
		(self compiledMethodAt: sel) fileIndex > 1].
	self
		fileOutChangedMessages: changes
		on: newFile
		moveSource: true
		toFile: 2
)
printCategoryChunk: cat on: aFileStream priorMethod: priorMethod = (
	
)
printMethodChunk: selector withPreamble: doPreamble on: outStream
		moveSource: moveSource toFile: fileIndex = (
	"Copy the source code for the method associated with selector onto the fileStream.  If moveSource true, then also set the source code pointer of the method."
	| preamble method oldPos newPos sourceFile endPos |
	doPreamble 
		ifTrue: [preamble:: self name , ' methodsFor: ' ,
					(self organization categoryOfElement: selector) asString printString]
		ifFalse: [preamble:: ''].
	method:: self methodDict at: selector ifAbsent:
		[outStream nextPutAll: selector; cr.
		outStream tab; nextPutAll: '** ERROR!  THIS SCRIPT IS MISSING ** ' translated; cr; cr.
		outStream nextPutAll: '  '.
		^ outStream].

	((method fileIndex = 0
		or: [(SourceFiles at: method fileIndex) == nil])
		or: [(oldPos:: method filePosition) = 0])
		ifTrue:
		["The source code is not accessible.  We must decompile..."
		preamble size > 0 ifTrue: [outStream cr; nextPut: $!; nextChunkPut: preamble; cr].
		outStream nextChunkPut: method decompileString]
		ifFalse:
		[sourceFile:: SourceFiles at: method fileIndex.
		preamble size > 0
			ifTrue:    "Copy the preamble"
				[outStream copyPreamble: preamble from: sourceFile at: oldPos]
			ifFalse:
				[sourceFile position: oldPos].
		"Copy the method chunk"
		newPos:: outStream position.
		outStream copyMethodChunkFrom: sourceFile.
		sourceFile skipSeparators.      "The following chunk may have ]style["
		sourceFile peek == $] ifTrue: [
			outStream cr; copyMethodChunkFrom: sourceFile].
		moveSource ifTrue:    "Set the new method source pointer"
			[endPos:: outStream position.
			method checkOKToAdd: endPos - newPos at: newPos.
			method setSourcePosition: newPos inFile: fileIndex]].
	preamble size > 0 ifTrue: [outStream nextChunkPut: ' '].
	^ outStream cr
)
putClassCommentToCondensedChangesFile: aFileStream = (
)
selectorsAndMethodsDo: selectorAndMethodBlock = (
	"Evaluate the two argument selectorAndMethodBlock for all the selector/method pairs in my method dictionary."

	^ self methodDict keysAndValuesDo: selectorAndMethodBlock
)
superclass = (
	^nil "IDE crap: allLiteralsOf"
)
thoroughWhichSelectorsReferTo: literal special: specialFlag byte: specialByte = (
	"Answer a set of selectors whose methods access the argument as a 
	literal. Dives into the compact literal notation, making it slow but 
	thorough "

	^self whichSelectorsReferTo: literal special: specialFlag byte: specialByte thorough: true
)
wantsChangeSetLogging = (
	^false
)
whichSelectorsReferTo: literal = (
	"Answer a Set of selectors whose methods access the argument as a
literal."

	| special byte |
	special:: Smalltalk hasSpecialSelector: literal ifTrueSetByte: [:b | byte:: b].
	^self whichSelectorsReferTo: literal special: special byte: byte

	"Rectangle whichSelectorsReferTo: #+."
)
whichSelectorsReferTo: literal special: specialFlag byte: specialByte = (
	"Answer a set of selectors whose methods access the argument as a literal."

	^self whichSelectorsReferTo: literal special: specialFlag byte: specialByte thorough: false
)
whichSelectorsReferTo: literal special: specialFlag byte: specialByte thorough: thorough = (
	"Answer a set of selectors whose methods access the argument as a literal. If thorough is true, then dives into the compact literal notation, making it slow but thorough "

	| who = IdentitySet new. |
	self selectorsAndMethodsDo: [ :selector :method |
		(((thorough
			ifFalse: [ method hasLiteral: literal ]
			ifTrue: [ method hasLiteralThorough: literal ]) or: [
				specialFlag and: [ method scanFor: specialByte ] ]) and: [
			literal isVariableBinding not or: [
				"N.B. (method indexOfLiteral: literal) < method numLiterals copes with l;ooking for
				Float bindingOf: #NaN, since (Float bindingOf: #NaN) ~= (Float bindingOf: #NaN)."
				(method indexOfLiteral: literal) ~= 0] ]) ifTrue: [
			who add: selector ] ].
	^who
)) : ()
class Association = (
"Associates an object of type K with an object of type V."|
	public key <K> ::= nil.
	public value <V> ::= nil.
|)
('accessing'
key: k <K> value: v <V> = (
	key:: k.
	value:: v.
)'as yet unclassified'
copy = (
	^class key: key value: value
)'comparing'
= other <Object> ^<Boolean> = (
	"| othera <Assoc[Object,Object]> |"
	"%todo: replace Association with Assoc when typecase is fully impl"
	"othera:: Association[Object,Object] coerce: other else: [ ^false ]."
	^key = other key and: [ value = other value ].
)
hash ^<Int> = (
	^key hash bitXor: value hash
)'printing'
printOn: strm <CharOutputStream> = (
	key printOn: strm.
	strm nextPutAll: ' -> '.
	value printOn: strm.
)) : ('as yet unclassified'
key: k <K> value: v <V> ^<Instance> = (
	^new key: k value: v
))
class Behavior = (
"My instances describe the behavior of other objects. I provide the minimum state necessary for creating and running instances. Most objects are created as instances of the more fully supported subclass, Class, but I am a good starting point for providing instance-specific behavior (as in Metaclass).

At this time, and for the foreseeable future, this class is polluted by a large number of methods necessary to support interaction with the surrounding Squeak Smalltalk implementation. In a pure Newspeak setting, these methods would be handled by mirrors exclusively.  The data in the slots would be private.

Any functionality beyond instantiation, mixin application and answering the class name is a security hole.

"|
private	superclass <Behavior | nil> "Must be slot 1; known to VM"
private	methodDict <MethodDictionary> "Must be slot 2; known to VM"
private	format <SmallInteger> "Must be slot 3; known to VM"
private	mixinSlot <Mixin | nil> "Must be slot 4; known to VM"
private	enclosingObjectSlot <Behavior | nil> "Must be slot 5; known to VM"
|)
('accessing'
inheritsFrom: aClass  = (
	"Answer whether the argument, aClass, is on the receiver's superclass 
	chain."

	| aSuperclass |
	aSuperclass:: superclass.
	[aSuperclass == nil]
		whileFalse: 
			[aSuperclass == aClass ifTrue: [^true].
			aSuperclass:: aSuperclass superclass].
	^false
)
language = (
	^mixinSlot language
)
name = (
	^'someBehavior'	
)
simpleName = (
	^mixin simpleName
)'as yet unclassified'
acceptsLoggingOfCompilation = (
	^false
)
canUnderstand: selector = (
	"Answer whether the receiver can respond to the message whose selector 
	is the argument. The selector can be in the method dictionary of the 
	receiver's class or any of its superclasses."

	(self includesSelector: selector) ifTrue: [^true].
	superclass == nil ifTrue: [^false].
	^superclass canUnderstand: selector
)
classSide = (
	^self theMetaClass
)
enclosingClass = (
	#BOGUS. "Used by syntax highlighter."
	^self enclosingObjectSlot == nil ifFalse: [self enclosingObjectSlot class]
)
organization = (
	^mixin organization 
)
printOn: stm = (
	stm nextPutAll: self name
)
selectorsAndMethodsDo: selectorAndMethodBlock = (
	"Evaluate the two argument selectorAndMethodBlock for all the selector/method pairs in my method dictionary."

	^ self methodDict keysAndValuesDo: selectorAndMethodBlock
)
wantsChangeSetLogging = (
	^false
)'initialize-release'
superclass: sc methodDictionary: md format: fmt = (
	superclass: sc.
	methodDict: md.
	format: fmt.
)'instance creation'
basicNew = (
	"<primitive: 70>"
	^vmmirror instaniateFixedClass: self ifFail: [self halt]
)
basicNew: sizeRequested = (
	"<primitive: 71>"
	^vmmirror instaniateVariableClass: self withSize: sizeRequested ifFail: [self halt]
)
new = (
	^self basicNew
)
new: sizeRequested = (
	^self basicNew: sizeRequested
)'smalltalk-reflection'
allInstVarNames = (
	"Answer an Array of the names of the receiver's instance variables. The 
	Array ordering is the order in which the variables are stored and 
	accessed by the interpreter."

	| vars |
	superclass == nil
		ifTrue: [vars:: self instVarNames copy]	"Guarantee a copy is answered."
		ifFalse: [vars:: superclass allInstVarNames , self instVarNames].
	^vars
)
allInstancesDo: aBlock = (
	"Evaluate the argument, aBlock, for each of the current instances of the 
	receiver.
	
	Because aBlock might change the class of inst (for example, using become:),
	it is essential to compute next before aBlock value: inst."
	| inst next |
	inst: self someInstance.
	[inst == nil]
		whileFalse:
		[
		next: inst nextInstance.
		aBlock value: inst.
		inst: next]
)
allSubclassesDo: aBlock = (
	"Evaluate the argument, aBlock, for each of the receiver's subclasses."

	self subclassesDo: 
		[:cl | 
		aBlock value: cl.
		cl allSubclassesDo: aBlock]
)
allSubclassesDoGently: block = (
	self allSubclassesDo: block
)
allSuperclasses = (
	^{} "IDE crap"
)
compiledMethodAt: selector <Symbol> ^<CompiledMethod> = (
	^ self methodDictionary at: selector
)
decompilerClass = (
	"IDE crap for when synthetic methods are confused as Smalltalk methods"
	^(Smalltalk at: #Compiler) decompilerClass
)
forgetDoIts = (
	"Nothing to do. Newspeak doits aren't installed in the methodDict"
)
includesBehavior: aClass = (
	^self == aClass or:[self inheritsFrom: aClass]
)
includesSelector: sel <Symbol> ^<Boolean> = (
	"Answer whether the message whose selector is the argument is in the 
	method dictionary of the receiver's class."

	^ self methodDict includesKey: sel 
)
instVarIndexFor: instVarName = (
	"Answer the index of the named instance variable."

	| index |
	index:: mixin instVarNames == nil
		ifTrue: [0]
		ifFalse: [mixin instVarNames indexOf: instVarName].
	index == 0 ifTrue: 
		[^superclass == nil 
			ifTrue: [0]
			ifFalse: [superclass instVarIndexFor: instVarName]].
	^superclass == nil 	
		ifTrue: [index]
		ifFalse: [index + superclass instSize]
)
instVarNames = (
	^mixin == nil
		ifTrue: [{} "Top"] 
		ifFalse: [mixin instVarNames]
)
methodDictionary = (
	^methodDict
)
setInstVarNames: instVarArray = (
"	| required |
	required:: self instSize.
	superclass notNil ifTrue:[required:: required - superclass instSize].
	instVarArray size = required
		ifFalse:[^self error: required printString, ' instvar names are required'].
	instVarArray isEmpty
		ifTrue:[instanceVariables:: nil]
		ifFalse:[instanceVariables:: instVarArray asArray]."
)
someInstance = (
	^vmmirror firstInstanceOf: self ifNone: [nil]
)
sourceCodeAt: selector = (
	"IDE crap for when synthetic methods are confused as Smalltalk methods"
	^ (self methodDict at: selector) getSourceFor: selector in: self
)
thoroughWhichSelectorsReferTo: literal special: specialFlag byte: specialByte = (
	"Answer a set of selectors whose methods access the argument as a 
	literal. Dives into the compact literal notation, making it slow but 
	thorough "

	^self whichSelectorsReferTo: literal special: specialFlag byte: specialByte thorough: true
)
whichSelectorsReferTo: literal special: specialFlag byte: specialByte = (
	"Answer a set of selectors whose methods access the argument as a literal."

	^self whichSelectorsReferTo: literal special: specialFlag byte: specialByte thorough: false
)
whichSelectorsReferTo: literal special: specialFlag byte: specialByte thorough: thorough = (
	"Answer a set of selectors whose methods access the argument as a literal. If thorough is true, then dives into the compact literal notation, making it slow but thorough "

	| who = IdentitySet new. |
	self selectorsAndMethodsDo: [ :selector :method |
		(((thorough
			ifFalse: [ method hasLiteral: literal ]
			ifTrue: [ method hasLiteralThorough: literal ]) or: [
				specialFlag and: [ method scanFor: specialByte ] ]) and: [
			literal isVariableBinding not or: [
				"N.B. (method indexOfLiteral: literal) < method numLiterals copes with l;ooking for
				Float bindingOf: #NaN, since (Float bindingOf: #NaN) ~= (Float bindingOf: #NaN)."
				(method indexOfLiteral: literal) ~= 0] ]) ifTrue: [
			who add: selector ] ].
	^who
)'testing'
instSize = (
	"Answer the number of named instance variables
	(as opposed to indexed variables) of the receiver."

	self flag: #instSizeChange.  "Smalltalk browseAllCallsOn: #instSizeChange"
"
	NOTE: This code supports the backward-compatible extension to 8 bits of instSize.
	When we revise the image format, it should become...
	^ ((format bitShift: -1) bitAnd: 16rFF) - 1
	Note also that every other method in this category will require
	2 bits more of right shift after the change.
"
	^ ((format bitShift: -10) bitAnd: 16rC0) + ((format bitShift: -1) bitAnd: 16r3F) - 1
)
instSpec = (
	^ (format bitShift: -7) bitAnd: 16rF
)
isBehavior = (
	"Return true if the receiver is a behavior"
	^true
)
isBits = (
	"Answer whether the receiver contains just bits (not pointers)."

	^ self instSpec >= 6
)
isBytes = (
	"Answer whether the receiver has 8-bit instance variables."

	^ self instSpec >= 8
)
isFixed = (
	"Answer whether the receiver does not have a variable (indexable) part."

	^self isVariable not
)
isMeta = (
	^ false
)
isMixinApplication = (
	^true "All instances of this Behavior class are.  Squeak Behaviors are not."
)
isPointers = (
	"Answer whether the receiver contains just pointers (not bits)."

	^self isBits not
)
isVariable = (
	"Answer whether the receiver has indexable variables."

	^ self instSpec >= 2
)
isWeak = (
	"Answer whether the receiver has contains weak references."
	^ self instSpec = 4
)
isWords = (
	"Answer whether the receiver has 16-bit instance variables."
	^self isBytes not
)) : ()
class Boolean = Value (
"Boolean is an abstract class defining the protocol for logic testing operations and conditional control structures for the logical values represented by the instances of its subclasses True and False."self error: 'You may not create any more Booleans - this is two-valued logic')
('as yet unclassified'
asAlien ^ <Integer> = (

	"Convert to C boolean representation"

	self subclassResponsibility
)'control'
and: f <[^Boolean]>	 ^ <Boolean> = (

	self subclassResponsibility
)
ifFalse: then <[]> ^ <Object> = (

	self subclassResponsibility
)
ifFalse: then <[^X def]> ifTrue: else <[^Y def]> ^ <X|Y> = (

	self subclassResponsibility
)
ifTrue: then <[]> ^ <Object> = (

	self subclassResponsibility
)
ifTrue: then <[^X def]> ifFalse: else <[^Y def]> ^ <X|Y> = (

	self subclassResponsibility
)
or: f <[^Boolean]>	^ <Boolean> = (

	self subclassResponsibility
)'operations'
& other <Boolean>^ <Boolean> = (

	"and"

	self subclassResponsibility
)
not ^ <Boolean> = (


	self subclassResponsibility
)
xor: other <Boolean> ^ <Boolean> = (

	"exclusive-or"

	^self ~~ other
)
| other <Boolean> ^ <Boolean> = (

	"or"

	self subclassResponsibility
)'testing'
isBoolean ^<Boolean> = (
	^true
)'utility'
onFalse: blk <[^Object]> ^ <Boolean> = (

	"Like onTrue:, but evaluates block when false."

	self subclassResponsibility
)
onTrue: blk <[^Object]>  ^<Boolean> = (

	"Like ifTrue: except that the receiver is returned.  This is sometimes useful when an expensive
	  side effect is needed based on the result of (a conditional expression whose
	  result must be returned or preserved), and where factoring out parts of the conditional expression
	  would obfusticate the control flow.   Example:  

				^<john can run a marathon>
						onTrue: [ <john, run marathon> ] 

	  is prettier (some think) than any of the alternatives:

		1) 	^<john can run a marathon>
						ifTrue: [ <john, run marathon>.
									  true ]
						ifFalse: [ false ]

		2)		| result <Boolean> |
				result :=  <john can run a marathon>.
				result
					ifTrue: [ <john, run marathon> ].
				^result

		3)		^<john can run a marathon> and: [ <john, run marathon>.  true ]
	  "

	self subclassResponsibility
)) : ()
class Class = Behavior (
"My instances describe the representation and behavior of objects.  Except for Top, my instances are the result of mixin applications.

The slot 'subclasses' is a redundant structure.  It is never used during execution, but is used by the development system to simplify or speed certain operations.

See comments in Behavior regarding problems wrt IDE support and security.
"|
 private subclassesSlot <WeakSet>
 private nameSlot <Symbol> "N.B.: This must not be called name.  Otherwise its setter will conflict classes such as CLibrary that have #name: as their primary factory."
|)
('as yet unclassified'
addSubclass: subclass = (

	self assert: [subclass superclass == self] message: 'Not my subclass!'.

	subclassesSlot == nil ifTrue: [subclassesSlot: WeakSet new].
	
	subclassesSlot add: subclass.
)
basicCategory = (
	^mixin category
)
category = (
	^mixin category
)
category: newCategory = (
	#BOGUS.
	#WARN.
)
isMeta = (
	^false
)
mixin = (
	^self mixinSlot
)
mixinApply: superklass = (
	| application |
	application:: self mixin apply: superklass.
	application setEnclosingObjectSlot: self enclosingObjectSlot.
	application class setEnclosingObjectSlot: self class enclosingObjectSlot.
	^application
)
name = (
	^self nameSlot
)
removeSubclass: aSubclass = (
	"If the argument, aSubclass, is one of the receiver's subclasses, remove it."

	subclassesSlot == nil ifFalse: [
		subclassesSlot remove: aSubclass ifAbsent: []
	].
)
setEnclosingObjectSlot: e = (
	enclosingObjectSlot: e.
	self class setEnclosingObjectSlot: e.
)
setName: n = (
	self nameSlot: n
)
subclasses= (
	^subclassesSlot ifNil: [{}]
)
subclassesDo: aBlock = (
	"Evaluate the argument, aBlock, for each of the receiver's immediate subclasses."

	subclasses == nil  ifFalse: [ subclasses do: aBlock ].
)
theMetaClass = (
	^self class
)
theNonMetaClass = (
	^self
)) : ()
class ClassMixin = AbstractMixin (
"A class-side mixin is the difference between a metaclass and Class. (In Newspeak metaclasses are direct subclasses of Class, unlike Smalltalk where they are subclasses of the class's superclass's metaclass.) Note that class-side mixins can only introduce additional methods, unlike instance-side mixins which can introduce slots, methods and nested classes. Because of this, module definitions (top-level classes) are always stateless.
"|
	organization <ClassOrganizer>
	methodDictionary <MethodDictionary> "must be slot 2; known to VM"
	instanceMixin <InstanceMixin>
	enclosingMixin <InstanceMixin> "must be slot 4; known to VM"
|)
('as yet unclassified'
applications = (
	^{}
)
instVarNames = (
	^Array new
)
isMeta ^<Boolean> = (
	^true
)
name = (
	^instanceMixin name , ' class'
)
printOn: stm = (
	stm nextPutAll: name , ' mixin'
)
simpleName = (
	^name
)
slots = (
	^Array new
)
theMetaClass = (
	^self
)
theNonMetaClass = (
	^instanceMixin
)) : ()
class False = Boolean ("False defines the behavior of its single instance, false -- logical negation. Notice how the truth-value checks become direct message sends, without the need for explicit testing.

Be aware however that most of these methods are not sent as real messages in normal use. Most are inline coded by the compiler as test and jump bytecodes - avoiding the overhead of the full message sends. So simply redefining these methods here will have no effect.")
('as yet unclassified'
asAlien ^ <Integer> = (
	^0
)'control'
&& f <[^Boolean]> ^ <Boolean> = (
	^false
)
and: f <[^Boolean]> ^ <Boolean> = (
	^false
)
ifFalse: then <[]> ^ <Object> = (
	^then value
)
ifFalse: then <[^X def]> ifTrue: else <[^Y def]> ^ <X|Y> = (
	^then value
)
ifTrue: then <[]> ^ <Object> = (
	^nil
)
ifTrue: then <[^X def]> ifFalse: else <[^Y def]> ^ <X | Y> = (
	^else value
)
or: f <[^Boolean]> ^ <Boolean> = (
	^f value
)
|| f <[^Boolean]> ^ <Boolean> = (
	^f value
)'operations'
& other <Boolean> ^ <Boolean> = (
	^false
)
not ^ <Boolean> = (
	^true
)
| other <Boolean> ^ <Boolean> = (
	^other
)'printing'
printOn: strm <CharOutputStream>
 = (
	strm nextPutAll: 'false'
)'utility'
onFalse: blk <[^Object]> ^ <Boolean> = (
	blk value.
	^self
)
onTrue: blk <[^Object]> ^ <Boolean> = (
	^self
)) : ()
class InstanceMixin = AbstractMixin (
"An instance-side mixin is the difference between a (non-meta) class and its superclass: a set of additional methods, slots and nested class declarations.

All of my members except methods to invoke mixin application should be hidden, and be only accessible by mirrors."|
name <Symbol>
methodDictionary <MethodDictionary> "must be slot 2; known to VM"
slots <List[{Symbol.Boolean}]> "name->mutable?"
enclosingMixin <InstaceMixin | nil> "must be slot 4; known to VM"
nestedMixins <Map[Symbol,InstanceMixin]> = Map new. "must be called nestedMixins; known to compiler"
applications <WeakSet> = WeakSet new.
classMixin <ClassMixin>
cachedHeaderSource <String>
cachedConstructorName <Symbol>
category <Symbol> ::= #Uncategorized.
organization <ClassOrganizer> "Yuck"
|)
('accessing'
apply: superclass <Class> ^<Class> = (
	^self perform: #|> with: superclass
)
apply: superclassArg <Class> withName: n <String> ^ <Class> = (

	| superclass <Class> invocation <Class> offset <Integer>  |

	superclass:: superclassArg yourself. "Extract real object from potential Delay"
 
	#BOGUS yourself. "Use named supplied as arg"
	invocation:: self invokeWithSuperclass: superclass withName: n.
	"invocation basicCategory: #'Irrelevant-Mixin-applications'."

	offset:: superclass instSize.
	offset ~= 0 ifTrue:[self generateAccessorsFor: invocation offset: offset].

	applications add: invocation.
	^invocation
)'as yet unclassified'
classComment = (
	
	^(Language newspeak3 parserInstance classHeader 
		parse: cachedHeaderSource readStream) classComment
)
classVarNames = (
	^{}
)
constructorName = (
	^cachedConstructorName
)
definingModule = (
	^enclosingMixin isNil ifTrue: [self] ifFalse: [enclosingMixin definingModule]
)
format: nInstVars <Integer> variable: isVar <Boolean> words: isWords <Boolean> pointers: isPointers <Boolean> weak: isWeak <Boolean> ^ <Integer> = (
	"This method was derived from the same method in ClassBuilder, and
	somwewhat cleaned up
	
	Compute the format for the given instance specification.
	nInstVars - the number of named inst vars (e.g. Point's x,y => nInstVars = 2).
	isVar  true if indexable inst vars.
	isWords  true for bit objects not organized as bytes (Float wpould you believe)
	isPointers  true for pointer objects (except CompiledMethod) false otherwise
	isWeak, true if indexable, named weak collection
	"
	| cClass instSpec sizeHiBits fmt |
"	NOTE: This code supports the backward-compatible extension to 8 bits of instSize. The format word is...
		<2 bits=instSize//64><5 bits=cClass><4 bits=instSpec><6 bits=instSize\\64><1 bit=0>"
	sizeHiBits:: (nInstVars+1) // 64.
	cClass:: 0.  "for now"
	instSpec:: isWeak
		ifTrue:[4]
		ifFalse:[isPointers
				ifTrue: [isVar
						ifTrue: [nInstVars>0 ifTrue: [3] ifFalse: [2]]
						ifFalse: [nInstVars>0 ifTrue: [1] ifFalse: [0]]]
				ifFalse: [isWords ifTrue: [6] ifFalse: [8]]].
	fmt:: sizeHiBits.
	fmt:: (fmt bitShift: 5) + cClass.
	fmt:: (fmt bitShift: 4) + instSpec.
	fmt:: (fmt bitShift: 6) + ((nInstVars+1)\\64).  "+1 since prim size field includes header"
	fmt:: (fmt bitShift: 1). "This shift plus integer bit lets wordSize work like byteSize"
	^fmt
)
generateAccessorsFor: invocation <Class> offset: offset <Integer> = (
	| ivs <Collection[String]> mtds <MethodDictionary> |
	ivs:: invocation instVarNames.
	mtds:: invocation methodDict.
	(offset +1  to: offset + ivs size) with: slots do:[:n <Integer> :slot <{Name.Mutable}> |
		
		mtds at: slot first asSymbol put: (GetterPool at: n).
		slot last 
			ifTrue: [mtds at: (slot first, ':') asSymbol put: (SetterPool at: n)]
			ifFalse: [mtds at: ('setOnce`', slot first, ':') asSymbol put: (SetterPool at: n)].
	]
)
instVarAt: n = (
	n = 14 ifTrue: [^self]. "compiler compatability"
	^super instVarAt: n
)
instVarNames = (
	^(slots collect: [:pair | pair first]) asArray
)
invokeWithSuperclass: superclass <Class> withName: n  <String> ^<Class> = (

	| fmt <Integer> newMeta <Metaclass> newClass <Class> env |

	newMeta:: Metaclass new.
	newMeta 
		superclass: Class
		methodDictionary: classMixin methodDictionary copy
		format: Class format.

	"newMeta setInstVarNames: {}."
	newClass:: newMeta new.
	fmt:: self pointerFormatNumberOfInstVars: superclass instSize + slots size .
	newClass
		superclass: superclass
		methodDictionary: methodDictionary copy
		format: fmt.
	newClass setInstVarNames: instVarNames.
	newClass setName: n.
	
	newClass mixinSlot: self.
	newMeta mixinSlot: self classMixin.
	
	applications add: newClass.
	superclass addSubclass: newClass.
	
	^newClass
)
isMeta ^<Boolean> = (
	^false
)
languageData = (
	^cachedHeaderSource
)
nestedClasses = (
	^nestedMixins values
)
pointerFormatNumberOfInstVars: n <Integer> ^ <Integer> = (
	^self format: n variable: false words: false pointers: true weak: false
)
printClass = (
"print the source for this class in the top level syntax"

| stream |
stream:: self language createOutputStreamForClassNamed: self name.
self printTopClassOn: stream.
stream close.
^stream fullName
)
printClassOn: stm = (

	stm nextPutAll: 'class '.
	stm nextPutAll: cachedHeaderSource.

	stm cr; nextPut: $(.
	nestedMixins values do: [:ea | stm cr. ea printClassOn: stm].
	writeOrganizationOf: self on: stm.
	stm nextPut: $).

	stm nextPutAll: ' : ('; cr.
	writeOrganizationOf: classMixin on: stm.
	stm nextPut: $).
)
printOn: stm = (
	stm nextPutAll: name , ' mixin'
)
printTopClassOn: stm = (

	stm nextPutAll: 'Newspeak3'; cr; nextPutAll: category asString printString; cr.

	stm nextPutAll: 'class '.
	stm nextPutAll: cachedHeaderSource.

	stm cr; nextPut: $(.
	nestedMixins values do: [:ea | stm cr. ea printClassOn: stm].
	writeOrganizationOf: self on: stm.
	stm nextPut: $).

	stm nextPutAll: ' : ('; cr.
	writeOrganizationOf: classMixin on: stm.
	stm nextPut: $).
)
sharedPools = (
	^{}
)
simpleName = (
	^(name subStrings: {$`}) last asSymbol
)
superclass = (
	^Object
)
theMetaClass = (
	^classMixin
)
theNonMetaClass = (
	^self
)
traitComposition = (
	^{}
)
writeOrganizationOf: mxn on: aStream = (
	| org <ClassOrganizer> |
	org:: mxn organization.

	(org elementArray isEmpty and:
		[ (org categories copyWithout: org class nullCategory) isEmpty]) ifTrue: [^self].

	org categories do:
		[ :cat | | sels |
		aStream  nextPut:$'; nextPutAll: cat;  nextPut:$'; cr.
		sels:: org listAtCategoryNamed: cat.
		sels do: [ :sel | aStream nextPutAll: (mxn compiledMethodAt: sel) getSource; cr; cr ]]
)
|> superclassArg <Class> ^<Class> = (
	| superclass = superclassArg yourself. | "Extract real object from potential Delay"
	^self apply: superclass withName: (self simpleName, 'mixin |>', superclass name)
)) : ()
class Magnitude = (
"A Magnitude[M] is an object that has a linear relationship with objects of type M, such
that they can be compared using the relational operations <,>,<=,>=.

%note: The derived operations are carefully defined to use <
    and not = for the following reasons:
        - speed
        - use of = may be bad for subclasses with a problematic definition of =,
          such as floating point numbers.
"|
|)
('comparing'
< other <M> ^<Boolean> = (


	self subclassResponsibility
)
<= other <M> ^<Boolean> = (


	"%todo: when using inherited types, use the original definition:
		^(other < self) not
	"
	^self < other or: [ self = other ]
)
> other <M> ^<Boolean> = (


	"%todo: for inherited types use:
		^other < self
	"
	^(self <= other) not
)
>= other <M> ^<Boolean> = (


	^(self < other) not
)
between: min <M> and: max <M> ^<Boolean> = (

	"test whether the receiver is in the range min -> max, inclusive"

	"test the max case first since end of range overflow is generally more common"
	^self <= max and: [ self >= min ]
)
compare: other <M>  ^<Int> = (

	"Returns -1 if self < other, 0 if self = other, or 1 if self > other"

	^self < other
		ifTrue: [ -1 ]
		ifFalse: [ self > other
							ifTrue: [ 1 ] ifFalse: [ 0 ]  ]
)
max: other <ARG> ^<Self | ARG> = (
		"{where X is arg 1 of #< message of receiverType;
			where ARG <X> is arg 1}"

	"The guaranteed is safe because of the inference clause"
	^self > ("guaranteed <M>:\" other)
		ifTrue: [ self ]
		ifFalse: [ other ]
)
min: other <ARG> ^<Self | ARG> = (
		"{where X is arg 1 of #< message of receiverType;
			where ARG <X> is arg 1}"

	"The guaranteed is safe because of the inference clause"
	^self < ("guaranteed <M>" other)
		ifTrue: [ self ]
		ifFalse: [ other ]
)) : ('utility'
defaultSort ^<[X,M, ^Boolean]> = (
	"{where X is returnType of #new message of receiverType}"
	"return a sort predicate suitable for a sorted collection of M"

	"The guarantee is safe because of the inference clause"
	^"guaranteed <[X,M,^Boolean]>"
		[ :m1 <Magnitude[M]> :m2 <M> | m1 <= m2 ]
))
class Metaclass = Behavior (
"Each class is an instance of a unique metaclass. All metaclasses are instances
of Metaclass, and are direct subclasses of Class. This differs from Smalltalk, where the metaclass would be a subclass of the metaclass of the class's superclass.
See comments in Behavior regarding problems wrt IDE suppport and security.
"|
	private thisClass <Instance>
|)
('as yet unclassified'
category = (
	^theNonMetaClass category
)
isMeta = (
	^true
)
mixin ^<ClassMixin> = (
	^self mixinSlot
)
name = (
	thisClass == nil
		ifTrue: [^'a Metaclass']
		ifFalse: [^thisClass name , ' class']
)
new = (
	"The receiver can only have one instance. Create it or complain that
	one already exists."

	thisClass class == self
		ifTrue: [self error: 'A Metaclass should only have one instance!']
		ifFalse: [thisClass: self basicNew. ^thisClass]
)
removeSubclass: aClass = (
	"Do nothing."
)
setEnclosingObjectSlot: e = (
	enclosingObjectSlot: e
)
subclassesDo: aBlock = (
	"Evaluate aBlock for each of the receiver's immediate subclasses."
	thisClass subclassesDo:[:aSubclass|
		"The following test is for Class class which has to exclude
		the Metaclasses being subclasses of Class."
		aSubclass isMeta ifFalse:[aBlock value: aSubclass class]].
)
theMetaClass = (
	^self
)
theNonMetaClass = (
	^thisClass
)) : ()
class Object = ImplementationBase (
"Object is the root of the object hierarchy in the Newspeak programming language, and provides behavior common to all objects.  The superclass of Object is the empty class Top.  The purpose of Top is to allow all code to live in mixins.

For each protocol P in the system, this class provides a method 'isP' which answers true if and only if the receiver is an instance of a class which implements the protocol P.

Unlike other systems inspired by Smalltalk, the number of methods in this class is deliberately kept to a minimum. Behavior should only be provided by this class if it should be considered part of the core set of features available to *all* Newspeak programmers.

The security model of the Newspeak programming language is based on capabilities and it is essential that capabilities cannot leak through behavior provided by this class. For example, this class provides no mechanism for accessing instance variables of its instances. Instead such capabilities are provided to the debugger and other tools through mirror based reflection modules.

Object has no instance variables, nor should any be added. This is due to several classes of objects that inherit from Object that have special implementations (SmallInteger and UndefinedObject for example) or the VM knows about and depends on the structure and layout of certain standard classes."|
|)
('accessing'
class = (
	"Answers the class of the receiver.
	Should not be overriden by subclasses."
	^vmmirror classOf: self
)
yourself = (
	^self
)'as yet unclassified'
cloneX = (
	#BOGUS. "It appears the JIT VM is not happy the standard VMMirror trick, so for now we inherit the primitive from ImplementationBase."
	^vmmirror clone: self
)
subclassResponsibility = (
	self Error signal: 'My subclass should have overridden ', thisContext sender selector printString
)'comparing'
= other <Object> ^<Boolean> = (
	"Answers true if the receiver is considered equal to 'other'.
	May be overridden by subclasses, and should be overridden if the method 'hash' is overridden.
	This method must implement an equivalence relation, that is, the following must hold:
	* x = x evaluates to true
	* if x = y evaluates to true then y = x must evaluate to true
	* if x = y evaluates to true and y = z evaluates to true then x = z must evaluate to true.
	Furthermore, this method must be consistent with the implementation of hash, that is:
	* if x = y evaluates to true then x hash = y hash must evaluate to true."
	^self == other
)
== other <Object> ^<Boolean> = (
	"Answers true if the receiver is the same object as 'other'.
	Should not be overridden by subclasses."
	^vmmirror is: self identicalTo: other
)
hash ^<Integer> = (
	"Answers the hash code for the receiver.
	May be overridden by subclasses, and should be overridden if the method '=' is overridden.
	The hash code must obey the requirements set forth in the method '=', that is, the following must hold:
	* if x = y evaluates to true then x hash = y hash must evaluate to true."
	^self identityHash
)
identityHash ^<Integer> = (
	"Answers the identity hash code of the reciever.
	Should not be overriden by subclasses."
	^vmmirror identityHashOf: self
)'message not understood'
doesNotUnderstand: aMessage = (

	| sel |
	sel: aMessage selector.
	sel isUnary ifTrue: [(sel startsWith: 'is') ifTrue: [^false]].

	(sel startsWith: 'initializer`') ifTrue: [ 
		msg setSelector: (sel allButFirst: 12) asSymbol.
		^msg sentTo: self.
	].

	^ MessageNotUnderstood new 
		message: aMessage;
		receiver: self;
		signal
)'newspeak requirement'
Array = (
	^ArraySlot
)
ByteArray = (
	^ByteArraySlot
)
Character = (
	^CharacterSlot
)
Delay = (
	^DelaySlot
)
Error = (
	^ErrorSlot
)
Float = (
	^FloatSlot
)
Future = (
	^DelaySlot
)
Integer = (
	^IntegerSlot
)
Object = (
	^ObjectSlot
)
String = (
	^StringSlot
)
Symbol = (
	^SymbolSlot
)'pattern matching extension'
Pattern = (
	^patternMatching PatternClass
)
case: pattern otherwise: fail = (
	| MatchFailure tempResult |
	MatchFailure: patternMatching Binding new.
	tempResult:: pattern doesMatch: self else: [ MatchFailure ].

	^ tempResult == MatchFailure
		ifTrue: [ pattern matchFailedFor: self escape: fail ]
		ifFalse: [ tempResult boundValue ].
)'printing'
printOn: s <WriteStream> ^<self> = (
	"Prints a representation of the receiver on the given stream.
	The printed representation is for debugging purposes only and should not be relied on for any other purpose whatsoever.
	May be overridden in subclasses."
	s
		nextPutAll: self class name;
		nextPut: $#;
		nextPutAll: self identityHash printString
)
printString ^<String> = (
	"Returns a printed representation of the receiver.
	The printed representation is for debugging purposes only and should not be relied on for any other purpose whatsoever.
	May be overridden in subclasses."
	^self String streamContents: [ :s | self printOn: s]
)'protected'
assert: condition <[^Boolean]> message: message = (
	"Raises an error with the given message if and only if condition evaluates to false."
	condition isBlock ifFalse: [Error signal: 'Block required'].
	condition value ifFalse: [Error signalAssertionFailure: message]
)'testing'
isImmutable "^<Boolean>" = (
	"Answer true if and only if the receiver is immutable.
	Should not be overridden in subclasses."
	^vmmirror isImmutable: self
)) : ()
class True = Boolean (
"True defines the behavior of its single instance, true -- logical assertion. Notice how the truth-value checks become direct message sends, without the need for explicit testing.

Be aware however that most of these methods are not sent as real messages in normal use. Most are inline coded by the compiler as test and jump bytecodes - avoiding the overhead of the full message sends. So simply redefining these methods here will have no effect.")
('as yet unclassified'
asAlien ^ <Integer> = (
	^1
)'control'
&& f <[^Boolean]> ^ <Boolean> = (
	^f value
)
and: f <[^Boolean]>	 ^ <Boolean> = (
	^f value
)
ifFalse: then <[]> ^ <Object> = (
	^nil
)
ifFalse: then <[^X def]> ifTrue: else <[^Y def]> ^ <X|Y> = (
	^else value
)
ifTrue: then <[]> ^<Object> = (
	^then value
)
ifTrue: then <[^X def]> ifFalse: else <[^Y def]> ^ <X|Y> = (
	^then value
)
or: f <[^Boolean]> ^ <Boolean> = (
	^true
)
|| f <[^Boolean]>^ <Boolean> = (
	^true
)'operations'
& other <Boolean> ^ <Boolean> = (
	^other
)
not ^ <Boolean> = (
	^false
)
| other <Boolean> ^ <Boolean> = (
	^true
)'printing'
printOn: strm <CharOutputStream>
 = (
	strm nextPutAll: 'true'
)'utility'
onFalse: blk <[^Object]> ^ <Boolean> = (
	^self
)
onTrue: blk <[^Object]> ^ <Boolean> = (
	blk value.
	^self
)) : ()
class UndefinedObject = Value (
"UndefinedObject's sole instance is nil. nil represents a prior value for variables that have not been initialized, or for results which are meaningless.")
('comparing'
= other <Object> ^<Boolean> = (
	^other isNil
)'copying'
copy ^<Instance> = (
	self messageNotUnderstood: 
		(self "Message" receiver: self selector: #copy arguments: nil "empty array")
)'printing'
printOn: strm <CharOutputStream> = (
	strm putAll: 'nil'
)'testing'
isNil ^<Boolean> = (
	^true
)
notNil ^<Boolean> = (
	^false
)) : ()
class Value = (
"Some objects cannot change after they are created. They are deeply immutable and known as value objects. A value object is globally unique, in the sense that no other object is equal to it.

An object o is a value object iff, under the assumption that o is a value object, it can be shown that:
-All its slots are immutable and contain value objects.
-Its enclosing objects are all value objects.
-Its class inherits from class Value and does not override its identity method (==).

Examples of such objects are numbers, booleans, characters, literal strings, symbols and module definitions.")
('as yet unclassified'
public copy ^<Instance> = (
	^self
)) : ()'as yet unclassified'
MessageNotUnderstood = (
	"Must be referred to indirectly so it is not serialized."
	^Smalltalk at: #MessageNotUnderstood
)
MutableString = (
	^String
)
SourceFiles = (
	"Must be referred to indirectly so it is not serialized."
	^Smalltalk at: #SourceFiles
)
TODO = (
	"
	Behavior heirarchy to use proper factory method initialization.
	
	Mixin/ClassMixin to use proper initialization.
	
	Mark private methods/slots.
	"
)
patternMatching = (
	^Smalltalk at: #CachedPatternMatching ifAbsentPut: [
		(Smalltalk at: #NSPatternMatching) 
			usingPlatform: (Smalltalk at: #BlackMarket) platform].
)
takeOver = (	

	(takeOver: 
		Class class class enclosingObjectSlot) 
			ifFalse: ['Class takeOver did nothing' out].
	
	(takeOver:
		(Smalltalk at: #NSCompilerTestingConfiguration) 
			mixin class enclosingObjectSlot)
				ifFalse: ['Via namespace takeOver did nothing' out].

	(takeOver: 
		(Smalltalk at: #Collections) class class class class enclosingObjectSlot)
				ifFalse: ['Via namespace takeOver did nothing' out].					
)
takeOver: oldKernel <Kernel> = (	
	| old neu |

	oldKernel == self ifTrue: [^false "Already in charge"].

	old:: {
		oldKernel Class. oldKernel Class class.	 	
		oldKernel Behavior. oldKernel Behavior class.
		oldKernel ClassMixin. oldKernel ClassMixin class.
		oldKernel Metaclass. oldKernel Metaclass class.	
		oldKernel InstanceMixin. oldKernel InstanceMixin class.
		oldKernel Object. oldKernel Object class}.

	neu:: {
		self Class. self Class class.
		self Behavior. self Behavior class.
		self ClassMixin. self ClassMixin class.
		self Metaclass. self Metaclass class.	
		self InstanceMixin. self InstanceMixin class.
		self Object. self Object class}.	

	1 to: old size do: [:i |		
		(old at: i) == (neu at: i) ifTrue: [shouldNotBeEqual].
		"(old at: i) simpleName = (neu at: i) simpleName ifFalse: [shouldBeEqual]."	
	].

	old elementsForwardIdentityTo: neu.
	^true
)) : ()