Newspeak3
'NS2V8'
class NS2V8ApplicationPackaging packageUsing: ns = ((* An app for the Newspeak-to-JavaScript compiler.

Derived from Newspeak2DartApplicationPackaging.

Copyright 2012 SAP AG.
Copyright 2012 Google Inc.
Copyright 2013 Ryan Macnak

Licensed under the Apache License, Version 2.0 (the ''License''); you may not use this file except in compliance with the License.  You may obtain a copy of the License at  http://www.apache.org/licenses/LICENSE-2.0 *)|
	CombinatorialParsing = ns NS2CombinatorialParsing CombinatorialParsing.
	Grammar = ns NewspeakGrammar.
	ASTs = ns NewspeakASTs.
	Parsing = ns NewspeakParsing.
	Generation = ns JavascriptGeneration.
	Mirrors = ns CompilerMirrorsForDart.
	Compilation = ns Newspeak2V8Compilation mixinApply: ns NewspeakCompilation.
	
	Runtime = ns RuntimeForV8.
|)
(
class ApplicationPackager usingPlatform: p = (|
	private parserLib = CombinatorialParsing usingPlatform: p.
	private grammar = Grammar parserLib: parserLib.
	private asts = ASTs usingLib: p.
	private parsing = Parsing usingPlatform: p grammar: grammar asts: asts.
	private generation = Generation usingPlatform: p.
	private mirrors = Mirrors usingPlatform: p.
	private compilation = Compilation
		usingPlatform: p
		parsing: parsing
		generation: generation
		mirrors: mirrors. 
	
	private OrderedCollection = p collections OrderedCollection.
	private PNGReadWriter = p squeak PNGReadWriter.
	private FileStream = p squeak FileStream.
	
	outDirectory
	|)
(
class FlatBlackMarket usingSmalltalk: st = (|
	Smalltalk = st.
|)
('as yet unclassified'
doesNotUnderstand: msg = (
	^Smalltalk at: msg selector ifAbsent: [super doesNotUnderstand: msg]
)) : ()
class ImportsRecorder usingNamespace: ns = (|
	namespace = ns.
	imports = OrderedCollection new.
|)
('as yet unclassified'
doesNotUnderstand: message = (
	imports add: message selector.
	^message sendTo: namespace
)) : ()'accessing'
public compileAppDefn: appDefn usingNamespace: ns to: output = (
	|
	resourcePaths = OrderedCollection new. 
	builder = compilation ProgramBuilder new.
	|
	builder runtimeSources: (extractSourcesFromConfiguration: Runtime usingNamespace: ns).
	builder applicationSources: (extractSourcesFromConfiguration: appDefn usingNamespace: ns).
	resourcePaths addAll: (extractResourcesFromConfiguration: Runtime usingNamespace: ns into: builder).
	resourcePaths addAll: (extractResourcesFromConfiguration: appDefn usingNamespace: ns into: builder).
	builder outputTo: output.
	^resourcePaths
)
public compileSource: appDefnSource usingNamespace: ns to: output = (
	| builder = compilation ProgramBuilder new. |
	builder runtimeSources: (extractSourcesFromConfiguration: Runtime usingNamespace: ns).
	builder applicationSources: {appDefnSource}.
	extractResourcesFromConfiguration: Runtime usingNamespace: ns into: builder.
	builder outputTo: output.
)'as yet unclassified'
private extractResourcesFromConfiguration: config usingNamespace: ns into: builder = (
	|
	resourcePaths = OrderedCollection new. 
	importsRecorder = ImportsRecorder usingNamespace: ns.
	|

	config packageUsing: importsRecorder.
	importsRecorder imports do: [:import <Symbol> |
		| resource |
		resource:: ns perform: import.
		resource isString ifTrue:
			[builder addStringResource: resource under: import].
		resource isForm ifTrue:
			[ | stream |
			[stream:: outDirectory forceNewFileNamed: import, '.png'.
			PNGReadWriter putForm: resource onStream: stream] ensure: [stream close].
			builder addImageResource: import, '.png' under: import.
			resourcePaths add: import, '.png']].

	^resourcePaths
)
private extractSourcesFromConfiguration: appDefn usingNamespace: ns = (
	(* Ordered is important: the app definition must come first *)
	|
	modules = OrderedCollection new. 
	importsRecorder = ImportsRecorder usingNamespace: ns.
	|
	modules add: (sourceOf: appDefn).

	appDefn packageUsing: importsRecorder.
	importsRecorder imports do: [:import <Symbol> |
		| resource |
		resource:: ns perform: import.
		resource isBehavior ifTrue: [modules include: (sourceOf: (resource))]].

	^modules asArray
)'private'
private sourceOf: klass = (
	| stream |
	stream:: (String new: 1000) writeStream.
	klass mixin printTopClassOn: stream.
	^stream contents.
)) : ()
class ChromeAppPackager usingPlatform: p = ApplicationPackager usingPlatform: p (|
	FileDirectory = p blackMarket FileDirectory.
	CrLfFileStream = p blackMarket CrLfFileStream.
	Smalltalk = p blackMarket Smalltalk.
|)
('as yet unclassified'
packageApplicationConfiguration: config <Class> = (
	| name flatBlackMarket scriptStream pageStream backgroundStream manifestStream |

	name:: config name.
	flatBlackMarket:: FlatBlackMarket usingSmalltalk: Smalltalk.
	
	outDirectory:: FileDirectory default / 'out' / name.
	outDirectory assureExistence.
		
	(* Should not automatically create a window, but until we have UI bindings... *)
		
	scriptStream:: outDirectory forceNewFileNamed: 'window.js'.
	[scriptStream lineEndConvention: #lf.
	scriptStream nextPutAll: (String streamContents: [:bufferStream |
	self compileAppDefn: config usingNamespace: flatBlackMarket to: bufferStream])]
		ensure: [scriptStream close].
	
	pageStream:: outDirectory forceNewFileNamed: 'window.html'.
	[pageStream nextPutAll: '<!DOCTYPE html><html><head><title>'.
	pageStream nextPutAll: name.
	pageStream nextPutAll: '</title></head><body><script type="text/javascript" src="'.
	pageStream nextPutAll: 'window'.
	pageStream nextPutAll: '.js"> </script></body></html>']
		ensure: [pageStream close].
		
	backgroundStream:: outDirectory forceNewFileNamed: 'background.js'.
	[backgroundStream nextPutAll: 
'chrome.app.runtime.onLaunched.addListener(function() {
  chrome.app.window.create("window.html", {});
});'.
	]
		ensure: [backgroundStream close].
		
	manifestStream:: outDirectory forceNewFileNamed: 'manifest.json'.
	[manifestStream nextPutAll: 
'{ "name": "', name, '", "version": "0.1", "app": {
    "background": { "scripts": ["background.js"] } } }'.
	]
		ensure: [manifestStream close].
		
	^outDirectory fullName
)) : ()
class WebPagePackager usingPlatform: p = ApplicationPackager usingPlatform: p (|
	FileDirectory = p blackMarket FileDirectory.
	CrLfFileStream = p blackMarket CrLfFileStream.
	Smalltalk = p blackMarket Smalltalk.
|)
('as yet unclassified'
packageApplicationConfiguration: config <Class> = (
	| name flatBlackMarket resourcePaths scriptStream pageStream cacheManifestStream |

	name:: config name.
	flatBlackMarket:: FlatBlackMarket usingSmalltalk: Smalltalk.

	outDirectory:: FileDirectory default / 'out' / name.
	outDirectory assureExistence.
		
	scriptStream:: outDirectory forceNewFileNamed: name, '.js'.
	[scriptStream lineEndConvention: #lf.
	scriptStream nextPutAll: (String streamContents: [:bufferStream |
	resourcePaths:: compileAppDefn: config usingNamespace: flatBlackMarket to: bufferStream])]
		ensure: [scriptStream close].
		
	pageStream:: outDirectory forceNewFileNamed: name, '.html'.
	[pageStream nextPutAll: '<!DOCTYPE html><html manifest="',name,'.appcache"><head>'.
	pageStream nextPutAll: '<meta name="viewport" content="width=device-width, initial-scale=1, maximum-scale=1">'.
	pageStream nextPutAll: '<title>'.
	pageStream nextPutAll: name.
	pageStream nextPutAll: '</title></head><body><script type="text/javascript" src="'.
	pageStream nextPutAll: name.
	pageStream nextPutAll: '.js"> </script></body></html>']
		ensure: [pageStream close].
		
	resourcePaths add: name, '.html'.
	resourcePaths addFirst: name, '.js'. (* Maybe the browser will fetch this first? *)
	
	cacheManifestStream:: outDirectory forceNewFileNamed: name, '.appcache'.
	[cacheManifestStream nextPutAll: 'CACHE MANIFEST'; lf.
	resourcePaths do: [:resourcePath | cacheManifestStream nextPutAll: resourcePath; lf].
	cacheManifestStream nextPutAll: 'NETWORK:'; lf; nextPutAll: '*'; lf]
		ensure: [cacheManifestStream close].
	
	^(outDirectory fileNamed: (name, '.html')) fullName
)) : ()) : ()