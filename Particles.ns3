Newspeak3
'Samples'
class Particles packageUsing: ns = (
|
|
)
(
class World usingPlatform: p = (
|
Timer = p time Timer.
Point = p graphics Point.
Color = p graphics Color.
List = p collections OrderedCollection.
Math = p aliens global at: 'Math'.
atoms
gc = p graphics insertCanvas.
|
atoms:: List new.
80 timesRepeat: [atoms add: Atom new].

Timer every: 30 run: [tick].
)
(
class Atom = (
|
x ::= Math random * 800.
y ::= Math random * 600.
vx ::= (Math random - 0.5) * 6.
vy ::= (Math random - 0.5) * 6.
m = Math random * 150 + 5.
r = m sqrt + 1.
color = Color r: Math random g: Math random b: Math random.
|
)
('as yet unclassified'
collideWith: b = (
	| a = self. dx dy R distance overlap ox oy e dvx dvy top bottom j |
	
	dx:: a x - b x.
	dy:: a y - b y.
	R:: a r + b r.
	(dx*dx) + (dy*dy) > (R*R) ifTrue: [^self].
	
	distance:: ((dx*dx) + (dy*dy)) sqrt.
	overlap:: R - distance.
	ox:: overlap * dx / distance.
	oy:: overlap * dy / distance.
	
	a x: (a x + (ox / 2)).
	a y: (a y + (oy / 2)).
	b x: (b x - (ox / 2)).
	b y: (b y - (oy / 2)).
	
	dvx:: a vx - b vx.
	dvy:: a vy - b vy.
	e:: 1.
	
	top:: (-1*(1+e)) * ((dvx*dx) + (dvy*dy)).
	bottom:: ((1 / a m) + (1 / b m)) * ((dx*dx) + (dy*dy)).
	j:: top/bottom.
	
	a vx: (a vx + ((j / a m)*dx)).
	a vy: (a vy + ((j / a m)*dy)).
	b vx: (b vx - ((j / b m)*dx)).
	b vy: (b vy - ((j / b m)*dy)).
)

drawOn: gc = (
	gc fillStyle: color.
	gc beginPath.
	gc arcAt: (Point x: x y: y) radius: r from: 0 to: 3.14159*2.
	gc fill.
)

tick = (
	x:: x + vx.
	y:: y + vy.
	
	x < r ifTrue: [vx:: vx abs].
	x > (800 - r) ifTrue: [vx:: vx abs * -1].
	y < r ifTrue: [vy:: vy abs].
	y > (600 - r) ifTrue: [vy:: vy abs * -1].
	
	vx abs > 16 ifTrue: [vx: vx * 0.9].
	vy abs > 16 ifTrue: [vy: vy * 0.9].
)

) : (
)'as yet unclassified'
repaint = (
	gc fillStyle: (Color r: 0 g: 0 b: 0 a: 0.12).
	gc beginPath.
	gc rectangle: ((Point x: 0 y: 0) corner: (Point x: 800 y: 600)).
	gc fill.
	atoms do: [:atom | atom drawOn: gc].
)

tick = (
	1 to: atoms size - 1 do: [:i | 
		i + 1 to: atoms size do: [:j | 
			(atoms at: i) collideWith: (atoms at: j)
		].
	].

	atoms do: [:atom | atom tick].
	repaint.
)

) : (
)'as yet unclassified'
main: p args: a = (
	World usingPlatform: p
)

) : (
)
