Newspeak3
'NS2Squeak'
class Newspeak2SqueakCompilation usingPlatform: platform
 newspeakParser: ns3Parser
 mirrorLib: mirrors = NewspeakCompilation
 usingPlatform: platform
 asts: ns3Parser ASTModule ((* This is the Newspeak compilation module. It includes the Newspeak compiler (Compiler) as well as a number of other classes used in compilation. For a  detailed overview of compilation see Compiler.

Classes that do not require access to the state of a compiler are defined at the module level, as siblings of the compiler.  This is true even if the class defines a phase of compilation, like the ScopeBuilder.

Relation to parsing:

Note that this module does not include a parser or AST classes.  These have utility independent of compilation, and so are available as a separate parsing module. This module requires such a parsing module as parameter. It uses this parameter to create a parser for compilation, and to subclass certain AST classes and tools for its own purposes.

The parser is instantiated upon module creation and stored in a module slot. We expect a Newspeak parser to be purely functional; hence we may share it among all compiler instances. It's not clear if this is a valid assumption in the long term.

Separating the parsing module makes it easy to change parsing strategies (though one could also define parsing here and override it in a subclass).

Other parameters:

The module uses some general purpose facilities which it expects to obtain from the underlying platform via the platform parameter.
In addition, a mirror library should be provided, as the compiler operates by querying and creating various mirrors. See the documentation of class Compiler for more discussion of the use of mirrors.

Copyright 2008 Cadence Design Systems, Inc.
Copyright 2010 Gilad Bracha, Felix Geller and Ryan Macnak
Copyright 2011 Matthias Kleine, Ryan Macnak and Cadence Design Systems
Copyright 2012-2013 Gilad Bracha, Ryan Macnak and Cadence Design Systems
   
   Licensed under the Apache License, Version 2.0 (the ''License''); you may not use this file except in compliance with the License.  You may obtain a copy of the License at  http://www.apache.org/licenses/LICENSE-2.0 *)|
      
	Collection = platform collections Collection.
	Dictionary = platform collections Dictionary.
	IdentityDictionary = platform collections IdentityDictionary.
	OrderedCollection = platform collections OrderedCollection.
	Set = platform collections Set.
	SortedList = platform collections SortedCollection.
	
	Parser = ns3Parser Parser.
	astModule = ns3Parser ASTModule.
	
	ASTTool = astModule ASTTool.
      ASTCopier = astModule ASTCopier.
	ASTTraverser = astModule ASTTraverser.
      SuperFalseVisitor = astModule FalseVisitor.

      AST = astModule AST.
	AssignmentAST = astModule AssignmentAST.
	BlockAST = astModule BlockAST.
	CascadedSendAST = astModule CascadedSendAST.
	CodeBodyAST = astModule CodeBodyAST.
	MessageAST = astModule MessageAST.
      MessagePatternAST = astModule MessagePatternAST.
	MethodAST = astModule MethodAST.
	MutableSlotDefAST = astModule MutableSlotDefAST.
	NormalSendAST = astModule NormalSendAST.
	NumberAST = astModule NumberAST.
	ReturnStatAST = astModule ReturnStatAST.
      SendAST = astModule SendAST.
	SetterSendAST = astModule SetterSendAST.
	StringAST = astModule StringAST.
	SymbolAST = astModule SymbolAST.
	TupleAST = astModule TupleAST.
      VarDeclAST = astModule VarDeclAST.
	VariableAST = astModule VariableAST.
	
	Language = platform blackMarket NsMultilanguage Language.
	LanguageCompiler = platform blackMarket NsMultilanguage LanguageCompiler.
	Pragma = platform blackMarket Pragma.

	CompiledMixinMirror = mirrors CompiledMixinMirror.
	LowLevelMixinMirror = mirrors LowLevelMixinMirror. 
	LowLevelMethodMirror = mirrors LowLevelMethodMirror.
	InstanceVariableMirror = mirrors InstanceVariableMirror.

	ClassDeclarationMirror = mirrors ClassDeclarationMirror.

	(* Module variables *)
	parser = Parser new.
	internalParser = ns3Parser InternalParser new.
	|)
(
class Compiler = super Compiler ((* The compiler has three main entry points: compileClassSource:Within:, compileClassHeader: within: and compileMethodSource:within:. These compile an entire class declaration, a class header, and a method, respectively. 

The compiler returns low level (e.g., VM level) objects representing the results of compilation.
These results are described in some detail in the individual methods. The results are never installed by the compiler. The compiler does not know or care whether it is running in the service of a live system. If the compiler is invoked from such a system, the caller may choose to install the results. This should be done as an atomic modification, since the results may be interdependent (e.g., a nested class and its enclosing class). 

Compilation begins with parsing, followed by a pass on the resulting AST to compute the scope. The scope is stored as a shadow tree of the AST. Specifically, the slot scopeMap stores a dictionary mapping AST nodes to scopes. This mapping is computed by ScopeBuilder.

Optimization note: 
This can be optimized, so that we only compute the scope of a class side, and then use it as a basis for computing the scope of each method. This would prevent us from holding onto the scopes for all methods when compiling a class. Likewise, we can avoid holding onto the scopes of all nested classes. 
** end note

When compiling an individual method, we also rewrite its AST into a lower level AST, and submit that to the AST2ByteCodeCompiler, which produces byte code.  If we are compiling a class declaration or a class header, we compute the overall structure of the resulting mixin, including any required synthetic members.

The NS2 implementation involves a considerable amount of synthetic code, dealing with accessors, nested classes, slot initialization, superclass initialization and the primary constructor. 

Nested classes are distinct for every instance of an outer class. Hence, every outer class has synthetic fields that hold the class objects for its nested classes. These fields are always accessed by a getter method which lazily initializes the field. The name of the getter method is the simple name of the inner class. The field, in contrast, is named X_slot, where X is the fully qualified name of the inner class' mixin. This is necessary to prevent it from clashing with nested classes with the same simple name elsewhere in the hierarchy. See accessorStringForNestedClassNamed:fullName:superName: .

The creation of classes is based on producing a low level mixin mirror for the class.

Nested classes also have an enclosingObject slot pointing at the object that created them, and a getter for it. See ensureEnclosingObjectAccessFor:.

The primary constructor induces a synthetic class method of the same name, which calls a synthetic  instance method of the same name on a freshly created instance. Within that instance method is the code for all the slot initializers, as well as the superclass constructor call. See processConstructorFor:inClass:. *)|

input <ReadStream>
scopeStack <OrderedCollection[Scope]> = OrderedCollection new.
literalTable <IdentityDictionary[Symbol, Integer]>
rewriter = Rewriter new.
squeakRewriter = SqueakRewriter new.
methodCompiler = AST2ByteCodeCompiler new.
currentDepth <Integer> (* The lexical nesting level of the current class; 
							top level classes are at level 0 *)
useAlternateBytecodeSet <Boolean> ::= true.
|)
(
class AST2ByteCodeCompiler = ASTTool ((* The compiler is a visitor on an AST that has been processed by the rewriter.
It is a subtype of  ASTTool[Self] - that is, its methods do not return individual results, but instead
side effect state within the compiler.

The compiler decides what instructions to generate at a somewhat abstract level, and asks
a code generator object to actually produce the instruction stream.

The code generator deals with issues like whether to use regular or wide instructions,
what the actual instruction codes are, what the actual offsets for branches are etc.,
so the compiler can abstract from these details. One can almost think of this as producing
assembly.

The trickiest part of the byte code compiler is deciding when to pop the result of an expression. Subexpressions leave their result on the operand stack, but top level expressions need not, since the result is not used. Furthermore, the VM does not clean up the operand stack upon return from a method, it seeems, so we are obligated to leave an empty operand stack on method exit.

There would appear to be a space-time tradeoff - we can spend cycles popping results, or we can allocate a larger operand stack. But since we always need to clean it up, the time is always lost so we should optimize for space.

In straight line code, the situation is simple: keep track of the depth of the stack, and pop it to zero at the end of a statement. However, once we have conditional branches, we need to ensure that the last statement in a branch leaves its result, because it is in fact a subexpression.

However, this is not true in case of loops!

We do not need to retain the result of the last statement of a non-inlined closure. This is its value, but the rewriter  already takes care of this by inserting a local return.

The compiler tracks whether we are at the start of a statement or not using the boolean slot statementStarting. When traversing nodes that may have subexpressions, the current value of statementStarting is always saved before traversing subexpressions, and restored afterwards. The convention is to store the current value of statementStarting into a local named shouldPop (since we should pop the operand stack if we are at the start of a statement, i.e., the expression is a top level one and its value will not be used).

The value of shouldPop is used when there is a choice between generating a popping or non-popping version of a bytecode. It is not, however, used to pop the results of individual subexpressions. Rather than clutter the logic with tests of shouldPop (or statementStarting) everywhere, we ensure that the method statementEnd is called at the end of each statement. It pops the stack as needed. Since only top level expressions will be popped, this does not change the maximal operand stack size; it only centralizes the handling.

The statementEnd method is called by CodeBodyAST, which traverses the statement lists. Also, when traversing a CodeBodyAST, statementStarting is set to true before each statement is handled - except for the last statment, whose result we may need as noted above.

statmentEnd needs to know how many pops to perform. Whenever we start a new CodeBodyAST, we must remember the depth of the operand stack at that time (stored in statementBaselineStackDepth). At the end of each statement, we want to pop the stack down to that depth. This complication could be avoided if we chose to have every expression consider whether it was top level or not.

Keeping track of operand stack depth is not as simple as it seems.
Closure code appears inline (regardless of whether execution is inlined!). Code in a non-inlined closure starts executing with an empty operand stack, but the depth of the method must be sufficient for the block.

To complicate matters further, the actual size of the activation is simply the sum of the number of parameters, number of locals and maximal operand stack size.

We must ensure sufficient space for any given closure defined in the method. The space required for a closure is the sum of its arity, the number of values copied into it, and its maximal operand stack size. Because the closure must explicitly initialize its locals to nil on startup, its operand stack will include sufficient space for its temporaries.

Ideally, we'd track the operand stack depth for the method and for each closure within it separately. Though it seems that the actual required depth would be the maximum of all these, that is simplistic.

We really need to compute the required context size which is the maximum of:

#method formals + #method locals + method operand stack size

#closure formals + #closure copy downs + closure operand stack size

assuming that the locals of the closure are accounted for in the stack size.

Then, the actual operand stack size we specify for the method is

context size - #method formals - #method locals

We don't do this yet.  What we do instead is often suboptimal, but sometimes insufficient. We compute the cumulative maximum operand stack size for all code in the method, including closures. The idea was that this would be conservative and simple, but in fact it may fail if a closure has a large number of arguments and copy-downs, and the actual operand stack usage is small. *)| 
  cgen <CodeGenerator>
  debugInfo <DebugInfo>

  shouldPop <Boolean>
  scopeStack <OrderedCollection[Scope]>

  (* Possibly these three should be unified as a ContextTracker or something.*)
  maxClosureContextSize <Integer>
  offsetStack <OrderedCollection[Integer]> (* see codeBodyNode:  rename localOffsetAllocator? *)
  contextDepth <Integer> (* The number of outerContexts that need to be traversed to reach the method context.  E.g., 0=method, 1=closure, 2=closure in closure, and so on. Used for debug info. *)

  result <LowLevelMethodMirror> (* refactor me to be expected from methodNode: ! *)
|)
(
class CodeGenerator = (|
code = LowLevelMethodMirror new.

numTemps ::= 1. (* temp0 always allocated *) (*why?*)
maxStackDepth ::= 0.
currentStackDepth ::= 0.
|)
('as yet unclassified'
accessModifier ^<Symbol> = (
	^ code accessModifier
)
accessModifier: am <Symbol> = (
	code accessModifier: am.
)
argCount: n <Integer> = (
	code argCount: n
)
bci ^<Integer> = (
	(* Answers the current bytecode index. Used for calculating jump lengths. Note this is NOT the pc. *)
	^code size
)
close ^<LowLevelMethodMirror> = (
	(* To be called at the end of code generation. *)
	| methodContextSize = maxStackDepth + code argCount + code maxLocals. |
	code maxStack: (methodContextSize max: maxClosureContextSize).
	^code
)
literals: ls = (
    code literals: ls.
)
maxLocals ^<Integer> = (
	^code maxLocals
)
maxLocals: n <Integer> = (
	code maxLocals: n
)
methodName: n <Symbol> = (
	code selector: n
)
pushMixinClass = (
(* The mixin class will be stored in the final literal -added after the end of the literal array *)
	pushLiteralWithIndex: literalTable size + 1
)'stack'
decrementStackDepthBy: n = (
	(* Reduce the current height of the operand stack *)
	currentStackDepth:: currentStackDepth - n.
	assert: [currentStackDepth >= 0] 
		message: 'Stack depth must not be negative'.
)
ensureStackDepth: targetDepth <Integer> = (

	assert: [targetDepth <= currentStackDepth]
		message: 'Stack shorter than expected'.
	
	[currentStackDepth > targetDepth] whileTrue: [pop]
)
incrementStackDepthBy: n = (
	(* Increase the current height of the operand stack. Keep track of the maximum operand stack height seen in this method (that's the whole point of tracking the stack height - we need this to create a CompiledMethod. *)
	currentStackDepth:: currentStackDepth + n.
	maxStackDepth:: maxStackDepth max: currentStackDepth.
)) : ()
class CodeGeneratorV3 = CodeGenerator (
(* We do not use the quick send bytecode for #== or #class because they hardwire the definitions appropriate for Object, but we expect Values and proxies to override #== and #class respectively. *)|
dontUse = Object new.
specialSelectors = 
	{#+. #-. #<. #>. #<=. #>=. #=. #~=. #*. #/. #\\. #@. 
	#bitShift:. #//. #bitAnd:. #bitOr:. 
	#at:. #at:put:. #size. #next. #nextPut:. #atEnd.
	dontUse. (* #==. *)
	dontUse. (* #class. *) 
	#blockCopy:. #value. #value:. #do:. #new. #new:. #x. #y}.
|)
('as yet unclassified'
indexForLiteral: literal = (
	^literalTable at: literal
)
patchJumpIfAt: index <Integer> with: targetData <Integer> = (
	code byteAt: index - 1 
	      put: ((code byteAt: index - 1) bitOr: ((targetData bitAnd: 16r300)  >> 8)).
	code byteAt: index
		put: ((code byteAt: index) bitOr: (targetData bitAnd: 16rFF))
)
pushRemoteTemporary: index inVector: vectorIndex = (
	assert: [index between: 0 and: 255]
		message: 'Temp index out of range'.
	assert: [vectorIndex between: 0 and: 255]
		message: 'Temp vector index out of range'.
	
	code byte: 140; byte: index; byte: vectorIndex.
	incrementStackDepthBy: 1.
)
sizeOfBackJump = (
	^2
)
storeIntoRemoteTemporary: index inVector: vectorIndex = (
	assert: [index between: 0 and: 255]
		message: 'Temp index out of range'.
	assert: [vectorIndex between: 0 and: 255]
		message: 'Temp vector index out of range'.
	
	code byte: 141; byte: index; byte: vectorIndex.
)'jumps'
jumpBy: distance <Integer>  = (

	(* always generates long jumps *)

	assert: [distance between: -1024 and: 1023]
		message: 'Unconditional jump distance out of range'.
      code
		byte: 160 + (distance + 1024 bitShift: -8);
		byte: distance + 1024 \\ 256.
)
jumpIf: bool <Boolean> by: distance <Integer> = (

	(* always generates long jumps *)

	| popAndJump <Integer> |
	assert: [distance between: 0 and: 1023]
		message: 'Conditional jump distance out of bounds'.
	decrementStackDepthBy: 1.
	popAndJump:: bool ifTrue: [168] ifFalse: [172].
	code 
		byte: popAndJump + (distance bitShift: -8);
		byte: (distance bitAnd: 16rFF).
)
patchClosureJumpAt: index <Integer> with: targetData <Integer> = (

	code byteAt: index - 1 
		put: ((code byteAt: index - 1) bitOr: ((targetData bitAnd: 16r700)  >> 8)).
	code byteAt: index 
		put: ((code byteAt: index) bitOr: (targetData bitAnd: 16rFF)).
)
patchJumpAt: index <Integer> with: targetData <Integer> = (

	code byteAt: index - 1 
		put: ((code byteAt: index - 1) bitOr: ((targetData bitAnd: 16r700)  >> 8)).
	code byteAt: index 
		put: ((code byteAt: index) bitOr: (targetData bitAnd: 16rFF)).
)'private'
pushInteger: value = (
	code byte: 112 + 3 + 2 + value.
	incrementStackDepthBy: 1.
)
pushLiteralWithIndex: index = (

	assert: [index between: 0 and: 255]
		message: 'Literal index out of range'.
		
	incrementStackDepthBy: 1.
	
	index < 32 ifTrue: [code byte: 32 + index. ^self].
	index < 64 ifTrue: [code byte: 128; byte: 128 + index. ^self].
	code byte: 132; byte: 96; byte: index.
)'returns'
returnBooleanFromMethod: bool = (
	code byte: (bool ifTrue: [121] ifFalse: [122]).
)
returnFromBlock = (
	code byte: 125.
	decrementStackDepthBy: 1.
)
returnFromMethod = (
	code byte: 124.
	decrementStackDepthBy: 1.
)
returnSelfFromMethod = (
	code byte: 120.
)'sends'
normalSend: selector numArgs: numArgs = (
	|
	index ::= specialSelectors indexOf: selector.
	|
	decrementStackDepthBy: numArgs + 1 (* the receiver *).
	incrementStackDepthBy: 1.
		
	(* Special selector? *)
	index = 0 ifFalse: 
		[code byte: 175 + index.
		^self].
	index:: indexForLiteral: selector.
	
	assert: [numArgs between: 0 and: 31]
		message: 'numArgs out of range'.
	assert: [index between: 0 and: 255]
		message: 'selector literal index out of range'.
	
	(index < 16 and: [numArgs < 3]) ifTrue:
		[code byte: 208 + (numArgs * 16) + index.
		^self].
	
	(index < 32 and: [numArgs < 8]) ifTrue: 
		[code byte: 131; byte: ((numArgs bitShift: 5) + index).
		^self].
	
	(index < 64 and: [numArgs < 4]) ifTrue: 
	 	[code
			byte: 134;
			byte: ((numArgs bitShift: 6) + index).
		 ^self].
	
	code byte: 132; byte: numArgs; byte: index.
)
superSend: selector numArgs: numArgs = (
	|
	selectorIndex = indexForLiteral: selector.
	|
	assert: [selectorIndex between: 0 and: 255]
		message: 'Selector index out of range'.
	assert: [numArgs between: 0 and: 255]
		message: 'Message arity out of range'.
		
	decrementStackDepthBy: numArgs + 1 (* the receiver *).
	incrementStackDepthBy: 1.
	
	code byte: 126; byte: numArgs; byte: selectorIndex.
)'stack'
dup = (
	code byte: 136.
	incrementStackDepthBy: 1.	
)
pop = (
	code byte: 135.
	decrementStackDepthBy: 1.	
)'temporaries'
popIntoRemoteTemporary: index inVector: vectorIndex = (
	assert: [index between: 0 and: 255]
		message: 'Temp index out of range'.
	assert: [vectorIndex between: 0 and: 255]
		message: 'Temp vector index out of range'.
	
	code byte: 142; byte: index; byte: vectorIndex.
	decrementStackDepthBy: 1.
)
popIntoTemporary: index = (
	assert: [index between: 0 and: 63] message: 'Temp index out of range'.
		
	index < 8 ifTrue:
		[code byte: 104 + index.
		decrementStackDepthBy: 1.
		^self].
	
	code byte: 130; byte: 64 + index.
	decrementStackDepthBy: 1.
)
pushTemporary: index = (
	assert: [index between: 0 and: 63] message: 'Temp index out of range'.
	
	index < 16 ifTrue:
		[incrementStackDepthBy: 1.
		code byte: 16 + index.
		^self].
	
	incrementStackDepthBy: 1.
	code byte: 128; byte: 64 + index.
)
storeIntoTemporary: index = (
	assert: [index between: 0 and: 63] message: 'Temp index out of range'.
	
	code byte: 129; byte: 64 + index.
)'testing'
hasSpecialReturnBoolean = (
	^true yourself
)
isImplicitReceiverSendAbsentReceiver = (
	^false
)
isSuperSendAbsentReceiver = (
	^false
)'values'
createClosureOfArity: numArgs copying: numCopied length: jumpSize = (

	| numExtensions numCopiedMod8 numArgsMod8 extA |
	
	decrementStackDepthBy: numCopied.
	incrementStackDepthBy: 1.
	
	assert: [jumpSize between: 0 and: 65535]
		message: 'Closure jump out of range'.
	assert: [numCopied between: 0 and: 15]
		message: 'Too many copied values in closure'.
	assert: [numArgs between: 0 and: 15]
		message: 'Too many args in closure'.
	
	code
		byte: 143;
		byte: numArgs + (numCopied bitShift: 4);
		byte: (jumpSize bitShift: -8);
		byte: (jumpSize bitAnd: 16rFF)
)
createEmptyArray: size = (
	assert: [size between: 0 and: 127] message: 'Array size must be < 128'.
	incrementStackDepthBy: 1.
	code byte: 138; byte: size.
)
createFullArray: size = (
	assert: [size between: 0 and: 127] message: 'Array size must be < 128'.
	decrementStackDepthBy: size.
	incrementStackDepthBy: 1.
	code byte: 138; byte: 128 + size.
)
pushEnclosingObject: depth = (
	assert: [depth between: 0 and: 127]
		message: 'Enclosing object depth out of range'.
	
	depth = 0 ifTrue: [^pushSelf].
	
	incrementStackDepthBy: 1.
	
	code byte: 139; byte: (indexForLiteral: depth). (*What?!*)
)
pushFalse = (
	code byte: 114.
	incrementStackDepthBy: 1.
)
pushImplicitReceiver: selector  = (

	| index = indexForLiteral: selector. |
	assert: [index between: 0 and: 255]
		message: 'Selector index out of bounds'.
	
	incrementStackDepthBy: 1.
	code byte: 127; byte: index.
)
pushLiteral: value = (
	value == true ifTrue: [^pushTrue].
	value == false ifTrue: [^pushFalse].
	value == nil ifTrue: [^pushNil].
	(value isInteger and: [value between: -1 and: 2])
		ifTrue: [^pushInteger: value].
	^pushLiteralWithIndex: (indexForLiteral: value).
)
pushNil = (
	code byte: 115.
	incrementStackDepthBy: 1.
)
pushSelf = (
	code byte: 112.
	incrementStackDepthBy: 1.
)
pushThisContext = (
	code byte: 137.
	incrementStackDepthBy: 1.
)
pushTrue = (
	code byte: 113.
	incrementStackDepthBy: 1.
)) : ('as yet unclassified'
slotsPerMethod = (
	^64
))
class CodeGeneratorV4 = CodeGenerator (
(* We do not use the quick send bytecode for #== or #class because they hardwire the definitions appropriate for Object, but we expect Values and proxies to override #== and #class respectively. *)|
dontUse = Object new.
specialSelectors = 
	{#+. #-. #<. #>. #<=. #>=. #=. #~=. #*. #/. #\\. #@. 
	#bitShift:. #//. #bitAnd:. #bitOr:. 
	#at:. #at:put:. #size. #next. #nextPut:. #atEnd.
	dontUse. (* #==. *)
	dontUse. (* #class. *)
	#blockCopy:. #value. #value:. #do:. #new. #new:. #x. #y}.
|)
('as yet unclassified'
close ^<LowLevelMethodMirror> = (
	code usesAlternateBytecodeSet: true.
	^super close
)
indexForLiteral: literal = (
	^literalTable at: literal
)
patchClosureJumpAt: blockBci <Integer> with: distance <Integer> = (
	
	assert: [distance between: 0 and: 65535]
		message: 'Closure jump out of range'.
		
	assert: [(code byteAt: blockBci-4) = 225] message: 'Not really a closure/extb?'.
	code byteAt: blockBci-3 put: (distance bitShift: -8).
	assert: [(code byteAt: blockBci-2) = 253] message: 'Not really a closure?'.
	(*blockBci-1*)
	code byteAt: blockBci put: (distance bitAnd: 255)
)
signedSingleExtendB: extendedIndex = (

	assert: [extendedIndex between: -128 and: 127]
		message: 'Single extend B out of range'.
	
	code
		byte: 225;
		byte: (extendedIndex >= 0 
			ifTrue: [extendedIndex]
			ifFalse: [extendedIndex + 256]) 
)
sizeOfBackJump = (
	^4
)
unsignedSingleExtendA: extendedIndex = (
	assert: [extendedIndex between: 0 and: 255]
		message: 'Single extend A out of range'.
	code byte: 224; byte: extendedIndex
)
unsignedSingleExtendB: extendedIndex = (
	assert: [extendedIndex between: 0 and: 255]
		message: 'Single extend B out of range'.
	code byte: 225; byte: extendedIndex
)'jumps'
jumpBy: distance = (

	distance < 0 ifTrue: [
		assert: [distance between: -32768 and: -1]
			message: 'Unconditional backjump out of range'.
		
		signedSingleExtendB: (distance bitShift: -8).
		code byte: 242; byte: (distance bitAnd: 255).
		^self.
	].

	assert: [distance between: 0 and: 32767]
		message: 'Unconditional jump out of range'.

	signedSingleExtendB: (distance bitShift: -8).
	code byte: 242; byte: (distance bitAnd: 255).	
)
jumpIf: bool <Boolean> by: distance <Integer> = (
	
	(* Always generates unextended jumps *)
	
	assert: [distance between: 0 and: 32767]
		message: 'Conditional jump out of range'.
		
	decrementStackDepthBy: 1.
	
	unsignedSingleExtendB: (distance bitShift: -8).
	code
		byte: (bool ifTrue: [243] ifFalse: [244]);
		byte: (distance bitAnd: 255).
)
label: pos = (
	halt.
)
patchJumpAt: jumpBci <Integer> with: distance <Integer> = (
	
	| extendedIndex |
	
	assert: [distance between: 0 and: 32767]
		message: 'Unconditional jump out of range'.
	
	assert: [(code byteAt: jumpBci-3) = 225] message: 'Not really a jump/extb?'.
	
	extendedIndex:: distance bitShift: -8.
	code byteAt: jumpBci-2 put: (extendedIndex >= 0 
			ifTrue: [extendedIndex]
			ifFalse: [extendedIndex + 256]). (*patch the extend b*)
	
	assert: [(code byteAt: jumpBci -1) = 242] message: 'Not really a jump?'.
	
	code byteAt: jumpBci put: (distance bitAnd: 255).
)
patchJumpIfAt: jumpBci <Integer> with: distance <Integer> = (
	
	assert: [distance between: 0 and: 32767]
		message: 'Conditional jump out of range'.
	
	assert: [(code byteAt: jumpBci-3) = 225] message: 'Not really a branch/extb?'.
	code byteAt: jumpBci-2 put: (distance bitShift: -8).
	assert: [(code byteAt: jumpBci-1) between: 243 and: 244] message: 'Not really a branch?'.
	code byteAt: jumpBci put: (distance bitAnd: 255)
)'private'
pushInteger: n <Integer> = (
	
	incrementStackDepthBy: 1.
	n = 0 ifTrue: [code byte: 78. ^self].
	n = 1 ifTrue: [code byte: 79. ^self].
	assert: [n between: -32768 and: 32767]
		message: 'Quick integer out of range'.
	(n < 0 or: [n > 255]) ifTrue: [signedSingleExtendB: (n bitShift: -8)].
	code byte: 229.
	code byte: (n bitAnd: 255).
)
pushLiteralWithIndex: index = (
	| extendedIndex |
	
	assert: [index between: 0 and: 65535]
		message: 'Literal index out of range'.
		
	incrementStackDepthBy: 1.
	
	index < 32 ifTrue: [code byte: 32 + index. ^self].
	
	(extendedIndex:: index) > 256 ifTrue:
		[unsignedSingleExtendA: extendedIndex // 256.
		extendedIndex:: extendedIndex \\ 256].
	code byte: 228; byte: extendedIndex.
)'returns'
returnFromBlock = (
	code byte: 218.
	decrementStackDepthBy: 1.
)
returnFromMethod = (
	code byte: 217.
	decrementStackDepthBy: 1.
)
returnSelfFromMethod = (
	code byte: 216
)'sends'
implicitReceiverSend: selector numArgs: numArgs = (
	|
	selectorIndex = indexForLiteral: selector.
	extendedIndex extendedNArgs
	|
	decrementStackDepthBy: numArgs.
	incrementStackDepthBy: 1.
	
	assert: [selectorIndex between: 0 and: 65535]
		message: 'Selector index out of range'.
	assert: [numArgs between: 0 and: 31]
		message: 'Message arity out of range'.
	
	(selectorIndex < 16 and: [numArgs = 0]) ifTrue: 
	 	[code byte: 160 + selectorIndex. ^self].
	
	(extendedIndex:: selectorIndex) > 31 ifTrue:
		[self unsignedSingleExtendA: extendedIndex // 32.
		 extendedIndex:: extendedIndex \\ 32].
	(extendedNArgs:: numArgs) > 7 ifTrue:
		[self unsignedSingleExtendB: extendedNArgs // 8.
		 extendedNArgs:: extendedNArgs \\ 8].
	code
		byte: 240;
		byte: extendedNArgs + (extendedIndex * 8)
)
normalSend: selector numArgs: numArgs = (
	|
	selectorIndex 
	extendedIndex extendedNArgs
	|
	
	decrementStackDepthBy: numArgs + 1 (* the receiver *).
	incrementStackDepthBy: 1.
	
	(specialSelectors includes: selector) ifTrue:
		[code byte: 79 + (specialSelectors indexOf: selector).
		^self].
		
	selectorIndex:: indexForLiteral: selector.
	
	assert: [selectorIndex between: 0 and: 65535]
		message: 'Selector index out of range'.
	assert: [numArgs between: 0 and: 31]
		message: 'Message arity out of range'.
	
	(selectorIndex < 16 and: [numArgs < 3]) ifTrue: 
	 	[code byte: 112 + (numArgs * 16) + selectorIndex.
		 ^self].

	(extendedIndex:: selectorIndex) > 31 ifTrue:
		[self unsignedSingleExtendA: extendedIndex // 32.
		 extendedIndex:: extendedIndex \\ 32].
	(extendedNArgs:: numArgs) > 7 ifTrue:
		[self unsignedSingleExtendB: extendedNArgs // 8.
		 extendedNArgs:: extendedNArgs \\ 8].
	code
		byte: 238;
		byte: extendedNArgs + (extendedIndex * 8)
)
outerSend: selector depth: depth numArgs: numArgs = (
	Error signal: 'Absent receiver outer sends unimplemented'.
)
selfSend: selector numArgs: numArgs = (
	Error signal: 'Absent receiver self sends unimplemented'.
)
superSend: selector numArgs: numArgs = (
	|
	selectorIndex = indexForLiteral: selector.
	extendedIndex extendedNArgs
	|
	assert: [selectorIndex between: 0 and: 65535]
		message: 'Selector index out of range'.
	assert: [numArgs between: 0 and: 31]
		message: 'Message arity out of range'.
		
	decrementStackDepthBy: numArgs.
	incrementStackDepthBy: 1.
	
	(extendedIndex:: selectorIndex) > 31 ifTrue:
		[unsignedSingleExtendA: extendedIndex // 32.
		 extendedIndex:: extendedIndex \\ 32].
	(extendedNArgs:: numArgs) > 7 ifTrue:
		[self unsignedSingleExtendB: extendedNArgs // 8.
		 extendedNArgs:: extendedNArgs \\ 8].
	code
		byte: 241;
		byte: extendedNArgs + (extendedIndex * 8)
)'stack'
dup = (
	code byte: 219.
	incrementStackDepthBy: 1.	
)
nop = (
	code byte: 221
)
pop = (
	code byte: 220.
	decrementStackDepthBy: 1.	
)'temporaries'
popIntoRemoteTemporary: index inVector: vectorIndex = (

	assert: [index between: 0 and: 255]
		message: 'Temp index out of range'.
	assert: [vectorIndex between: 0 and: 255]
		message: 'Temp vector index out of range'.
	
	code byte: 252; byte: index; byte: vectorIndex.
	decrementStackDepthBy: 1.
)
popIntoTemporary: index = (
	assert: [index between: 0 and: 63] message: 'Temp index out of range'.
		
	(* 184-191	10111 i i i	Pop and Store Temporary Variable #iii *)
	index < 8 ifTrue:
		[code byte: 184 + index.
		decrementStackDepthBy: 1.
		^self].
	
	(* 237  11101101	i i i i i i i i  Pop and Store Temporary Variable #iiiiiiii *)
	code byte: 237; byte: index.
	decrementStackDepthBy: 1.
)
pushRemoteTemporary: index inVector: vectorIndex = (
	assert: [index between: 0 and: 255]
		message: 'Temp index out of range'.
	assert: [vectorIndex between: 0 and: 255]
		message: 'Temp vector index out of range'.
	
	code byte: 250; byte: index; byte: vectorIndex.
	incrementStackDepthBy: 1.
)
pushTemporary: index = (
	assert: [index between: 0 and: 63] message: 'Temp index out of range'.
	
	(* 64-71		01000 i i i		Push Temporary Variable #iii *)
	(* 72-75		010010 i i		Push Temporary Variable #ii + 8 *)
	index < 12 ifTrue:
		[incrementStackDepthBy: 1.
		code byte: 64 + index.
		^self].
	
	(* 230		11100110	i i i i i i i i	Push Temporary Variable #iiiiiiii *)
	incrementStackDepthBy: 1.
	code byte: 230; byte: index.
)
storeIntoRemoteTemporary: index inVector: vectorIndex = (
	assert: [index between: 0 and: 255]
		message: 'Temp index out of range'.
	assert: [vectorIndex between: 0 and: 255]
		message: 'Temp vector index out of range'.
	
	code byte: 251; byte: index; byte: vectorIndex.
)
storeIntoTemporary: index = (
	assert: [index between: 0 and: 63] message: 'Temp index out of range'.
	
	code byte: 234; byte: index.
)'testing'
hasSpecialReturnBoolean = (
	^false
)
isImplicitReceiverSendAbsentReceiver = (
	^true
)
isOuterSendAbsentReceiver = (
	^false
)
isSelfSendAbsentReceiver = (
	^false
)
isSuperSendAbsentReceiver = (
	^true
)'values'
createClosureOfArity: numArgs copying: numCopied length: jumpSize = (

	| numExtensions numCopiedMod8 numArgsMod8 extA |
	
	decrementStackDepthBy: numCopied.
	incrementStackDepthBy: 1.
	
	assert: [jumpSize between: 0 and: 65535]
		message: 'Closure jump out of range'.
	assert: [numCopied between: 0 and: 127]
		message: 'Too many copied values in closure'.
	assert: [numArgs between: 0 and: 127]
		message: 'Too many args in closure'.
	
	extA:: numExtensions:: 0.
	(numArgsMod8:: numArgs) > 7 ifTrue:
		[extA:: numArgs // 8.
		 numArgsMod8:: numArgsMod8 \\ 8].
	(numCopiedMod8:: numCopied) > 7 ifTrue:
		[extA:: extA + (numCopied // 8 * 16).
		 numCopiedMod8:: numCopiedMod8 \\ 8].
	extA ~= 0 ifTrue:
		[self unsignedSingleExtendA: extA.
		 numExtensions:: 1].
	(*jumpSize > 255 ifTrue: *)
		numExtensions:: numExtensions + 1.
		 unsignedSingleExtendB: jumpSize // 256.
	code
		byte: 253;
		byte: (numExtensions bitShift: 6) + (numCopiedMod8 bitShift: 3) + numArgsMod8;
		byte: (jumpSize bitAnd: 16rFF)
)
createEmptyArray: size = (
	(* 231 11100111 jkkkkkkk	Push (Array new: kkkkkkk) (j = 0) *)
	assert: [size between: 0 and: 127] message: 'Array size must be < 128'.
	incrementStackDepthBy: 1.
	code byte: 231; byte: size.
)
createFullArray: size = (
	assert: [size between: 0 and: 127] message: 'Array size must be < 128'.
	decrementStackDepthBy: size.
	incrementStackDepthBy: 1.
	code byte: 231; byte: 128 + size.
)
pushEnclosingObject: depth = (
	assert: [depth between: 0 and: 127]
		message: 'Enclosing object depth out of range'.
	
	depth = 0 ifTrue: [^pushSelf].
	
	incrementStackDepthBy: 1.
	
	signedSingleExtendB: depth negated.
	code byte: 77
)
pushFalse = (
	code byte: 77.
	incrementStackDepthBy: 1.
)
pushLiteral: value = (
	value == true ifTrue: [^pushTrue].
	value == false ifTrue: [^pushFalse].
	value == nil ifTrue: [^pushNil].
	(value isInteger and: [value between: -32768 and: 32767])
		ifTrue: [^pushInteger: value].
	^pushLiteralWithIndex: (indexForLiteral: value).
)
pushNil = (
	unsignedSingleExtendB: 2.
	code byte: 77.
	incrementStackDepthBy: 1.	
)
pushSelf = (
	code byte: 76.
	incrementStackDepthBy: 1.
)
pushThisContext = (
	unsignedSingleExtendB: 3.
	code byte: 77.
	incrementStackDepthBy: 1.
)
pushTrue = (
	unsignedSingleExtendB: 1.
	code byte: 77.
	incrementStackDepthBy: 1.	
)) : ('as yet unclassified'
slotsPerMethod = (
	^32768 (* 2<<16 / 4 *)
))
class DebugInfo = ((* Debugger information. *)|
	private sourceMapping <MutableMap[Integer, Interval]> = Dictionary new.
	localVariables <MutableList[LocalVariableDebugInfo]> = OrderedCollection new.
|)
('as yet unclassified'
addInfoForLocal: varInfo <SemanticVarDecl> = (

	| info |
	info: LocalVariableDebugInfo new.
	info name: varInfo ast name.
	info zeroOriginOffset: varInfo offset.
	info	remoteVector: nil.
	info contextDepth: contextDepth.
	^localVariables addLast: info.
)
addInfoForRemote: varInfo <SemanticVarDecl> in: remoteVarInfo <SemanticVarDecl> = (

	| info |
	info: LocalVariableDebugInfo new.
	info name: varInfo ast name.
	info zeroOriginOffset: varInfo offset.
	info	remoteVector: remoteVarInfo ast name.
	info contextDepth: contextDepth.
	^localVariables addLast: info.
)
mapPC: pc <Integer> to: src <interval> = (
	sourceMapping at: pc put: src
)
mapperForContext: ctxt = (
	^DebugMapper forContext: ctxt usingDebugInfo: self.
)
sourceMap ^ <SortedList[Association[Integer, Interval]]> = (
	| assocs <List[Association[Integer, Interval]]> |
	assocs:: sourceMapping associations.
	^(SortedList 
		new: assocs size 
		sortBlock:[:a <Association[Integer, Interval]> :b <Association[Integer, Interval]> | 
			a key < b key
			]) addAll: assocs; yourself
)) : ()
class DebugMapper forContext: ctxt usingDebugInfo: dbgInfo = ((*  *)|
	context <MethodContext> = ctxt.
	debugInfo <DebugInfo> = dbgInfo.
	myDepth <Integer> = self depthOfContext: ctxt.
|)
('as yet unclassified'
astForContextAtDepth: targetDepth <Integer> ^<AST> = (
	|
	returnContext
	|
	myDepth >= targetDepth ifFalse: [deny].
	
	returnContext:: NormalSendAST new
		recv: hereNode;
		msg: (MessageAST new
			sel: doitContextArgumentName;
			args: {}).
		
	(myDepth - targetDepth) timesRepeat: [
		returnContext:: NormalSendAST new
			recv: (NormalSendAST new
				recv: returnContext;
				msg: (MessageAST new 
					sel: #closure;
					args: {}));
			msg: (MessageAST new 
				sel: #outerContext;
				args: {})].
	^returnContext	
)
contextAtDepth: targetDepth <Integer> ^<MethodContext> = (
	|
	returnContext
	|
	myDepth >= targetDepth ifFalse: [deny].
	
	returnContext: context.
	(myDepth - targetDepth) 
		timesRepeat: [returnContext: returnContext closure outerContext].
	^returnContext	
)
depthOfContext: ctxt <MethodContext> ^<Integer>= (
	^ctxt closure isNil 
		ifTrue: [0 (* method *)] 
		ifFalse: [1 + (depthOfContext: ctxt closure outerContext)]
)
getLeastLocalValueOf: varName <Symbol> ^<Object> = (
	| info |
	info:: leastLocalInfoFor: varName.
	(* defensive programming, but should be unnecessary now that
	 DebugMapper>localNames filters out variables in deeper contexts. *)
	myDepth >= info contextDepth ifFalse:
		[^nil].
	^info remoteVector isNil
		ifTrue:  [(contextAtDepth: info contextDepth) tempAt: info zeroOriginOffset + 1]
		ifFalse: [(getValueOf: info remoteVector) ifNotNil:
					[:rv| rv at: info zeroOriginOffset + 1]]
)
getValueOf: varName <Symbol> ^<Object> = (
	| info |
	info:: mostLocalInfoFor: varName.
	(* defensive programming, but should be unnecessary now that
	 DebugMapper>localNames filters out variables in deeper contexts. *)
	myDepth >= info contextDepth ifFalse:
		[^nil].
	^info remoteVector isNil
		ifTrue:  [(contextAtDepth: info contextDepth) tempAt: info zeroOriginOffset + 1]
		ifFalse: [(getLeastLocalValueOf: info remoteVector) ifNotNil:
					[:rv| rv at: info zeroOriginOffset + 1]]
)
getterAstFor: varName <Symbol> ^<SendAST> = (
	| info |
	info:: mostLocalInfoFor: varName.
	
	^info remoteVector isNil
		ifTrue:  [NormalSendAST new 
			recv: (astForContextAtDepth: info contextDepth);
			msg: (MessageAST new
				sel: #tempAt:;
				args: {NumberAST new val: info zeroOriginOffset + 1})]
		ifFalse: [NormalSendAST new
			recv: (leastLocalGetterAstFor: info remoteVector);
			msg: (MessageAST new
				sel: #at:;
				args: {NumberAST new val: info zeroOriginOffset + 1})]
)
leastLocalGetterAstFor: varName <Symbol> ^<SendAST> = (
	| info |
	info:: leastLocalInfoFor: varName.
	assert: [info remoteVector isNil].
	^NormalSendAST new 
		recv: (astForContextAtDepth: info contextDepth);
		msg: (MessageAST new
				sel: #tempAt:;
				args: {NumberAST new val: info zeroOriginOffset + 1})
)
leastLocalInfoFor: varName <Symbol> ^<LocalVarDebugInfo> = (
	
	| bestInfo |
	debugInfo localVariables do: [:info |
		(info name = varName and: [info validPCRange includes: pc]) ifTrue: [
			(bestInfo isNil or: [info contextDepth < bestInfo contextDepth]) ifTrue: [
				bestInfo:: info]]].
	bestInfo notNil ifFalse: [deny].
	^bestInfo
)
localNames = (
	| names |
	names: OrderedCollection new.

	debugInfo localVariables do: [:info |
		((info validPCRange includes: pc)
		and: [info isSynthetic not
		and: [myDepth >= info contextDepth]]) ifTrue: [
			names include: info name ]].
	^names
)
mostLocalInfoFor: varName <Symbol> ^<LocalVarDebugInfo> = (
	
	| bestInfo |
	debugInfo localVariables do: [:info |
		(info name = varName and: [info validPCRange includes: pc]) ifTrue: [
			(bestInfo isNil or: [info contextDepth > bestInfo contextDepth]) ifTrue: [
				bestInfo:: info]]].
	bestInfo notNil ifFalse: [deny].
	^bestInfo
)
pc = (
	^context pc
)
setterAstFor: varName <Symbol> putting: newVal <AST> ^<SendAST> = (
	| info |
	info:: mostLocalInfoFor: varName.
	
	^info remoteVector isNil
		ifTrue:  [NormalSendAST new 
			recv: (astForContextAtDepth: info contextDepth);
			msg: (MessageAST new
				sel: #tempAt:put:;
				args: {
					NumberAST new val: info zeroOriginOffset + 1.
					newVal})]
		ifFalse: [NormalSendAST new
			recv: (leastLocalGetterAstFor: info remoteVector);
			msg: (MessageAST new
				sel: #at:put:;
				args: {NumberAST new val: info zeroOriginOffset + 1.
					newVal})]
)) : ()
class LocalVariableDebugInfo = ((* Debugger information for a local. *)|
      name <Symbol>
	validPCRange <Interval>
	zeroOriginOffset <Integer> (* in remoteVector if not nil, otherwise in context *)
	remoteVector <Symbol | nil>
	contextDepth <Integer> (* 0 = method, 1 = closure, 2 = nested closure, ... *)
	
|)
('as yet unclassified'
isSynthetic ^<Boolean> = (
	(* remoteVector or setter temp: debugger will not want to report these *)
	^name includes: $@
)) : ()'as yet unclassified'
addDebugInfo: sourceInterval <Interval> = (
	(* Byte to map is first byte of e.g. send, which is the next, i.e. pc.
	Hence this must be sent immediately before generating the send or store bytecode.
	Can't send after generating the bytecode because there may be multiple bytes and mapping the last byte is wrong. *)
	debugInfo mapPC: pc to: sourceInterval
)
addLocalVar: var <VarDeclAST> = (
	| offset <Integer> localVariableDebugInfo |

	(indexOfVar: var) isNil ifFalse: [^self]. 
	(* The setterTemp is declared in each expansion of a setter send, but we only need one. *)

	offset:: nextOffset.
	
	(* ('Assigned ', offset printString, ' to ', var name) out. *)
	(currentScope at: var name) offset: offset.
	
	localVariableDebugInfo:: debugInfo addInfoForLocal: (currentScope at: var name).

	#BOGUS. (* Should not include parameters here. *)
	cgen maxLocals: (cgen maxLocals max: offset+1).
	
	^localVariableDebugInfo
)
applyShouldNotPopTo: node = (
	| original result |
	original:: shouldPop.
	shouldPop:: false.
	result:: node apply: self.
	shouldPop:: original.
	^result
)
applyShouldPopTo: node = (
	| original result |
	original:: shouldPop.
	shouldPop:: true.
	result:: node apply: self.
	shouldPop:: original.
	^result
)
cascadeNormal: node <SendAST> = (

	node isCascadedSendNode
		ifTrue: [cascadeNormal: node prevSend]
		ifFalse: [self applyShouldNotPopTo: node recv].
	
	cgen dup.
	self applyShouldNotPopTo: node msg.
	addDebugInfo: (node msg start to: node msg end).
	cgen normalSend: node msg sel numArgs: node msg args size.
	cgen pop.
)
cleanup = (
	cgen: nil.
  	debugInfo: nil.
	contextDepth: nil.
	
	shouldPop: nil.
	maxClosureContextSize: nil.
	scopeStack: nil.

	offsetStack: nil.
)
conditionalNode: node <IfAST> = (
	| ifPos <Integer> endThen <Integer> depth <Integer> thenDepth <Integer> |

	(* cond. branch(L1). then. jump(L2). L1: else. L2: *)
	(* cond. branch(L1). then. L1: *)
	
	(* issues: utilizing short jumps; nested blocks and popping *)
	
	self applyShouldNotPopTo: node condition.
	cgen jumpIf: node ifTrue not by: 0. (* jump to else (but needs patching) *)

	ifPos: cgen bci. (* remember patch address *)
	depth: cgen currentStackDepth.

	assert: [node then isCodeBodyNode and: [node then parameters isEmpty]]
		message: 'Nullary block expected for then clause'.
	pushScope:: scopeMap at: node then.
	node then apply: self.
	thenDepth: cgen currentStackDepth.
	popScope.
	
	node else isNil
		ifTrue: [endThen: cgen bci.]
		ifFalse:
			[assert:[node else isCodeBodyNode and:[node else parameters isEmpty]]
				message: 'Nullary block expected for else clause'.
			cgen currentStackDepth: depth.
			cgen jumpBy: 0.
			(* need jump over else segment (but needs patching) *)
			endThen:: cgen bci.
			pushScope:: scopeMap at: node else.
			node else apply: self.
			popScope.
			cgen patchJumpAt: endThen with: cgen bci - endThen.
			(* patch jump over else *)].
		
	cgen patchJumpIfAt: ifPos with: endThen - ifPos.
	(* patch the jump after the test so it comes to end of the then block *)
	cgen currentStackDepth: (thenDepth max: cgen currentStackDepth).
	(* if one of the branches did a non-local return, the operand stack depth will differ between them. The smaller is the NLR, which doesn't matter, as control will leave the method, so we keep the larger one. *)
	shouldPop ifFalse:
		[cgen currentStackDepth: (cgen currentStackDepth max: depth+1)].
	(* If shouldPop is false we  want to assume the if left a result on the stack, even if it did not because both branches ended in an NLR *)
)
currentMethod ^ <SemanticMethod> = (
	#BOGUS.
	^currentScope at: #'`currentMethod'
)
enclosingObjectNode: node <EnclosingObjectAST> = (
	cgen pushEnclosingObject: node depth.
)
literalArray ^  <Array[Character | Number | String | Symbol]> = (
	| literals <Array[Character | Number | String | Symbol]> |
	literals:: Array new: literalTable size.
	literalTable keysAndValuesDo:[:k : v | literals at: v + 1 put: k].
	^literals
)
localReadNode: node <LocalReadAST> = (
	temporaryNode: node
)
localWriteNode: node <LocalWriteAST> = (
	self applyShouldNotPopTo: node value.
	(* addDebugInfo: (node variable start to: node value end). *)
	addDebugInfo: (node value start to: node value end).
      shouldPop
		ifTrue: [popAndStoreInto: node variable]
		ifFalse: [storeInto: node variable] .
)
methodLocalReturnNode: node <MethodLocalReturnAST> = (
	^self returnStatNode: node
)
nextOffset = (
	|  next |
	next:: offsetStack removeLast.
	offsetStack addLast: next + 1.
	^next
)
nonLocalReturnNode: node <NonlocalReturnAST> = (
	^self returnStatNode: node
)
ordinarySendNode: node <NormalSendAST> = (

	node recv isReceiverNode ifTrue: [^selfSendNode: node].
	node recv isExplicitRcvrNode ifTrue: [^outerSendNode: node].

	self applyShouldNotPopTo: node recv.
	self applyShouldNotPopTo: node msg.
	addDebugInfo: (node msg start to: node msg end).
	cgen normalSend: node msg sel numArgs: node msg args size.
)
outerSendNode: node <OuterSendAST> = (
	
	cgen isOuterSendAbsentReceiver
		ifTrue: 
			[self applyShouldNotPopTo: node msg.
			addDebugInfo: (node msg start to: node msg end).
			cgen outerSend: node msg sel depth: node depth numArgs: node msg args size]
		ifFalse:
			[cgen pushEnclosingObject: node depth.
			self applyShouldNotPopTo: node msg.
			addDebugInfo: (node msg start to: node msg end).
			cgen normalSend: node msg sel numArgs: node msg args size].
)
pc = (
	^cgen code compiledMethodSize + 1
)
popOffsetCounter = (
	offsetStack removeLast.
)
popScope ^ <Scope> = (
  ^scopeStack removeLast
)
processLiteralMessagesOf: node <MethodAST> = (

	| pragmas |
	pragmas: OrderedCollection new.

	node body literalMessages ifNotNil: [:bodyLitMessages |
		bodyLitMessages do: [:messageAST |
			pragmas add: (Pragma
				keyword: messageAST sel
				arguments: (messageAST args collect: 
					[:symbolAST | symbolAST val]) asArray)
		].
	].

	cgen code pragmas: pragmas.
)
pushOffsetCounter = (
	offsetStack addLast: 0.
)
pushScope: s <Scope>= (
	scopeStack addLast: s
)
slotsPerMethod ^ <Integer> = (
	^useAlternateBytecodeSet
		ifTrue: [CodeGeneratorV4 slotsPerMethod]
		ifFalse: [CodeGeneratorV3 slotsPerMethod]
)'initialize-release'
setup = (
	cgen:: useAlternateBytecodeSet
		ifTrue: [CodeGeneratorV4 new] 
		ifFalse: [CodeGeneratorV3 new].
  	debugInfo: DebugInfo new.
	contextDepth: 0.
	
	shouldPop: true.
	maxClosureContextSize: 0.
	scopeStack: OrderedCollection new.

	offsetStack: OrderedCollection new.
	contextDepth: 0.
)'node traversal'
arrayNode: node <ArrayAST> = (
	node elements isNil
		ifTrue: 
			[cgen createEmptyArray: node size]
		ifFalse:
			[node elements do: [:ea | self applyShouldNotPopTo: ea].
			cgen createFullArray: node size].
)
blockLocalReturnNode: node <BlockLocalReturnAST> = (

	self applyShouldNotPopTo: node expr.
	cgen returnFromBlock.
)
boolNode: node <BoolAST> = (
	cgen pushLiteral: node val
)
cascadedSendNode: node <CascadedSendAST> = (
	
	| baseSend ::= node. |
	[baseSend isCascadedSendNode]
		whileTrue: [baseSend: baseSend prevSend].

	baseSend recv isReceiverNode ifTrue:
		[#BOGUS. (* May need special handling once we have absent receiver self sends. *)].
	
	baseSend recv isExplicitRcvrNode ifTrue:
		[outerCascadeNotSupported.
		^self].
	
	baseSend isSuperSendNode ifTrue:
		[superCascadeNotSupported.
		^self].
	
	assert: [baseSend isNormalSendNode or: [baseSend isSelfSendNode]]
		message: 'Unknown type of cascade'.
	
	cascadeNormal: node prevSend.
	self applyShouldNotPopTo: node msg.
	addDebugInfo: (node msg start to: node msg end).
	cgen normalSend: node msg sel numArgs: node msg args size.
)
charNode: node <CharAST> = (
	cgen pushLiteral: node val
)
closureNode: node <ClosureAST> = (
	| blockPos <Integer> savedStackDepth <Integer> savedStackMax <Integer> |

	node copiedValues do: [:each | self applyShouldNotPopTo: each].
	
	(* do this after pushing copied values! *)
	pushScope: (scopeMap at: node).  pushOffsetCounter. contextDepth: contextDepth + 1.

	cgen
		createClosureOfArity: node parameters size
		copying: node copiedValues size
		length: 0 (* needs patching *).

	blockPos: cgen bci.
	savedStackDepth:: cgen currentStackDepth.	
	savedStackMax:: cgen maxStackDepth.
	cgen maxStackDepth: 0.
	 

	(node pushNilCount) timesRepeat: [cgen pushNil]. 
	(* These nils establish the temp frame for temps other than params and copied values. They contribute to the operand stack size, obviating the need to directly include the number of local variables in the context size *)
	node body apply: self.
	
	maxClosureContextSize:: 
		maxClosureContextSize max: (node parameters size + node copiedValues size + cgen maxStackDepth).
	cgen currentStackDepth: savedStackDepth.
	cgen maxStackDepth: savedStackMax.
	
	
	cgen patchClosureJumpAt: blockPos with: cgen bci - blockPos. (* patch jump over block body *)

	popScope. popOffsetCounter. contextDepth: contextDepth - 1.
)
codeBodyNode: node <CodeBodyAST> = (
	|
	stmtCount <Integer>
	stmtSize <Integer> 

	baselineStackDepth <Integer>
	localVariableDebugInfos
	startPC
	validPCRange
	|

	(* This could be a method body, a closure body, part of an inlined block, or a setter send.  Each method body and closure body needs to have separate indexing for temps because their activations will be separate MethodContexts.  Inlined blocks and setter sends should continue to use the same indexing as their enclosing method or closure.  To accomplish this, we use a stack of counters. *)
	
	localVariableDebugInfos:: OrderedCollection new.
	startPC:: pc.
	
	localVariableDebugInfos addAll:
		(node parameters collect: [:param | addLocalVar: param]).
		
	(* Copied values are included at the beginning of temporaries thanks to the Rewriter. *)
		
	node temporaries do: 
		[:temp | | varInfo <SemanticVarDecl> remoteVarInfo <SemanticVarDecl> |
		varInfo:: currentScope at: temp name.
		varInfo remote 
			ifFalse: [localVariableDebugInfos add: (addLocalVar: temp)]
			ifTrue:
				[remoteVarInfo:: currentScope at: varInfo remoteVector name.
				localVariableDebugInfos add:
					(debugInfo addInfoForRemote: varInfo in: remoteVarInfo)]].

	stmtCount:: 1.
	stmtSize:: node statements size.
	baselineStackDepth:: cgen currentStackDepth.
	node statements do: [:statement | 
		stmtCount < stmtSize 
			ifTrue: 
				[(* Pop intermediate results. *)
				self applyShouldPopTo: statement.
				cgen ensureStackDepth: baselineStackDepth]
			ifFalse:
				[(* Pop or retain last result based on context. *)
				statement apply: self.
				shouldPop 
					ifTrue: [cgen ensureStackDepth: baselineStackDepth]
					ifFalse: [assert: [cgen currentStackDepth >= baselineStackDepth]]].
		
		stmtCount:: stmtCount + 1.
	].
	
	validPCRange:: startPC to: pc - 1.
	localVariableDebugInfos do: [:each | each validPCRange: validPCRange].
	^localVariableDebugInfos
)
contextNode: node <ContextAST> = (
	cgen pushThisContext
)
implicitReceiverSendNode: node <ImplicitReceiverSendAST> = (

	cgen isImplicitReceiverSendAbsentReceiver
		ifTrue: 
			[self applyShouldNotPopTo: node msg.
			addDebugInfo: (node msg start to: node msg end).
			cgen implicitReceiverSend: node msg sel numArgs: node msg args size]
		ifFalse:
			[cgen pushImplicitReceiver: node msg sel.
			self applyShouldNotPopTo: node msg.
			addDebugInfo: (node msg start to: node msg end).
			cgen normalSend: node msg sel numArgs: node msg args size].
)
loopNode: node <LoopAST> = (

	(* prologue. L1: cond. branch(L2). body. jump(L1). L2: *)

	|
	topOfLoop <Integer>
	whilePos <Integer>
	baselineStackDepth <Integer>
	skope <Scope>
	localVariableDebugInfos = OrderedCollection new.
	|

	node body isNil ifFalse:
		[pushScope:: scopeMap at: node body].

	baselineStackDepth:: cgen currentStackDepth.
	localVariableDebugInfos addAll: (self applyShouldPopTo: node prologue).
	cgen ensureStackDepth: baselineStackDepth.
	(* Ensure prologue maintains stack depth to prevent overflowing it during say nested loops. *)
	
	topOfLoop:: cgen bci.
	
	skope:: scopeMap at: node condition ifAbsent: [nil].
	skope isNil ifFalse: [pushScope: skope].
	localVariableDebugInfos addAll: (self applyShouldNotPopTo: node condition). 
	skope isNil ifFalse: [popScope].
	
	cgen jumpIf: node isWhileTrue not by: 0. 
	whilePos:: cgen bci. (* remember patch address *)

	node body isNil ifFalse:
		[self applyShouldPopTo: node body.
		cgen ensureStackDepth: baselineStackDepth.
 		(* Ensure loop body maintains stack depth to prevent overflowing it during loop execution. *)
		popScope].
	
	cgen jumpBy: topOfLoop - cgen bci - (cgen sizeOfBackJump). (* jump to top of loop *)
	
	cgen patchJumpIfAt: whilePos with: cgen bci - whilePos.
 	(* patch the jump after the test so it comes to end of the then block *)
	
	shouldPop ifFalse: [cgen pushNil]. (* Result of loop message. Technically wrong, but Squeak makes the same mistake, and loops are almost never evaluated for value. *)
	
	localVariableDebugInfos do: [:each | each validPCRange: (each validPCRange first to: pc - 1)].
)
messageNode: message <MessageAST> = (
	shouldPop ifTrue: [halt].
	message args do: [:arg <ExpressionAST> | arg apply: self].
)
messagePatternNode: node <MessagePatternAST> = (
	halt.
	node parameters do: [:p | addLocalVar: p]
)
methodNode: node <MethodAST> = (
	(* Create a compiled method, whose body is the result of compiling the node's body. *)
	(* | result <LowLevelMethodMirror> | *)
	setup.
	pushScope: (scopeMap at: node). pushOffsetCounter.
		(* currentScope at: #'`currentMethod' put: (SemanticMethod decl: node).  TODO: Is this used? By whom? *)
		cgen
			maxLocals: 0;
			accessModifier: node accessModifier;
			methodName: node pattern selector; 
			argCount: node pattern parameters size;
			literals: literalArray.
                (* uncomment the following to make everything except  test...-methods public*)
		(* (node pattern selector startsWith: 'test')
			ifFalse: [cgen accessModifier: #public]. *)
		node body apply: self.
		cgen code debugInfo: debugInfo. (* set debug info *)
		processLiteralMessagesOf: node.
		(* currentMethod frameSize: cgen maxLocals. *)
	popScope. popOffsetCounter.
	result:: cgen close.
	cleanup.
	^result
)
mixinClassNode: node  = (
	cgen pushMixinClass.
)
nilNode: node = (
	cgen pushNil.
)
numberNode: node <NumberAST>  = (
	cgen pushLiteral: node val.
)
parameterNode: node <ParameterAST> = (
	cgen pushTemporary: (indexOfVar: node).
)
receiverNode: node = (
	cgen pushSelf.
)
returnSelfMethodNode: node <ReturnSelfMethodAST> ^ <LowLevelMethodMirror>  = (
  (* create an empty compiled method with appropriate flags *)
   result:: LowLevelMethodMirror new selector: node selector; 
                                                         returnOnlySelf: currentMethodSource.
   ^result
)
returnStatNode: node <ReturnStatAST> = (

	(* if expr is self, true, false or nil do something special *)
	(node expr isReceiverNode) ifTrue:
		[(* implicit return selfs at end of method have no source range *)
		node expr start == nil ifFalse:
			[addDebugInfo:  (node expr start to: node expr end)].
		^cgen returnSelfFromMethod].
	(cgen hasSpecialReturnBoolean & node expr isBoolNode) ifTrue:
		[addDebugInfo:  (node expr start to: node expr end).
		^cgen returnBooleanFromMethod: node expr val].
	(false & node expr isNilNode) ifTrue:
		[addDebugInfo:  (node expr start to: node expr end).
		^cgen returnNilObj].

	self applyShouldNotPopTo: node expr.
	addDebugInfo: (node expr start to: node expr end).
	#BOGUS yourself. (*block returns don't add this debug info. should they, or is it unnecessary here?*)
	
	cgen returnFromMethod.
)
selfSendNode: node <SendAST> = (

	cgen isSelfSendAbsentReceiver
		ifTrue:
			[self applyShouldNotPopTo: node msg.
			addDebugInfo: (node msg start to: node msg end).
			cgen selfSend: node msg sel numArgs: node msg args size]
		ifFalse:
			[cgen pushSelf.
			self applyShouldNotPopTo: node msg.
			addDebugInfo: (node msg start to: node msg end).
			cgen normalSend: node msg sel numArgs: node msg args size].
)
stringNode: node <StringAST>  = (
	cgen pushLiteral: node val.
)
superSendNode: node <SuperSendNode> = (
	
	cgen isSuperSendAbsentReceiver ifFalse: [cgen pushSelf].
	self applyShouldNotPopTo: node msg.
	addDebugInfo: (node msg start to: node msg end).
	cgen superSend: node msg sel numArgs: node msg args size.
)
symbolNode: node <SymbolAST>  = (
	cgen pushLiteral: node val.
)
temporaryNode: node <TemporaryVariableAST> = (
	(isRemote: node) 
		ifFalse: [cgen pushTemporary: (indexOfVar: node)]
		ifTrue:
			[cgen
				pushRemoteTemporary: (indexOfVar: node)
				inVector: (indexOfVar: (vectorOf: node))].
)'private'
currentMethodSource ^ <String> = (
	^outer Compiler input contents
)
indexOfVar: v <VarDeclAST> ^ <Integer> = (
	^(currentScope at: v name asSymbol) offset
)
isRemote: node ^ <Boolean> = (
	^(currentScope at: node name asSymbol) remote
)
popAndStoreInto: node <VarDeclAST> = (
	(isRemote: node)
		ifFalse: [cgen popIntoTemporary: (indexOfVar: node)]
		ifTrue:
			[cgen
				popIntoRemoteTemporary: (indexOfVar: node)
				inVector: (indexOfVar: (vectorOf: node))]
)
storeInto: node <VarDeclAST> = (
	(isRemote: node)
		ifFalse: [cgen storeIntoTemporary: (indexOfVar: node)]
		ifTrue:
			[cgen
				storeIntoRemoteTemporary: (indexOfVar: node)
				inVector: (indexOfVar: (vectorOf: node))]
)
vectorOf: v <VarDeclAST> ^<VarDelcAST> = (
	^(currentScope at: v name asSymbol) remoteVector
)'scopes'
currentScope ^ <Scope> = (
	^scopeStack last
)) : ()
class MixinClassNode = AST ((* A special AST node to represent access to the mixin class stored in a method. Recall that outer classes have accessors for their nested classes; these lazily manufacture the class, cache it, and later access the cached value.

To do this, the accessor must apply the mixin of the nested class to the superclass. Said mixin is nested within the mixin that defined the accessor (the mixin of the outer class declaration), and can be accessed via the outer mixin's metadata slot.

The issue then is how to get the outer mixin.  In Squeak, for example, the outer mixin is stored in the last literal of every one of its methods (what Squeak calls the ''methodClass''). To reach it, we must push the last literal of the method on to the operand stack.

However, the accessor method is not specified at the byte code level; that would be tedious, and also tied to a specific implementation (the Squeak byte codes). It is much better to specify the method via an AST that is compiled (we could use source code, but the use of synthetic names makes that problematic). 

In order to specify access to the mixin in the accessor method, we need to provide an AST that corresponds to the idea of ''accessing my mixin''. This is what this class does.

The rewriter leaves this node untouched, while the lower layers of the compiler that deal with code generation produce suitable code for it - e.g., Squeak byte codes.

*))
('as yet unclassified'
apply: aTool <ASTTool > ^ <Self> = (
	^aTool mixinClassNode: self
)
isExpressionNode = (
	^true	
)) : ()
class Rewriter = super Rewriter ()
('as yet unclassified'
largeTupleNode: aNode <TupleAST> = (
	(* One might rewrite a large tuple into a tree representing the expression
		(Array new: n) at: 1 put:: e1 ; ...  at: n put: en; yourself
where n is the size of the tuple. The above scheme requires that 1 through n are added to the literal frame (at least for bytecode V3). Existing code includes large tuple literals that when compiled this way exceed the (V3) limit for literals. So we rewrite to 
		(Array braceStream: n) nextPut: e1; ... nextPut: en; contents *)

	|
	els <OrderedCollection[AST]>
	array <NormalSendAST>
	len <NumberAST>
	tup <NormalSendAST>
	index <Integer>
	|
	len:: NumberAST new val: aNode elements size.
	len start: aNode start; end: aNode end.
	
	array:: NormalSendAST new
		to: (NormalSendAST new 
			to: implicitReceiverNode 
			send:  (MessageAST new sel:#Array; args:{}; start: aNode start; end: aNode end) (* Array *)) 
		send: (MessageAST new sel: #braceStream:; args: {len}; start: aNode start; end: aNode end). (* Array braceStream: n *)
	array start: aNode start; end: aNode end.
	
	aNode elements size = 0 ifTrue:
		[^(NormalSendAST new
			to: array
			send: (MessageAST new sel: #contents; args: {})) apply: self].

	index:: 0.
	tup:: aNode elements inject: array into: [:r :e |  | msg <MessageAST> |
	      index:: index + 1.
	      msg:: MessageAST new sel: #nextPut:; 
								args: {e}.
		msg start: aNode start; end: aNode end.
	      index = 1 
			ifTrue: [NormalSendAST new to: r send: msg; start: aNode start; end: aNode end] 
						(* (Array braceStream: n) nextPut: e1 *)
	      	ifFalse: [CascadedSendAST new to: r cascade: msg; start: aNode start; end: aNode end]. (* ...; nextPut: ek *)
		].
	
	^((CascadedSendAST new
		to: tup
		cascade: (MessageAST new sel: #contents; args:{}; start: aNode start; end: aNode end)) start: aNode start; end: aNode end)
	(* (Array braceStream: n) nextPut: e1; ... nextPut: en; contents *)
	 apply: self
	(* visit elements *)
)
mixinClassNode: node <MixinClassNode> = (
	^node
)
processMappedVariable: msg decl: binding = (
	^(msg selector last = $: 
		ifTrue:[binding setterAst: msg args first]
		ifFalse:[binding getterAst])
			apply: self
)
tupleNode: node <TupleAST> = (
	(* We can only use pushConsArray for tuples small enough to fit all elements on the operand stack. The optimial limit is the large frame size minus the current stack depth. We don't track the current depth here, so an arbitrary conservative limit is used. *)
	node elements size > 7 ifTrue: [^self largeTupleNode: node].
	^super tupleNode: node
)) : ()
class SqueakRewriter = ((* Closure indirection, literal tables, ?long tuples. *)|
	scopeStack = OrderedCollection new.
|)
(
class ASTTraverser = ()
('as yet unclassified'
blockLocalReturnNode: node <BlockLocalReturnAST> = (
	node expression apply: self.
)
blockNode: node <BlockAST> = (
	node body apply: self.
)
boolNode: node <BooleanAST> = (
	(* Leaf *)
)
cascadedSendNode: node <CascadedSendAST> = (
	node prevSend apply: self.
	node msg apply: self.
)
charNode: node <CharAST> = (
	(* Leaf *)
)
conditionalNode: node <ConditionalAST> = (
	node condition apply: self.

	depth:: depth - 1.
	node then apply: self.
	node else apply: self.
	depth:: depth + 1.
)
contextNode: node <ContextAST> = (
	(* Leaf *)
)
enclosingObjectNode: node <EnclosingObjectAST> = (
	(* Leaf *)
)
implicitReceiverSendNode: node <ImplicitReceiverSendAST> = (
	node message apply: self.
)
loopNode: node <LoopAST> = (
	depth:: depth - 1.
	assert: [node prologue isCodeBodyNode] message: ''.
	assert: [node condition isCodeBodyNode] message: ''.
	assert: [node body isCodeBodyNode] message: ''.
	node prologue apply: self.
	node condition apply: self.
	node body apply: self.
	depth:: depth + 1.
)
messageNode: node <MessageAST> = (
	node args do: [:each | each apply: self].
)
messagePatternNode: node <MessagePatternAST> = (
	(* Leaf *)
)
methodLocalReturnNode: node <MethodLocalReturnAST> = (
	node expression apply: self.
)
methodNode: node <MethodAST> = (
	node pattern apply: self.
	node body apply: self.
)
mixinClassNode: node <MixinClassNode> = (
	(* Leaf *)
)
nilNode: node <NilAST> = (
	(* Leaf *)
)
nonLocalReturnNode: node <NonlocalReturnAST> = (
	node expression apply: self.
)
numberNode: node <NumberAST> = (
	(* Leaf *)
)
ordinarySendNode: node <OrdinarySendAST> = (
	node receiver apply: self.
	node message apply: self.
)
outerSendNode: node <OuterSendAST> = (
	node message apply: self.
)
receiverNode: node <ReceiverAST> = (
	(* Leaf *)
)
selfSendNode: node <SelfSendAST> = (
	node message apply: self.
)
stringNode: node <StringAST> = (
	(* Leaf *)
)
superSendNode: node <SuperSendAST> = (
	node message apply: self.
)
symbolNode: node <SymbolAST> = (
	(* Leaf *)
)
tupleNode: node <TupleAST> = (
	node elements do: [:each | each apply: self]
)) : ()
class ArrayAST = AST ((* To use pushFullArray/pushEmptyArray *)|
	elements <OrderedCollection[AST] | nil>
	size <Integer>
|)
('as yet unclassified'
apply: tool = (
	^tool arrayNode: self.
)
isExpressionNode = (
	^true
)) : ()
class CogClosureAST = BlockAST (|
	copiedValues
	pushNilCount
|)
('as yet unclassified'
apply: aTool = (
	^aTool closureNode: self
)) : ()
class CopyDownAnalyzer for: node <CodeBodyAST> = ASTTraverser ((* A closure needs to copy variables that are read but defined in the outer scope.  At the point of this anaylsis, indirection has already been added so there should be no reads to an order context and (if this block is nested) outer closures have already copied what they need, so outer reads should only be one level out. *)|
	depth ::= 0.
	shadowing = Dictionary new.
	needsToCopy = Set new.
	pushNilCount ::= 0.
	|node apply: self)
('as yet unclassified'
codeBodyNode: node <CodeBodyNode> = (
	depth:: depth + 1.

	node parameters do: [:each | self shadow: each name].
	node temporaries do: [:each | self shadow: each name].
	
	depth = 1 ifTrue: [pushNilCount: pushNilCount + node temporaries size].


	node statements
		do: [:each | each apply: self].

	depth:: depth - 1.
)
isShadowed: var <Symbol> ^<Boolean> = (
	^(shadowing at: var ifAbsent: [^false]) <= depth
)
localReadNode: node <LocalReadAST> = (
	variableRead: node variable
)
localWriteNode: node <LocalWriteAST> = (
	node value apply: self. (* This happens first! *)
	variableWrite: node variable
)
shadow: var <Symbol> = (
	(* ('shadowing ' , var , ' at ', depth printString) out. *)
	shadowing at: var put: depth.
)
variableRead: var <SlotEntry> = (
	(isShadowed: var name) ifTrue: [^self].
	var remote ifTrue: [^variableRead: var remoteVector].
	(* ('need to copy ', var name, ' for read at ', depth printString) out. *)
	needsToCopy add: var name.
)
variableWrite: var = (
	(isShadowed: var name) ifTrue: [^self].
	var remote ifTrue: [^variableRead: var remoteVector].
	assert: [false] message: var name, ' should have been made indirect'.
	needsToCopy add: var name.
)) : ()
class IndirectionAnalyzer for: node <CodeBodyAST> = ASTTraverser (|
	depth ::= 0.	
	candidates = Set new.
	needsIndirection = Set new.
	hasNestedRead = Set new.

	|node apply: self)
('as yet unclassified'
codeBodyNode: node <CodeBodyNode> = (
	depth:: depth + 1.
	
	depth = 1 ifTrue:
		[ | scope = scopeMap at: node. |
		scope isNil ifFalse: [
		(* Parameters are immutable, so they never need indirection. *)
		node temporaries do: [:temp | candidates add: (scope at: temp name)]]].
	
	node statements
		do: [:each | each apply: self].
	
	depth:: depth - 1.
)
localReadNode: node <LocalReadAST> = (
	self variableRead: node variable
)
localWriteNode: node <LocalWriteAST> = (
	node value apply: self. (* This happens first! *)
	self variableWrite: node variable
)
variableRead: var <SlotEntry> = (
	(candidates includes: var) ifFalse: [^self].
	
	depth > 1 ifTrue: [hasNestedRead add: var].
)
variableWrite: var <SlotEntry> = (
	(candidates includes: var) ifFalse: [^self].
	
	(* Depth 1 being the code body that declaring the variables we're considering. *)
	depth > 1 ifTrue: [ 
		(* this is a nested write, must indirect *)
		needsIndirection add: var.
		(* ('nested write of ', var) out. *)
	] ifFalse: [
		(hasNestedRead includes: var) ifTrue: [ 
			(* write after nested read, must indirect *)
			(* ('read after write of ', var) out. *)
			needsIndirection add: var.
		].
	].
)) : ()'as yet unclassified'
addLiteral: s <Character | Number | String | Symbol> = ( 
	(literalTable includesKey: s) ifFalse: [literalTable at: s put: literalTable size].
)
blockLocalReturnNode: node <BlockLocalReturnAST> = (
	^rewriter BlockLocalReturnAST expression: (node expression apply: self)
)
blockNode: node <BlockAST> ^ <ClosureAST> = (
(* This method should only be called on a block that is not going to be inlined.

There are two things that require rewriting in blocks.

1. a.  Non-empty blocks must have their body rewritten recursively
    b. Empty blocks need a nil expression in them.
    c. All blocks must have their parameters rewritten from generic VarDecls to ParameterASTs. 
2. If the last statement in a block is not a return, the block returns
the expression to its caller. So the last expression needs to be replaced by a 'local return'.

We accomplish (1) by calling rewriteInlinedBlockNode:, and then do  (2) explicitly.

The reason for this separation is that step (1) is always needed; step (2) is only needed if the block is not inlined.
*)
	|
	result <CogClosureAST>
	needsCopy <NeedsCopyFinder>
	copiedVarNames <List[Symbol]>
	copiedValues <List[TemporaryVariableAST]>
	setterTempNeeded <Boolean>
	|
	copiedVarNames: OrderedCollection new.
	needsCopy:: (CopyDownAnalyzer for: node body).
	needsCopy needsToCopy do: [:var <Symbol> |	
			(* (var , ' will be copied') out. *)
			copiedVarNames addFirst: var.
			
			node body temporaries addFirst: (
				MutableSlotDefAST new
					slotDecl: (rewriter TemporaryAST name: var type: nil);
					initializer: nil
			).
			(scopeMap at: node) at: var put:
				(SlotEntry
					forDeclaration: (rewriter TemporaryAST name: var type: nil)
					atDepth: nil)].
		
	copiedValues:: copiedVarNames collect: [:var <Symbol> |
		rewriter LocalReadAST of: (currentScope at: var)].
	
	pushScopeForNode: node.
	result:: CogClosureAST new
		body: (node body apply: self).
	result body parameters: (OrderedCollection withAll: node parameters).
	result copiedValues: copiedValues.
	result pushNilCount: needsCopy pushNilCount.
	popScope.
	scopeMap at: result put: (scopeMap at: node).
	^result copyPositionFrom: node
)
boolNode: node <BooleanAST> = (
	(* No need to add literal: true and false are quick literals in both bytecode sets. *)
	^node
)
cascadedSendNode: node <CascadedSendAST> = (
	^(CascadedSendAST new
		to: (node prevSend apply: self)
		cascade: (node msg apply: self))
		copyPositionFrom: node
)
charNode: node <CharAST> = (
	addLiteral: node val.
	^node
)
codeBodyNode: node <CodeBodyAST> ^ <CodeBodyAST> = (
	| 
	bodyScope <Scope>
	statements <OrderedCollection[StatementAST]>
	temps <OrderedCollection[TemporaryAST]> 
	result <CodeBodyAST>
	itv <TemporaryAST>
	itvEntry <SlotEntry>
	i <Integer>
	finder <NeedsIndirectionFinder>
	|
	pushScopeForNode: node. 
	bodyScope:: scopeMap at: node.
	
	temps:: OrderedCollection withAll: node temporaries.

	statements:: OrderedCollection new.
 	finder:: IndirectionAnalyzer for: node.
	finder needsIndirection isEmpty ifFalse: [
		(* introduce an indirect temp vector aka remote vector *)
		itv:: rewriter TemporaryAST
			name: ('@indirectionVector',scopeStack size printString) asSymbol
			type: nil.
		itvEntry:: SlotEntry forDeclaration: itv atDepth: currentDepth.
		bodyScope
			at: itv name put: itvEntry; (* getter *)
			at: (itv name,':') asSymbol put: itvEntry. (* setter *)
		temps addLast: itv. (* N.B. addLAST, copied values must be at beginning *)
		
		(* initialize itv in the very first statement *)
		statements add: (rewriter LocalWriteAST
			to: itvEntry
			assign: (ArrayAST new size: finder needsIndirection size)).
	].

	i::0.
	finder needsIndirection do: [:tempEntry <SlotEntry> |
		assert: [tempEntry remote not].
		tempEntry
			remote: true;
			remoteVector: itvEntry;
			offset: i.
		i:: i+1.
	].

	(* add actual statements in code body *)
	node statements
		do: [:stat | statements add: (stat apply: self)].

	result:: CodeBodyAST new
            parameters: node parameters
            temporaries: temps
            statements: statements;
		literalMessages: node literalMessages;
            start: node start; end: node end.
	popScope.
	scopeMap at: result put: bodyScope.
	^result copyPositionFrom: node
)
conditionalNode: node <ConditionalAST> = (
	^(rewriter ConditionalAST
		if: (node condition apply: self)
		is: node ifTrue
		then: (inlinedCodeBodyNode: node then)
		else: (inlinedCodeBodyNode: node else))
		copyPositionFrom: node
)
contextNode: node <ContextAST> = (
	^node
)
enclosingObjectNode: node <EnclosingObjectAST> = (
	(* The V3 bytecode set must find the depth in the literal table. *)
	useAlternateBytecodeSet ifFalse: [addLiteral: node depth].
	^node
)
implicitReceiverSendNode: node <ImplicitReceiverSendAST> = (
	^(rewriter ImplicitReceiverSendAST new
		message: (node message apply: self);
		depth: node depth)
		copyPositionFrom: node
)
inlinedCodeBodyNode: node <CodeBodyAST> ^ <CodeBodyAST> = (
	| 
	bodyScope <Scope>
	result <CodeBodyAST>
	|
	pushScopeForNode: node. 
	bodyScope:: scopeMap at: node.
	
	result:: CodeBodyAST new
            parameters: node parameters
            temporaries: node temporaries
            statements: (node statements collect: [:stat | stat apply: self]);
		literalMessages: node literalMessages;
            start: node start; end: node end.
	popScope.
	scopeMap at: result put: bodyScope.
	^result copyPositionFrom: node
)
localReadNode: node <LocalReadAST> = (
	^node
)
localWriteNode: node <LocalWriteAST> = (
	^rewriter LocalWriteAST
		to: node variable
		assign: (node value apply: self)
)
loopNode: node <LoopAST> = (
	^rewriter LoopAST new
		prologue: (inlinedCodeBodyNode: node prologue);
		condition: (inlinedCodeBodyNode: node condition);
		isWhileTrue: node isWhileTrue;
		body: (inlinedCodeBodyNode: node body)
)
messageNode: node <MessageAST> = (
	addLiteral: node sel.
	^(MessageAST new
		send: node sel
		with: (node args collect: [:ea | ea apply: self]))
		copyPositionFrom: node
)
messagePatternNode: node <MessagePatternAST> ^ <MessagePatternAST> = (
	^node
)
methodLocalReturnNode: node <MethodLocalReturnAST> = (
	^rewriter MethodLocalReturnAST expression: (node expression apply: self)
)
methodNode: node <MethodAST> = (
	| pattern body result |
	literalTable:: Dictionary new.
	
	pattern:: node pattern apply: self.
	body:: node body apply: self.
	body parameters: pattern parameters.
	result:: MethodAST new 
		pattern: pattern
		body: body
		accessModifier: node accessModifier.
	scopeMap at: result put: (scopeMap at: node).
	^result
)
mixinClassNode: node <MixinClassNode> = (
	^node
)
nilNode: node <NilAST> = (
	(* No need to add literal: nil is a quick literal in both bytecode sets. *)
	^node
)
nonLocalReturnNode: node <NonlocalReturnAST> = (
	^rewriter NonlocalReturnAST expression: (node expression apply: self)
)
numberNode: node <NumberAST> = (
	addLiteral: node val.
	^node
)
ordinarySendNode: node <OrdinarySendAST> = (
	^(rewriter OrdinarySendAST
		to: (node receiver apply: self)
		send: (node message apply: self))
		copyPositionFrom: node
)
outerSendNode: node <OuterSendAST> = (
	(* The V3 bytecode set must find the depth in the literal table. *)
	useAlternateBytecodeSet ifFalse: [addLiteral: node depth].
	^(rewriter OuterSendAST new
		message: (node message apply: self);
		depth: node depth)
		copyPositionFrom: node
)
pushScope: s <Scope> = (
	^scopeStack addLast: s
)
pushScopeForNode: node <AST> = (
	^scopeStack addLast: (scopeMap at: node)
)
receiverNode: node <ReceiverAST> = (
	^node
)
selfSendNode: node <SelfSendAST> = (
	^(rewriter SelfSendAST new
		message: (node message apply: self))
		copyPositionFrom: node
)
stringNode: node <StringAST> = (
	(* Note we treat it as a symbol. *)
	addLiteral: node val asSymbol.
	^node
)
superSendNode: node <SuperSendAST> = (
	^(rewriter SuperSendAST new
		message: (node message apply: self))
		copyPositionFrom: node
)
symbolNode: node <SymbolAST> = (
	addLiteral: node val.
	^node
)
tupleNode: node <TupleAST> = (
	^ArrayAST new 
		elements: (node elements collect: [:ea | ea apply: self]);
		size: node elements size 
)'scopes'
currentScope ^ <Scope> = (
	^scopeStack last
)
popScope ^<Scope> = (
	^scopeStack removeLast
)) : ()''
splitInitializersFor: aNode <ClassHeaderAST> ^ <Collection[MethodAST]> = (

	(* Because the init sequence may not fit in a single method, because too many literals are needed for all the setters and getters, we produce an init method that calls a number of private sub-initializers. This main initializer routine first calls the superclass's main initializer, then calls the subinitializers to set all the slots, and then any initialization code in the header. Answers a collection of MethodASTs including the main and sub intializers. *)
	| 
	start <integer>
	end <Integer>
	stmts <List[StmtAST]> 
	initHdr <MessagePatternAST>
	body <CodeBodyAST>  
	slots <Collection[StmtAST]>
	subs <Collection[MethodAST]>
	copier <ASTCopier>
	|

	start:: aNode superConstructorCall start.
	end::  aNode  superConstructorCall end.
	copier:: ASTCopier new.
	initHdr:: aNode constructor apply: copier. (* set up scope with constructor parameters *)
	initHdr selector: (initializerSelectorNameFor: initHdr selector).	
	subs:: subInitializersFor: aNode. (* compute (empty) subinitializers *)
	
	slots:: (aNode slots reject: [:slot | slot initializer isNil]) collect: 
		[:slot <SlotDefAST> |
		| aMsg <MessageAST> slotInitializer <ExpressionAST> |
		slotInitializer:: slot initializer apply: copier.
		aNode isSeq ifFalse: [slotInitializer:: futureFor: slotInitializer].
		aMsg:: MessageAST new 
			send: (setterSelectorFor: slot) with:  {slotInitializer};
			start: slot start;
			end: slot end.
		NormalSendAST new msg: aMsg; 
			recv: (selfNode start: aMsg start; end: aMsg end);
			start: aMsg start; end: aMsg end].

	(* populate subinit methods with slot initialization code *)
	(1 to: slots size) with: slots do: [:n :stmt |
		(subs at: n  // methodCompiler slotsPerMethod + 1) body statements add: stmt]. 

	stmts:: OrderedCollection new.
	(* first call the superclass initializer *)
	stmts add: (superConstructorCallFor: aNode). 
	(* then call the subinitializers for the slots *)
	1 to: subs size -1 do: 
		[:i | stmts add: (sendForSub: (subs at: i))].
	(* then the initializer code *)
	aNode initExprs do:
		[:ie <ExpressionAST> | stmts add: (ie apply: copier)]. 

	body:: CodeBodyAST new
		temporaries: OrderedCollection new
		statements: stmts;
		start: start; end: end.
	subs at: subs size put: (MethodAST new 
		pattern: initHdr body: body accessModifier: #public;
		start: start; end: end).

	^subs
)'as yet unclassified'
private astForComputingMixinApplicationClassDeclaration: simpleName <String> 
fullName: nestedName <String> 
superCall: superCall <NormalSendAST> 
iList: iList <List[SendAST]>
factoryName: factoryName <Symbol> ^ <NormalSendAST> = (
(* Returns a tree representing the computation of a mixin application
class. First we compute the chain of anonymous mixin applications that
constitutes the superclass of the desired mixin application class using
#inheritanceASTWIthSuperclass:iList: and then we define a named mixin
application by applying the last element of the inheritance chain to that
superclass.
.*)
	|  
	nestedMixin = nestedMixinASTFor: nestedName.
	superChain = inheritanceASTWIthSuperclass: ( superCall recv)  iList:  iList allButLast.
	mixinApp = NormalSendAST new
		to: (mixinSendTreeFor: iList last recv)
		send: #apply:withName:index:
		with: {
			superChain.
			NormalSendAST new
				to: (NormalSendAST new
					to: (SymbolAST new val: nestedName, '`')
					send: #,
					with: {NormalSendAST new
						to: (NormalSendAST new to: selfNode send: #hash with: {})
						send: #asString
						with: {}})
				send: #asSymbol
				with: {}.
				NumberAST new val: iList size
				}.
	|
	
	^addInitializersToMixinApplication: mixinApp nestedMixin: nestedMixin factoryName: factoryName
	
)
checkForDuplicateNames: node <ClassDeclarationAST> = (

	| namesWithDuplicates names duplicateNames |
	
	namesWithDuplicates:: OrderedCollection new.
	node instanceSide categories do: [:cat |
		cat methods do: [:each | namesWithDuplicates add: each selector]].
	
	node hdr slots do: [:each |
		namesWithDuplicates add: each name.
		each isMutable ifTrue: [namesWithDuplicates add: each name, ':']].
	
	namesWithDuplicates
		addAll: (node instanceSide nestedClasses collect: [:each | each name]).

	names:: Set new.
	duplicateNames:: Set new.
	namesWithDuplicates do: [:each | | present |
		present:: true.
		names include: each ifNew: [present:: false].
		present ifTrue: [duplicateNames add: each]].
	
	duplicateNames isEmpty ifFalse: [ | message |
		message:: String streamContents: [:s |
			s
				nextPutAll: 'Class ';
				nextPutAll: node name;
				nextPutAll: '" has multiple definitions for '.
			duplicateNames
				do: [:each | s nextPutAll: each asString]
				separatedBy: [ s nextPutAll: ','].
			s nextPutAll: '.'.].
		assert: [false] message: message].
)
private classDeclarationInitializerFor: aNode <ClassHeaderAST> ^<MethodAST> = (

	| 
	start <integer>
	end <Integer>
	stmts <List[StmtAST]> 
	initHdr <MessagePatternAST>
	body <CodeBodyAST>  
	slots <Collection[StmtAST]>
	copier <ASTCopier>
	|

	start:: aNode superConstructorCall start.
	end::  aNode  superConstructorCall end.
	copier:: ASTCopier new.
	initHdr:: aNode constructor apply: copier. (* set up scope with constructor parameters *)
	initHdr selector: (initializerSelectorNameFor: initHdr selector).	
		
	(* Call super initializers *)
	stmts:: mixinInitializationCallsFor: aNode inheritanceChain.

     (* run slot initializers *)
	slots:: computeSlotInitializersFor: aNode.

	stmts addAll: slots.
	
	(*  run init expressions *)
	aNode initExprs do:
		[:ie <ExpressionAST> | stmts add: (ie apply: copier)]. 
	
	body:: CodeBodyAST new
		parameters: initHdr parameters
		temporaries: OrderedCollection new
		statements: stmts;
		start: start; end: end.
	^MethodAST new 
		pattern: initHdr body: body accessModifier: #public;
		start: start; end: end
)
public compileClassSource: src <ReadStream> within: enclosing <MixinMirror> ^  <MixinRep>   = (
(* the input is source code for a class declaration and a mirror on the enclosing class of the class to be
compiled. The result is a pair of type MixinRep, defined recursively as:

MixinRep ={CompiledMixinMirror. Collection[MixinRep]}

It is a tuple whose first element is an uninstalled low level mirror representing an enclosing
class, and whose second element is a collection of MixinReps, each of which represents one of the nested classes of the enclosing class.

The actual MixinRep returned depends on whether we are compiling a top level class. If so, enclosing is nil, and we return a MixinRep representing said top level class and all its nested classes (recursively).

Otherwise, we return a MixinRep which represents the enclosing class and the nested class that was given in the source (and all its nested classes recursively).
*)

| tree <AST>  mixinRep  <MixinRep> |
  enclosing isNil
	ifTrue: [	tree:: parser compilationUnit parse: src ]
	ifFalse: [	tree:: parser nestedClassDecl parse: src ].
  saveInput: src.
  setScopeFor: tree in: enclosing. (* scope construction phase *)

  mixinRep:: enclosing isNil
	ifTrue: [ classNode: tree ]
	ifFalse: [ compileNestedClassAST: tree into: enclosing declaration ].

  scopeMap:: Dictionary new. 
  ^ mixinRep
)
public compileExpressionSource: src <ReadStream | String> inContext: aContext <MethodContext> inMixin: enclosing <MixinMirror> allowBlackMarket: allowBlackMarket <Boolean> ^<LowLevelMethodMirror>
= (
	(* Compile a DoIt method.  If aContext is not nil, compile a DoItIn:ctxt method where accesses to names in scope of the context are rewritten to access through the context.  Cog closures complicate this rewriting compared to BlockContexts, as access might be rather indirect.  Also, in cases where a temp is only a copied value and not in an remote indirection vector, it would be impractical to handle writes such that they actually track down all places where the temp lives and update them.  In these cases, we should merely find the most local copy, which I believe is what closure compiled Squeak does. *)
	|
	body <CodeBodyAST>
	stm <ReadStream>
	method <MethodAST>
	result <LowLevelMethodMirror>
	|
	assert: [aContext isNil or: [aContext method methodClass language isNewspeakLanguage3]]
		message: 'Context is not NS3!'.
	
	(* Parse as a code body *)
	src isString ifTrue: [stm: src readStream] ifFalse: [stm: src].
	body:: (allowBlackMarket ifTrue: [internalParser] ifFalse: [parser])
		doItExpression parse: stm.
	input: stm.
	
	(* Ensure last statement is a return statement *)
	body statements isEmpty ifFalse:
		[body statements last isReturnStatNode ifFalse:
			[| last |
			last:: body statements removeLast.
			body statements add: (ReturnStatAST new 
				expr: last; start: last start; end: last end)]].

	(* Put into a method *)
	method:: MethodAST new
		pattern: (aContext isNil
			ifTrue: [allowBlackMarket 
				ifTrue: [MessagePatternAST new selector: #DoItWith: parameters: 
					{VarDeclAST name: doitBlackMarketArgumentName type: nil}]
				ifFalse: [MessagePatternAST new selector: #DoIt parameters: {}]]
			ifFalse: [allowBlackMarket
				ifTrue: [MessagePatternAST new selector: #DoItIn:With: parameters: 
					{VarDeclAST name: doitContextArgumentName type: nil.
					VarDeclAST name: doitBlackMarketArgumentName type: nil}]
				ifFalse: [MessagePatternAST new selector: #DoItIn: parameters: 
					{VarDeclAST name: doitContextArgumentName type: nil}]]);
		body: body;
		accessModifier: #public.
	
	(* Build scope *)
	aContext isNil ifTrue: [
		setScopeFor: method in: enclosing.
	] ifFalse: [
		| debugInfo debugMapper |
		debugInfo:: (aContext method propertyValueAt: #debugInfo) ifNil:
			[(compileMethodSource: aContext method getSource asString readStream
				within: enclosing) debugInfo. (* Resets scope, so do first *)].
		setScopeFor: method in: (enclosing).
		debugMapper:: debugInfo mapperForContext: aContext.
		debugMapper localNames do: [:name <Symbol> |
			currentScope at: name put: 
				(DebugMappedLocalEntry name: name mapper: debugMapper).
			currentScope at: (name,':') asSymbol put: 
				(DebugMappedLocalEntry name: name mapper: debugMapper).
		].
	].
	
	(* Rewrite and compile *)
	((method apply: rewriter) 
		apply: squeakRewriter)
			apply: methodCompiler.
 	result: methodCompiler result.
	result src: stm contents.
	scopeMap:: Dictionary new.
	^result
)
compileNestedClassAST: tree <ClassDeclarationAST | MixinApplicationClassAST> into: enclosingClass <ClassDeclarationMirror> ^ <MixinRep> = (
	| 
		nestedClassMixinReps <List[MixinRep]>
		enclosingMirror <CompiledMixinMirror>
		lowMirror <LowLevelMixinMirror>
	|
		enclosingMirror:: enclosingClass compiledMixinMirror.
		lowMirror:: enclosingMirror lowLevelMirror.
		
		nestedClassMixinReps:: compileNestedClass: tree within: lowMirror.
		generateSlotAccessorsFor: lowMirror.
		
		 
		
		^ { enclosingMirror . nestedClassMixinReps }.
		(* what about other nested classes of enclosing? Not the compiler's responsibility; they are unchanged, and the surrounding reflection system should add them to the mixin rep *)
)
computeSlotInitializersFor: aNode <ClassHeaderAST> ^ <Collection[StmtAST]> = (
	^(aNode slots reject: [:slot | slot initializer isNil]) collect: 
		[:slot <SlotDefAST> |
		| aMsg <MessageAST> slotInitializer <ExpressionAST> |
		slotInitializer:: slot initializer apply: ASTCopier new.
		aNode isSeq ifFalse: [slotInitializer:: futureFor: slotInitializer].
		aMsg:: MessageAST new 
			send: (setterSelectorFor: slot) with:  {slotInitializer};
			start: slot start;
			end: slot end.
		NormalSendAST new msg: aMsg; 
			recv: (selfNode start: aMsg start; end: aMsg end);
			start: aMsg start; end: aMsg end].
)
doitBlackMarketArgumentName = (
	^'`'
)
doitContextArgumentName = (
	(* Use #'@context', but this can't be mentioned in the debugger,
	and doesn't need to be since the debugger provides Inspect Context.
	But if you're desperate, use ThisContext, as does the Squeak debugger. *)
	^true ifTrue: [#'@context'] ifFalse: [#'ThisContext']
)
futureFor: expression <ExpressionAST> ^<ExpressionAST> = (
	(* @here Future computing: [expression] *)
	| 
	sscope <Scope>
	block <BlockAST>
	codebody <CodeBodyAST>
	futureExp <ExpressionAST>
	|
	
	sscope:: Scope new.
	sscope superScope: currentScope.
	
	futureExp:: NormalSendAST new
		recv: (NormalSendAST new recv: hereNode; msg: (MessageAST new sel: #Future; args: {}));
		msg: (MessageAST new sel: #computing:; args: {
			block:: BlockAST new body: (
				 codebody:: CodeBodyAST new
					parameters: OrderedCollection new;
					 temporaries: OrderedCollection new;
					statements: {expression}
			)
		}).
		
	scopeMap at: codebody put: sscope.
	scopeMap at: block put: sscope.
	^futureExp
)
generateSlotAccessorsFor: m <MixinMirror> = (
(* we could let the accessors be generated in the mixin. However, we want to optimize mixin application, so it is best if an initial set of accessors is compiled into the mixin. *)
| 
  ivIndices <SequenceableCollection[Integer]> 
  ivs <Collection[InstanceVariableMirror]> 
|
  ivs:: m instVars collect:[:iv | iv]. (* Gross - extract a collection from the mirror group *)
      ivIndices:: 1 to: ivs size.
  ivs with: ivIndices do: [:iv <InstanceVariableMirror> :n <Integer>  | 
    m methods addMirror: (iv generateGetterForSlot: n).
    m methods addMirror: (iv generateSetterForSlot: n)]
)
inheritanceASTWIthSuperclass: sc <ExpressionAST>  iList:  iList <List[SendAST]> ^ <NormalSendAST> = (
(*
Produce an AST representing a series of mixin applications corresponding 
to the contents of the inheritance chain described by iList, applied to
the tree representing the superclass clause sc.
Each mixin application is provided an index according to its position in
the list. This index is used as a suffix when renaming the instance initializer
of the mixin application, in order to ensure that none of the instance initializers
of the various mixin applications produced conflict with each other.
*)
	^(iList inject: {sc. 1} into:[:nsc <{ExpressionAST. Integer}> :m <SendAST> | 
		{NormalSendAST new
				to: (mixinSendTreeFor: m recv)
				send: #apply:index:
				with: {nsc first. NumberAST new val: nsc last}.
		nsc last + 1
		}
		]) first.
)
methodNode: aNode <MethodAST> ^ <LowLevelMethodMirror> = (

	| tree <AST> |
	tree:: aNode apply: rewriter. (* rewrite ast *)
	tree:: tree apply: squeakRewriter.
	tree apply: methodCompiler. (* final pass: visit rewritten ast *)
	(* pop scope? *)
	^(methodCompiler result) src: (sourceForNode: aNode)
)
mixinApplicationClassNode: nc <MixinApplicationClassAST> 
	in: mixinMirror <LowLevelMixinMirror> category: cat <String>  ^ <MixinRep> = (
	
	| 
	llmam = LowLevelMixinMirror 
		named: (namer fullyQualifySimpleName: nc name with: mixinMirror name) isMeta: false.
	scope <Scope> = scopeMap at: nc. 
	init <LowLevelMethodMirror>  = 
		processMethod: (mixinApplicationInitializerFor: nc) inScope: scope in: llmam.
	iListSource <String>
	hdrSource <String>
	cmm <CompiledMixinMirror> 
	|
	iListSource:: nc inheritanceChain allButLast
		 inject: '' into: [:s  : e |  s, (sourceForNode: e), ' <: '].
      iListSource:: iListSource, (sourceForNode:  nc inheritanceChain first).
      hdrSource:: nc name, ' = ', iListSource.
      cmm:: (CompiledMixinMirror header: hdrSource mirror: llmam)
					category: cat;
					comment:''.
	init 	metadata 
			at: #isSubinitializer put: true;
			at: #hasBody put: false.
	llmam methods addMirror: init.
	processFactoryFor: nc inScope: scope in: llmam classMixin.
	^{cmm. {}}
)
mixinApplicationInitializerFor: classDef <MixinApplicationClassAST> = (
(* Probably migrate to superclass *)
	| 
	pattern = classDef constructor apply: ASTCopier new.
	iList = classDef inheritanceChain.
	stmts = mixinInitializationCallsFor: iList.
	body = CodeBodyAST new temporaries: {} statements: stmts.
	mtd = MethodAST new pattern: pattern body: body accessModifier: #public.
	|
	pattern selector: (initializerSelectorNameFor: pattern selector).
	body start: stmts first start; end: stmts last end.
	mtd start: pattern start; end: body end.
	^mtd.
)
mixinCall: number <Integer> inheritanceList: iList <Collection[SendAST]>  ^ <NormalSendAST> = (
	(* Probably migrate to superclass *)
	(* create call to superclass initializer *)
	|
	aNode <SendAST> = iList at: number.
	var  <VariableAST> = VariableAST new name: #self; start: aNode start; end: aNode end.
	mixinMsg <MessageAST> = aNode msg apply: ASTCopier new.
	send <NormalSendAST> = NormalSendAST new to: var send: mixinMsg; start: aNode start; end: aNode end.
	|

      mixinMsg sel: (superMixinInitializerSelectorFor: mixinMsg selector number: number -1).
	^send
)
mixinInitializationCallsFor: iList <List[SendAST]>  ^ <List[StmtAST]> = (
      | stmts <List[StmtAST]> = OrderedCollection new. |
	iList size to: 2 by: -1 do:[:i | stmts addFirst: (mixinCall: i inheritanceList: iList)].
	stmts addFirst: (superFactoryCallFor: iList first).
	^stmts
)
private mixinInitializerFor: aNode <ClassHeaderAST> ^ <MethodAST> = (

	(* WIP: Creates the instance initializer method for the mixin. This code is shared by all invocations of the mixin defined by the argument *)
	| 
	start <integer>
	end <Integer>
	stmts <List[StmtAST]> 
	initHdr <MessagePatternAST>
	body <CodeBodyAST>  
	slots <Collection[StmtAST]>
	subs <Collection[MethodAST]>
	copier <ASTCopier>
	|
	copier:: ASTCopier new.
	initHdr:: aNode constructor apply: copier. (* set up scope with constructor parameters *)
	initHdr selector: (mixinInitializerSelectorNameFor: initHdr selector).	
	
	stmts:: (aNode slots reject: [:slot | slot initializer isNil]) collect: 
		[:slot <SlotDefAST> |
		| aMsg <MessageAST> slotInitializer <ExpressionAST> |
		slotInitializer:: slot initializer apply: copier.
		aNode isSeq ifFalse: [slotInitializer:: futureFor: slotInitializer].
		aMsg:: MessageAST new 
			send: (setterSelectorFor: slot) with:  {slotInitializer};
			start: slot start;
			end: slot end.
		NormalSendAST new msg: aMsg; 
			recv: (selfNode start: aMsg start; end: aMsg end);
			start: aMsg start; end: aMsg end].

	(* then add the initializer code *)
	aNode initExprs do:
		[:ie <ExpressionAST> | stmts add: (ie apply: copier)]. 
	start:: stmts isEmpty ifTrue:[initHdr end+2] ifFalse:[stmts first start].
	end:: stmts isEmpty ifTrue:[initHdr end + 3] ifFalse:[stmts last end].
	body:: CodeBodyAST new
		temporaries: OrderedCollection new
		statements: stmts;
		start: start; end: end.
	^MethodAST new 
		pattern: initHdr body: body accessModifier: #public;
		start: initHdr start; end: end.
)
mixinInitializerSelectorNameFor: selector <Symbol> ^<Symbol> = (
	^('mixinInitializer`', selector) asSymbol
)
mixinSendTreeFor: c <ExpressionAST> ^ <NormalSendAST> = (
	^NormalSendAST new 
		to: c 
		send: (MessageAST new send: #mixin with: {}); 
		start: c start; end: c end
)
private processClassSideOf: aNode <ClassAST> ofMixin: mixinMirror <LowLevelMixinMirror> = (
| primaryFactory <MethodMirror> factoryAST <MethodAST> |
	processFactoryFor: aNode in: mixinMirror.
	processSide: aNode classSide ofMixin: mixinMirror. (* compile class methods *)
)
private processInstanceSideOf: aNode <ClassAST> ofMixin: mixinMirror <LowLevelMixinMirror> ^ <Collection[MixinRep]> = (
| side <SideAST> |
	processInitializerFor: aNode in: mixinMirror.
	side:: aNode instanceSide.
	processSide: side ofMixin: mixinMirror. (* compile instance methods *)
	^compileNestedClassesOf: side within: mixinMirror. (* gather nested classes *)
)
private processMethod: aNode <MethodAST> 
inScope: s <Scope>  in: mixinMirror <LowLevelMixinMirror> ^ <LowLevelMethodMirror> = (
|  scopeBuilder <ScopeBuilder> method <LowLevelMethodMirror> |

	scopeBuilder:: ScopeBuilder nestedIn: s atLevel: currentDepth.
	aNode apply: scopeBuilder.
(* need to build scope for method before visiting it *)
	method:: methodNode: aNode.
	method metadata at: #isSynthetic put: true.
	mixinMirror methods addMirror: method.
	^method
(* install method *)	
)
private processSide: side <SideAST> ofMixin: mixinMirror <LowLevelMixinMirror> = (
  side categories do:[:cat |  | mirr <MethodMirror> |
                     cat methods do:[:m | 
	(* who checks that the methods do not conflict with each other or with another slot or class in the same enclosing class? The mirror or the compiler? *)
	                         	mirr:: methodNode: m.
					  	mirr metadata at: #category put: cat name.
                               	mixinMirror methods addMirror: mirr.
                                 ].
                     ].
)
saveInput: src <ReadStream> = (
  src position: 1.
  input: src. (* save the input *)
)
setterSelectorFor: slot <SlotDefAST | InstanceVariableMirror> ^<Symbol> = (
	^((slot isMutableSlot ifTrue: [ '' ] ifFalse: [ 'setOnce`' ]), slot name, ':') asSymbol.
)
sourceForNode: node <AST> ^ <String> = (
	| savedPos <Integer> pos <Integer> len <Integer> s <String> |
	savedPos:: input position.
	len: (node end - node start) + 1.
	s:: String new: len.
	input position: node start -1.
	input next: len into: s startingAt:1.
	input position: savedPos.
	^s
)
splitMixinInitializersFor: aNode <ClassHeaderAST> ^ <Collection[MethodAST]> = (

	(* Because the init sequence may not fit in a single method, because too many literals are needed for all the setters and getters, we produce an init method that calls a number of private sub-initializers. This main initializer routine first calls the superclass's main initializer, then calls the subinitializers to set all the slots, and then any initialization code in the header. Answers a collection of MethodASTs including the main and sub intializers. *)
	| 
	start <integer>
	end <Integer>
	stmts <List[StmtAST]> 
	initHdr <MessagePatternAST>
	body <CodeBodyAST>  
	slots <Collection[StmtAST]>
	subs <Collection[MethodAST]>
	copier <ASTCopier>
	|

	copier:: ASTCopier new.
	initHdr:: aNode constructor apply: copier. (* set up scope with constructor parameters *)
	initHdr selector: (initializerSelectorNameFor: initHdr selector).	
	subs:: subInitializersFor: aNode. (* compute (empty) subinitializers *)
	
	slots:: (aNode slots reject: [:slot | slot initializer isNil]) collect: 
		[:slot <SlotDefAST> |
		| aMsg <MessageAST> slotInitializer <ExpressionAST> |
		slotInitializer:: slot initializer apply: copier.
		aNode isSeq ifFalse: [slotInitializer:: futureFor: slotInitializer].
		aMsg:: MessageAST new 
			send: (setterSelectorFor: slot) with:  {slotInitializer};
			start: slot start;
			end: slot end.
		NormalSendAST new msg: aMsg; 
			recv: (selfNode start: aMsg start; end: aMsg end);
			start: aMsg start; end: aMsg end].

	(* populate subinit methods with slot initialization code *)
	(1 to: slots size) with: slots do: [:n :stmt |
		(subs at: n  // methodCompiler slotsPerMethod + 1) body statements add: stmt]. 

	stmts:: OrderedCollection new.
	(* Call the subinitializers for the slots *)
	1 to: subs size -1 do: 
		[:i | stmts add: (sendForSub: (subs at: i))].
	start:: stmts isEmpty ifTrue:[0] ifFalse:[stmts first start].
	end:: stmts isEmpty ifTrue:[0] ifFalse:[stmts last end].
	(* then the initializer code *)
	aNode initExprs do:
		[:ie <ExpressionAST> | stmts add: (ie apply: copier)]. 

	body:: CodeBodyAST new
		temporaries: OrderedCollection new
		statements: stmts;
		start: start; end: end.
		(* need to add per-class initializer as well *)
	subs at: subs size put: (MethodAST new 
		pattern: initHdr body: body accessModifier: #public;
		start: start; end: end).

	^subs
)
superFactoryCallFor: aNode <SendAST> ^ <NormalSendAST> = (
	(* Probably migrate to superclass *)
	(* create call to superclass initializer *)
	|
	var  <VariableAST> = VariableAST new name: #super; start: aNode start; end: aNode end.
	superInitMsg <MessageAST> = aNode msg apply: ASTCopier new.
	send <NormalSendAST> = NormalSendAST new to: var send: superInitMsg; start: aNode start; end: aNode end.
	|
      superInitMsg sel: (initializerSelectorNameFor: superInitMsg sel).
	^send
)
superMixinInitializerSelectorFor: selector <Symbol> number: i <Integer> ^<Symbol> = (
	^('mixinInitializer`',  i printString, '`', selector ) asSymbol
)
syntheticNameSeparator ^ <String> = (
  ^Language syntheticNameSeparator	
)
syntheticNameSeparatorCharacter ^ <Character> = (
  ^Language syntheticNameSeparatorCharacter	
)'factories'
makePrimaryFactoryFor: hdr <ClassHeaderAST> ^<MethodAST> = (

	(* Manufacture a method with the given class header's primary constructor's signature, and body that creates a new instance of the class and calls its initializer *)
	|
	cons <MessagePatternAST>
	start <Integer> end <Integer>
	
	newInstance <ExpressionAST>
	args <List[ExpressionAST]>
	initializerSelectorName <Symbol>
	msgFromConstructor <MessageAST>
	stmts <List[AST]>
	body <CodeBodyAST>
	|
	
	cons:: hdr constructor apply: ASTCopier new.
	start:: cons start.
	end:: cons end.

	newInstance:: NormalSendAST new 
		to: selfNode
		send: (MessageAST new 
			send: #basicNew with: {};
                  start: start; end: end); 
		start: start; end: end. (* self basicNew *)

	args:: hdr constructor parameters collect:
		[:vd <VarDeclAST> | 
		NormalSendAST new
			to: hereNode
			send: (MessageAST new
				send: vd name with: {};
				start: vd start; end: vd end);
		start: vd start; end: vd end].

	initializerSelectorName:: initializerSelectorNameFor: cons selector.
	msgFromConstructor:: MessageAST new send: initializerSelectorName with: args.
	msgFromConstructor start: start; end: end.

	stmts:: OrderedCollection new 
		add: (ReturnStatAST new expr: 
			(NormalSendAST new 
				to: newInstance 
				send: msgFromConstructor;
				start: start; end: end);
                   start: start; end: end);
		yourself.
		
	body:: CodeBodyAST new
		temporaries: OrderedCollection new
		statements: stmts;
		start: start; end: end.

	^MethodAST new
		pattern: cons body: body accessModifier: #public; 
		start: start; end: end.
)
private processFactoryFor: aNode <ClassAST> in: mixinMirror <LowLevelMixinMirror> = (
	processFactoryFor: aNode hdr inScope: (scopeMap at: aNode classSide) in: mixinMirror.
)
private processFactoryFor: aNode <ClassHeaderAST> inScope: s in: mixinMirror <LowLevelMixinMirror> = (
	
	| factoryAST <MethodAST> |
 	factoryAST:: makePrimaryFactoryFor: aNode.
	(processMethod: factoryAST inScope: s in: mixinMirror)
		metadata at: #isConstructor put: true.
)'instance initializers'
private allInitializersFor: aNode <ClassHeaderAST> ^<Collection[MethodAST]> = (

	^aNode slots size > methodCompiler slotsPerMethod
	   ifTrue: [splitInitializersFor: aNode]
	   ifFalse: [{classDeclarationInitializerFor: aNode. mixinInitializerFor: aNode}]
)
initializerSelectorNameFor: selector <Symbol> ^<Symbol> = (
	^('initializer`', selector) asSymbol
)
private processInitializerFor: aNode <ClassAST> in: mixinMirror <LowLevelMixinMirror> = (
	processInitializerFor: aNode hdr inScope: (scopeMap at: aNode instanceSide) in: mixinMirror.
)
private processInitializerFor: aNode <ClassHeaderAST> inScope: s <Scope> in: mixinMirror <LowLevelMixinMirror> = (

	| initializerASTs <List[MethodAST]> |
	#BOGUS yourself. (* The superclass clause is not a property of the mixin; this method is specific to the application. That said, all applications derived from the class declaration will share it *)
	processMethod: (superConstructorMethodFor: aNode) inScope:  s in: mixinMirror. 
 	initializerASTs:: allInitializersFor: aNode.
	initializerASTs do:
		[:init <MethodAST> | 
			(processMethod: init inScope: s in: mixinMirror) 
				metadata at: #isSubinitializer put: true
		]
)
sendForSub: sub <MethodAST> ^<NormalSendAST> = (

	(* Create a single call to a sub-initializer from the main initiializer *)
	| args <Collection[SendAST]> |
	args:: sub pattern parameters collect: [:arg | hereSendFrom: arg].
	
	^(NormalSendAST new
		to: selfNode
		send: (MessageAST new
			send: sub selector with: args;
			start: sub  start; end:   sub end))
	start: sub start; end: sub end				
)
subInitializer: n <Integer> for: aNode <ClassHeaderAST>  ^ <MethodAST> = (

	|  initHdr <MessagePatternAST> body <CodeBodyAST>  |
	initHdr:: aNode constructor apply: ASTCopier new. (* set up scope with constructor parameters *)
	initHdr selector: (subinitializer: n nameFor: aNode).
	body:: CodeBodyAST new
		temporaries: OrderedCollection new
		statements: OrderedCollection new;
		start: aNode start; end: aNode end.
	^MethodAST new
		pattern: initHdr body: body accessModifier: #private;
		start: aNode start; end: aNode end.
)
subInitializersFor: aNode <ClassHeaderAST> ^<Array[MethodAST]> = (
	
	| subs <Collection[MethodAST]>  numSubs <Integer>  |
	numSubs:: aNode slots size // methodCompiler slotsPerMethod + 1.
	subs:: Array new: numSubs + 1.
	1 to: numSubs do: [:n <Integer> | 
		subs at: n put: (subInitializer: n for: aNode)].
	^subs
)
subinitializer: n <Integer> nameFor: aNode <ClassHeaderAST> ^<Symbol> = (

	| prefix <String> suffix <String> fqn <String> |
	prefix:: aNode name, syntheticNameSeparator, n printString, syntheticNameSeparator, 'init'.
	suffix:: aNode constructor parameters size = 0 
                    ifTrue:['']
                    ifFalse:[
				aNode constructor parameters size = 1
					ifTrue:[':']
					ifFalse:[syntheticNameSeparator, aNode constructor selector]].
	^(prefix, suffix) asSymbol
)
superConstructorCallFor: aNode <ClassHeaderAST> ^<NormalSendAST> = (
	
	(* create call to super constructor method *)
	|
	var  <VariableAST> 
	send <NormalSendAST>
	start <Integer> 
	end <Integer>  
	superMsg <MessageAST>
	args <List[VariableAST]> 
	|

	start:: aNode superConstructorCall start.
	end:: aNode superConstructorCall end.
	var:: VariableAST new name: #self; start: start; end: end.
	args:: aNode constructor parameters collect:
		[:p <VarDeclAST> | hereSendFrom: p].
	superMsg:: MessageAST new 
		send: (superConstructorNameFor: aNode) with: args; 
		start: start; end: end.
	send:: NormalSendAST new to: var send: superMsg; start: start; end: end.	
	^send
)
superConstructorMethodFor: aNode <ClassHeaderAST> ^<MethodAST> = (
	| 
	start <Integer> 
	end <Integer>
	stmts <List[StmtAST]> 
	var  <VariableAST> 
	initHdr <MessagePatternAST>
	superMsg <MessageAST> 
	send <NormalSendAST>
	body  <CodeBodyAST>
	copier <ASTCopier>
	|
	start:: aNode superConstructorCall start.
	end:: aNode superConstructorCall end.
	copier:: ASTCopier new.
	initHdr:: aNode constructor apply: copier.
	initHdr selector: (superConstructorNameFor: aNode).
	(* create call to superclass initializer *)
	stmts:: OrderedCollection new. 
	var:: VariableAST new name: #super; start: start; end: end.
	superMsg:: aNode superConstructorCall apply: copier.
	superMsg sel: (initializerSelectorNameFor: superMsg sel).
	send:: NormalSendAST new to: var send: superMsg; start: start; end: end. 
	stmts addFirst: send.
	body:: CodeBodyAST new
		temporaries: OrderedCollection new
		statements: stmts;
		start: start; end: end.
	^MethodAST new 
		pattern: initHdr body: body accessModifier: #private;
		start: start; end: end
)
superConstructorNameFor: aNode <ClassHeaderAST> ^ <Symbol> = (
	^(aNode name, '`superInit`', aNode constructor selector) asSymbol 
)'make lexically visible'
scopeMap = (
	^super scopeMap
)'nested classes - private'
private accessorASTForNestedClassNamed: simpleName <String> fullName: nestedName <String> classTree: classTree <NormalSendAST> accessModifier: am <Symbol> ^<MethodAST> = (

(* Build an AST for a method that would lazily create a nested class via mixin application, and cache it. The generated code has the form

simpleName = (
	self nestedName`slot isNil ifTrue: [self nestedName`slot: (
		((<classTree>
		) enclosingObjectSlot: self
	)].
	^self nestedName`slot
)	

where <classTree> is the AST that computes the class.
*)

	|  nestedClassSlot mixinApplication
	  setEnclosingObject assignSlot nilCheck block if return |

	nestedClassSlot:: NormalSendAST new
		to: selfNode
		send: (namer mixinSlotNameFor: nestedName) asSymbol with: {}.	
	mixinApplication:: classTree.
	(* Set for both the class and the metaclass! *)
	setEnclosingObject:: NormalSendAST new
		to: mixinApplication
		send: #setEnclosingObjectSlot:
		with: {selfNode}.
	assignSlot:: NormalSendAST new
		to: selfNode
		send: ((namer mixinSlotNameFor: nestedName), ':') asSymbol
		with: {setEnclosingObject}.
	nilCheck:: NormalSendAST new
		to: nestedClassSlot
		send: #isNil
		with: {}.
	block:: BlockAST new
		body: (CodeBodyAST new
			parameters: {}
			temporaries: {}
			statements: {assignSlot}).
	if:: NormalSendAST new
		to: nilCheck
		send: #ifTrue:
		with: {block}.	
	return:: ReturnStatAST new expr: nestedClassSlot.
		
	^MethodAST new
		accessModifier: am;
		pattern: (MessagePatternAST new 
			selector:  simpleName asSymbol
			parameters: {});
		body: (CodeBodyAST new
		 	parameters: {}
			temporaries: {}
			statements: {if. return});
		start: 1; end: 1
)
addInitializersToMixinApplication: mixinApp <NormalSendAST> nestedMixin: nestedMixin factoryName: facName <Symbol> = (
(* This method produces an AST representing code of the form:

[ | mixinApp |
  mixinApp = .... (* incoming parameter *)
  mixinApp methodDictionary at: initName put: ((nestedMixin methodDictionary at: initName) clone methodClass: mixinApp).
  mixinApp class methodDictionary 
     at: factoryName 
     put: ((nestedMixin classMixin methodDictionary at: factoryName) clone methodCass: mixinApp class).
  mixinApp
] value

This code is then placed in the class accessor for a mixin application class.
Executing this code will copy the precompiled factory and 
instance initializer methods from the fake mixin nodes where they are stored at compile time (nestedMixin and nestedMixin's classMixin)
to the actual mixin application (mixinApp and mixinApp class) generated at run time.
*)
|
nestedMixin2 = nestedMixin apply: ASTCopier new.
mixinAppVarDecl = VarDeclAST name: #mixinApp type: nil.
mixinAppSlotDef = MutableSlotDefAST new slotDecl: mixinAppVarDecl; initializer: nil. (* | mixinApp |  *)
mixinAppVar1 = hereSendFrom: mixinAppVarDecl. (* mixinApp *)
mixinAppVar2 = hereSendFrom: mixinAppVarDecl.
mixinAppVar3 = hereSendFrom: mixinAppVarDecl.
mixinAppVar4 = hereSendFrom: mixinAppVarDecl.
mixinAppVar5 = hereSendFrom: mixinAppVarDecl.
classMixinMessage = MessageAST new send: #classMixin with: {}.
classMessage = MessageAST new send: #class with: {}.
methodDictMessage1 = MessageAST new send: #methodDictionary with: {}.
methodDictMessage2 = MessageAST new send: #methodDictionary with: {}.
methodDictMessage3 = MessageAST new send: #methodDictionary with: {}.
methodDictMessage4 = MessageAST new send: #methodDictionary with: {}.
valueMessage = MessageAST new send: #value with: {}.
nestedMixinClass = NormalSendAST new to: nestedMixin send: classMixinMessage. (* nestedMixin classMixin *)
mixinClassMtds = NormalSendAST new to: nestedMixinClass send: methodDictMessage1. 
(* nestedMixin classMixin methodDictionary*)
mixinInstMtds = NormalSendAST new to: nestedMixin2 send: methodDictMessage2. 
(* nestedMixin methodDictionary*)
mixinAppClass = NormalSendAST new to: mixinAppVar1 send: classMessage. (* mixinApp class*)
mixinAppClass2 = NormalSendAST new to: mixinAppVar5 send: classMessage.
classMtds = NormalSendAST new to: mixinAppClass send: methodDictMessage3. 
(* mixinApp class methodDictionary*)
instMtds = NormalSendAST new to: mixinAppVar3 send: methodDictMessage4. (* mixinApp methodDictionary*)
initName = SymbolAST new val: (initializerSelectorNameFor: facName). (* #'initializer`factoryName' *)
initMethod = NormalSendAST new to: mixinInstMtds send: #at: with:{initName}. 
(* nestedMixin methodDictionary at: #'initializer`factoryName' *)
initMethodClone =  NormalSendAST new to: initMethod send: #clone with:{}.
(* (nestedMixin methodDictionary at: #'initializer`factoryName') clone *)
initMethodCopy = NormalSendAST new to: initMethodClone send: #methodClass: with: {mixinAppVar4}.
(* (nestedMixin methodDictionary at: #'initializer`factoryName') clone methodClass: mixinApp *) 
factoryName = SymbolAST new val: facName.  (* #factoryName *)
factoryMethod = NormalSendAST new to: mixinClassMtds send: #at: with:{factoryName}.
(* nestedMixin classMixin methodDictionary at: #'factoryName *)
factoryMethodClone =  NormalSendAST new to: factoryMethod send: #clone with:{}.
(* (nestedMixin classMixin methodDictionary at: #'factoryName) clone *)
factoryMethodCopy = NormalSendAST new to: factoryMethodClone send: #methodClass: with: {mixinAppClass2}.
(* (nestedMixin classMixin methodDictionary at: #'factoryName) clone methodClass: mixinApp class *)
setMixinApp = NormalSendAST new to: hereNode send: #mixinApp: with:{mixinApp}.
setInstInit = NormalSendAST new to: instMtds send: #at:put: with:{initName. initMethodCopy}. 
(* mixinApp methodDictionary 
       at: #'initializer`factoryName' 
       put: ((nestedMixin methodDictionary at: #'initializer`factoryName')  clone methodClass: mixinApp) *)
setClassFactory = NormalSendAST new to: classMtds send: #at:put: with:{factoryName. factoryMethodCopy}.
(* mixinApp class methodDictionary 
       at: #factoryName
       put: ((nestedMixin classMixin methodDictionary at: #factoryName) clone methodClass: mixinApp class) *)
stmts = OrderedCollection new 
                  add: setMixinApp ; add: setInstInit; add: setClassFactory; add: mixinAppVar3; yourself.
blockBody = CodeBodyAST new temporaries: {mixinAppSlotDef} statements: stmts.
block = BlockAST new body: blockBody. (* [...] *)
result = NormalSendAST new to: block send: valueMessage. (* [...] value*)
|
^result
)
private astForComputingClassDeclaration: simpleName <String> fullName: nestedName <String> superCall: superCall <NormalSendAST> iList: iList <List[SendAST]> ^ <NormalSendAST> = (
(* Compute the tree for an ordinary class declaration whose superclass is a generalized mixin application. *)
	| 	
	nestedMixin = nestedMixinASTFor: nestedName.  
	superChain = inheritanceASTWIthSuperclass: superCall recv iList: iList.	
	|

	^NormalSendAST new
		to: nestedMixin
		send: #apply:withName:
		with: {
			superChain.
			NormalSendAST new
				to: (NormalSendAST new
					to: (SymbolAST new val: nestedName, '`')
					send: #,
					with: {NormalSendAST new
						to: (NormalSendAST new to: selfNode send: #hash with: {})
						send: #asString
						with: {}})
				send: #asSymbol
				with: {}
				}.
)
compileNestedClasses: nestedClasses <Collection[ClassDeclarationAST | MixinApplicationClassAST]> within: mixinMirror <LowLevelMixinMirror> ^ <List[MixinRep]> = (

| results <List[MixinRep]> = OrderedCollection new. cat <String> = mixinMirror category, '-nested'.  |
	nestedClasses do:
		[ :nc <ClassDeclarationAST | MixinApplicationClassAST> |  | nSlotName <String> |
		nc category: cat.
		nSlotName:: namer slotNameForNestedClassNamed: nc name 
		                   within: mixinMirror name.
		mixinMirror instVars addMirror: (InstanceVariableMirror named: nSlotName mutable: true accessModifier: #private).
		 nc isClassDeclarationNode ifTrue:[
			results add: (classNode: (nestedMixinWrapperFor: nc in: mixinMirror)). 
			(* make 'mixin' for nested class *)
			]
			ifFalse: [
				results add: (mixinApplicationClassNode: nc in: mixinMirror category: cat).
				].
		createNestedClassAccessorFrom: nc within: mixinMirror 
		].
	^results
)
compileNestedClassesOf: aNode <SideAST> within: mixinMirror <LowLevelMixinMirror> ^ <Collection[MixinRep]> = (
(* where do we check that nested classes do not conflict with each other, or with methods or slots? *)
	^compileNestedClasses: aNode nestedClasses within: mixinMirror
)
private createNestedClassAccessorFrom: classDecl <ClassDeclarationAST | MixinApplicationClassAST> within: mixinMirror <LowLevelMixinMirror> = (
 
(* Create an accessor method for the nested class represented by the incoming class tree.
This accessor will lazily generate the nested class when its enclosing instance is first asked for it.
*)
| n <String>  accessorString <String> ast <MethodAST> nestedName <String> hdr <ClassHeaderAST> classTree <NormalSendAST>  scope <Scope> |

n:: classDecl name.
nestedName:: namer fullyQualifySimpleName: n with: mixinMirror name.
classTree:: classDecl isMixinApplicationClassAST
	ifTrue:[
		scope:: scopeMap at: classDecl.
		astForComputingMixinApplicationClassDeclaration: n  
			fullName: nestedName 
			superCall: classDecl inheritanceChain first  
			iList: classDecl inheritanceChain allButFirst
			factoryName: classDecl constructor selector
		]
	ifFalse:[
		hdr:: classDecl hdr.
		scope:: (scopeMap at: classDecl instanceSide) superScope.
		astForComputingClassDeclaration: n 
			fullName: nestedName 
			superCall: hdr superCall
			iList: hdr inheritanceChain allButFirst.
		].
ast:: accessorASTForNestedClassNamed: n
		fullName: nestedName
		classTree: classTree
		accessModifier: classDecl accessModifier.
(processMethod: ast inScope: scope  in: mixinMirror)
	metadata at: #isNestedClassAccessor put: true.
)
private nestedMixinASTFor: nestedName <String> ^ <NormalSendAST> = (
| 
	mixin = NormalSendAST new
		to: MixinClassNode new
		send: #value
		with: {}.
	nestedMixins = NormalSendAST new
		to: mixin
		send: #nestedMixins
		with: {}.
|
^NormalSendAST new
		to: nestedMixins
		send: #at:
		with: {SymbolAST new val: 
			(namer mixinNameOfClassNamed: nestedName)}.
)
nestedMixinWrapperFor: nested <ClassDeclarationAST> in: outerClass <MixinMirror>  ^ <ClassDeclarationAST> = (

(* Wrap a class declaration to make it look like a nested mixin within outerClass should. This includes making the name be the fully qualified name, and hiding the superclass *)
| wrapper <ClassDeclarationAST> |

wrapper:: nested clone.
wrapper hdr: nested hdr clone.
wrapper hdr name: (namer fullyQualifySimpleName: nested name with: outerClass name).
^wrapper
)'private'
computeMixinFrom: aNode <ClassHeaderAST> ^ <CompiledMixinMirror> = (

	| mixinMirror <LowLevelMixinMirror> hdrString <String>  |
	
	mixinMirror::  LowLevelMixinMirror named: aNode name isMeta: false.
	
	aNode slots do: [:slot | mixinMirror instVars addMirror: 
		(InstanceVariableMirror named: slot name mutable: slot isMutableSlot accessModifier: slot accessModifier)].
	
	hdrString:: input contents copyFrom: aNode start to: aNode end.
	
	^(CompiledMixinMirror header: hdrString mirror: mixinMirror)
		category: aNode category;
		comment: (aNode classComment isNil ifTrue: [''] ifFalse: [aNode classComment])
)
getInstanceVariables: aClassHeaderNode <ClassHeaderAST> ^ <String> = (

 (* returns the language specific slots - e.g. instance variables as a string *)
^aClassHeaderNode  slots inject:'' into:[:s <String> :v <SlotDefAST>  | s, v name, ' '].
)
hereNode ^<VariableAST> = (
	(* Generates an AST representing the implicit receiver. We use a string so that we can represent a name that is not a legal identier *)
	
	^VariableAST new name: #'@here'; start: 0; end: 0.
)
hereSendFrom: arg <VarDeclAST> ^<MessageNode> = (
	^NormalSendAST new
		to: hereNode
		send: (MessageAST new 
			send: arg name
			with: {};
			start: arg start; end: arg end); 
		start: arg start; end: arg end
)
selfNode ^ <VariableAST> = (

(* generate a an AST representing self; used for implicit self sends *)
^VariableAST new name: #self; start: 0; end: 0
)'public access'
classNode: node <ClassDeclarationAST> ^ <MixinRep> = (
	(* type MixinRep = {CompiledMixinMirror. {MixinRep}} *)
	|
	mixinMirror <LowLevelMixinMirror>
	nestedClasses <Collection[MixinRep]>
	|
	assert: [node hdr category notNil] message: 'No category for class header'.
	node hdr slots do: [:each | checkForReservedWord: each name].
	checkForDuplicateNames: node.
	mixinMirror:: computeMixinFrom: node hdr.

	currentDepth::currentDepth + 1.
	nestedClasses:: processInstanceSideOf: node ofMixin: mixinMirror lowLevelMirror.
	generateSlotAccessorsFor: mixinMirror lowLevelMirror. (* must be called after nested classes are processed, so all synthetic slots have been added *)
	processClassSideOf: node ofMixin: mixinMirror lowLevelMirror classMixin.
	currentDepth:: currentDepth - 1.
	
	^{mixinMirror. nestedClasses}
)
compileClassHeader: src <ReadStream> of: classDecl <ClassDeclarationMirror> ^ <MixinRep> = (
	| rep <MixinRep> newMirror <LowLevelMixinMirror> oldMirror <LowLevelMixinMirror> enclosing <NS2ClassStencilMirror> |
		(* Works by appending () to src and compiling as complete class source
		Then adds existing non-synthetic methods and nested class accessors/slots to result *)	
	enclosing:: classDecl enclosingClass.
	enclosing isNil 
		ifTrue:[
			rep:: compileClassSource: (src contents, '()') readStream within: nil.
			newMirror:: rep first lowLevelMirror.
			] (* classDecl is top level class *)
		ifFalse:[
			rep:: compileClassSource: (src contents, '()') readStream within: enclosing instanceSide (* mixin *).
			newMirror:: rep last first first lowLevelMirror
			].(* classDecl is nested class *)
	classDecl instanceSide nestedClasses  do:[:nc <ClassStencilMirror> | | nSlotName <String> |
		nSlotName:: namer slotNameForNestedClassNamed: nc simpleName 
		                   within:  classDecl name.
		
		newMirror instVars addMirror: (InstanceVariableMirror named: nSlotName asSymbol mutable: true accessModifier: #public)
		].
	(* add existing nested class slots into result *)
	oldMirror:: classDecl compiledMixinMirror lowLevelMirror.
	oldMirror methods do:[:m <LowLevelMethodMirror>  | 
		(m isSynthetic not or:[m isNestedClassAccessor]) ifTrue:[newMirror methods addMirror: m]	
		].
	oldMirror classMixin methods do:[:m <LowLevelMethodMirror>  | 
		(m isSynthetic not or:[m isNestedClassAccessor]) ifTrue:[newMirror classMixin methods addMirror: m]	
		].
	(* copy existing methods and nested class accessors into result *)
	self generateSlotAccessorsFor: newMirror.
	(* reindex as changes in slots may have moved synthetic slots for nested classes *)
	^rep
)
public compileExpressionSource: src <ReadStream | String> inContext: aContext <MethodContext> inMixin: enclosing <MixinMirror> ^<LowLevelMethodMirror>
= (
	^compileExpressionSource: src inContext: aContext inMixin: enclosing allowBlackMarket: false
)
public compileMethodSource: src <ReadStream | String> within: enclosing <MixinMirror>  ^<LowLevelMethodMirror> = (
	| tree <AST> result <LowLevelMethodMirror> stm <ReadStream> | 
	#BOGUS yourself. (* Make sure we take the line below out when new src control is in use *)
	src isString ifTrue: [stm:: src readStream] ifFalse: [stm:: src].
	tree:: parser methodDecl parse: stm.
	saveInput: stm.
	setScopeFor: tree in:  (enclosing). (* scope construction phase *)
	result::methodNode: tree. (* rewrite and compile *)
	scopeMap:: Dictionary new.
	^result
)
public compileNestedClass: nc <ClassDeclarationAST> within: mixinMirror <LowLevelMixinMirror> ^ <List[MixinRep]> = (
	^compileNestedClasses: (OrderedCollection with: nc) within: mixinMirror.
)'scopes'
currentScope ^ <Scope> = (
	^scopeStack last
)
nestScope: s <Scope> = (
	(* Hook up a new lexically nested scope, and push onto the scope stack *)
	s setSuperScope: currentScope.
	pushScope: s
)
popScope ^<Scope> = (
	^scopeStack removeLast
)
pushScope: s <Scope> = (
	scopeStack addLast: s
)
setScopeFor: node <AST> in: enclosing <MixinMirror | nil> = (
	| scopeBuilder |
	pushScope: Scope new.
	enclosing isNil ifFalse: [nestScope:: ScopeBuilder new buildScopeFor: enclosing].
	currentDepth:: currentScope depth.
	scopeBuilder:: ScopeBuilder nestedIn: currentScope atLevel: currentDepth.
	node apply: scopeBuilder
)) : ()
class DebugMappedLocalEntry name: nom mapper: mapper = SymbolTableEntry forAST: nil atDepth: nil ((* Describe the class in this comment. *)|
	name = nom.
	debugMapper = mapper.
|)
('as yet unclassified'
getterAst = (
	^debugMapper getterAstFor: name.
)
isDebugMappedLocalEntry = (
	^true
)
isMessagePatternNode = (
	^false
)
isMethodNode = (
	^false
)
isVarDeclNode = (
	^false
)
setterAst: newVal = (
	^debugMapper setterAstFor: name putting: newVal.
)) : ()'as yet unclassified'
PERFORMANCE_NOTE = (
	(* The compiler could generate better code in the following areas:
	* Setter sends evaluated for effect.
	* Literals evaluated for effect (e.g., the BOGUS marker).
	* Inlining ifNil: etc
	* Inlining and:/or:
	* Eliot suggested a way to implement setter sends without adding another temp. The value to be saved could be dup'd and later accessed by tempAt, relying on the fact that tempAt is not bounds checked. This might also be easier to decompile. (Although note absent receiver here sends will allow a dup-pop pattern similar to cascades.)
	* When possible, it should not generate subinitializers, which involve at least two extra sends per mixin to instainate simple objects.
	*)
)
assert: c = (
	self assert: c message: 'Assert failed'.
)'make lexically visible'
Scope = (
	^super Scope
)
SlotEntry = (
	^super SlotEntry
)
SymbolTableEntry = (
	^super SymbolTableEntry
)
namer = (
	^super namer
)) : ()