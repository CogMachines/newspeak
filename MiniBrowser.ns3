Newspeak3
'Samples'
class MiniBrowser packageUsing: manifest = ()
(
class Browsing usingPlatform: p = (|
	Subject = p hopscotch core Subject.
	Presenter = p hopscotch core Presenter.
	TextEditorFragment = p hopscotch fragments TextEditorFragment.
	Color = p graphics Color.
|)
(
class ClassPresenter onSubject: s = Presenter onSubject: s ()
('as yet unclassified'
definition = (
	^column: {
		(column: {
			link: subject model name action: [enterSubject:: ClassSubject onModel: subject model (* Why do we need a copy? *)].
			label: 'Slots'.
			row: {
				mediumBlank.
				column: (subject model instanceSide slots collect: [:ea | label: ea name]).
			}.
		}) color: (Color r: 0.855 g: 0.855 b: 0.9).
		(row: {label: 'Classes'}) color:  (Color r: 0.921 g: 0.921 b: 0.94).
		row: {
			mediumBlank.
			(column: (subject model instanceSide nestedClasses collect: [:ea | 
				collapsed: [link: ea name action: [enterSubject:: ClassSubject onModel: ea]]
				expanded: [(ClassSubject onModel: ea) presenter]])) elasticity: 1.
		}.
		(row: {label: 'Methods'}) color:  (Color r: 0.921 g: 0.921 b: 0.94).
		row: {
			mediumBlank.
			(column: (subject model instanceSide methods collect: [:ea | method: ea name])) elasticity: 1.
		}.
		(row: {label: 'Class Methods'}) color:  (Color r: 0.921 g: 0.921 b: 0.94).
		row: {
			mediumBlank.
			(column: (subject model classSide methods collect: [:ea | method: ea name])) elasticity: 1.
		}.
	}
)
method: name = (
	| toggle |
	toggle:: 
		collapsed: [link: name action: [toggle expand]]
		expanded: [column: {link: name action: [toggle collapse]. TextEditorFragment new}].
	^toggle
)) : ()
class ClassSubject onModel: m = Subject onModel: m (|
|)
('as yet unclassified'
createPresenter = (
	^ClassPresenter onSubject: self
)
title = (
	^model name
)) : ()) : ()'as yet unclassified'
main: platform args: args = (
	| 
	browsing = Browsing usingPlatform: platform.
	model = platform mirrors ClassDeclarationMirror reflecting: platform hopscotch yourself class mixin.
	|
	platform hopscotch core HopscotchWindow openSubject: (browsing ClassSubject onModel: model)
)) : ()